; <html><head><title>Copies refinement files to cluster </title></head><body><pre>
; send.pam                ArDean Leith    June 2002
; Source location: /net/sicily/usr1/leith/clus/spahn/refine/pubsub 
;
; INPUT:
; dala{***group}                     Stack files
; data{***group}                     Stack files
; final/defgrp{***x77}/trans{***x56} Doc. files
; order_select                       Doc. file
; vol{***iter}                       Starting volume
; ctfs                               CTF Starting file   
;
; OUTPUT:
; output/dala{**iter}_{***group}      Stack files
; output/data{***group}               Stack files
; output/trans{**iter}_{***group}     Doc. files
; output/order_select                 Doc. file
;
;.....................may have to edit these names ..................

FR L             ; data input directory? 
<dir_in>/net/sylt/usr11/spahn/Super-EF2

FR L             ; output directory?
<dir_out>leith@patroon:~leith/spahn/input

FR L             ; order_select input filename? 
<order_select_in>order_select

FR L             ; ctf input filename?
<ctf_in>ctf/ctfs

FR L             ; volume input filename template?
<vol_in>final/vol{***x56}

FR L             ; trans input filename template?
<trans_in>final/defgrp{***x77}/trans{***x56}

FR L             ; dala input stack template?
<dala_in>dala{***x77}

FR L             ; data input stack template? 
<data_in>data{***x77}


; --------------------- edit these registers ------------------

x56 = 1        ; starting  iteration
x65 = 3        ; starting  defocus group
x66 = 43       ; ending    defocus group

; ------------- no need to edit for default refinement ----------

FR L           ; vol output file template? 
<vol_out> vol{**x56}

FR L           ; dala output? 
<dala_out> dala{**x56}_{***x77}

FR L           ; ?trans output? 
<trans_out> trans{**x56}_{***x77}

; ------------------------ operations------------------------------

VM           ;  - order_select input
scp <dir_in>/<order_select_in>.$DATEXT  <dir_out>     


VM           ; final/vol input 
scp <dir_in>/<vol_in>.$DATEXT  <dir_out>/<vol_out>.$DATEXT     


VM           ; ctfs input
scp <dir_in>/<ctf_in>.$DATEXT  <dir_out>     


DO LB1 x77=x65,x66   ; loop over defocus groups -------------------

   VM        ; dala input
   scp <dir_in>/<dala_in>.$DATEXT <dir_out>/<dala_out>.$DATEXT    


   VM        ; data input
   scp <dir_in>/<data_in>.$DATEXT <dir_out>    

   VM        ; trans input
   scp <dir_in>/<trans_in>.$DATEXT <dir_out>/<trans_out>.$DATEXT     

LB1

EN
; </body></pre></html>
