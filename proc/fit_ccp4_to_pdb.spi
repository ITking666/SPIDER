; fit_ccp4_to_pdb.spi

; THIS PROCEDURE ORIENTS AND SHIFTS A SPIDER VOLUME THAT HAS BEEN CONVERTED TO
; A BRIX VOLUME BY "CP TO BRIX" AND HAS BEEN USED (THE BRIX VOLUME) TO
; FIT A PDB STRUCTURE USING "O" OR REAL SPACE REFINEMENT (RSR). THE OUTPUT 
; OF THIS PROCEDURE IS A CCP4 VOLUME WHICH IS AT THE CORRECT LOCATION AND 
; ORIENTATION AS THAT OF THE FITTED PDB STRUCTURE. THIS CAN BE VISUALIZED 
; USING "O" OR "CHIMERA" OR "PYMOL".
 
; ALTERNATIVELY, 

; (1) ONE CAN ALSO ORIENT AND SHIFT THE FITTED PDB FILE TO FIT THE CCP4 
; VOLUME WHICH IS GENERATED FROM THE UNROTATED AND UNSHIFTED SPIDER VOLUME,
; WHICH CAN BE VISUALIZED BY USING "O" OR "CHIMERA" OR "PYMOL". USE PROCEDURE 
; "fit_pdb_to_ccp4.spi" IN THE SPIDER PROCEDURE DIRECTORY"spider/proc/".


; (2) ONE CAN USE "CP TO BRIX" TO CONVERT THE UNROTATED AND UNSHIFTED 
; SPIDER VOLUME TO A BRIX VOLUME WHICH CAN BE USED TO VISUALIZE THE
; FITTED PDB STRUCTURE IN "O" OR "CHIMERA" OR "PYMOL".


 
 
;;;;;;;;;;;;;;;;;;;;;;;BEGINING OF INPUT PARAMETERS;;;;;;;;;;;;;;;;;;;

FR L
?INPUT SPIDER VOLUME NAME ?<1>
vol_spi

FR L
?OUTPUT CCP4 VOLUME NAME ?<2>
vol_ccp4.ccp4

;PIXEL SIZE "X20"
X20 = 2.82

;DATA LENGTH FOR CCP4 FILE "X21"
X21 = 32

;;;;;;;;;;;;;;;;;;;;;;;END OF INPUT PARAMETERS;;;;;;;;;;;;;;;;;;;;;;;;;



; ROTATE THE VOLUME SO THAT X = Y, Y = X AND Z = -Z
RT 3D
<1>
_1
90,0,0


RT 3D
_1
_2
0,180,0


RT 3D
_2
_3
180,0,0

; FIND NSAM, NROW AND NSLICE
FI [NSAM],[NROW],[NSLICE]
<1>
12,2,1

; FIND THE CENTER IN PIXELS, FOR EVEN DIMENSION, IT IS NSAM/2 ETC..
; FOR ODD DIMENSION, IT IS (NSAM/2 + 1) ETC..

X25 = INT([NSAM]/2 )
X26 = INT([NROW]/2 )
X27 = INT([NSLICE]/2 )

X30 = ([NSAM]  - 2*X25)   + X25
X31 = ([NROW]  - 2*X26)   + X26
X32 = ([NSLICE]- 2*X27)   + X27

; CALCULATE THE CENTER IN ANGSTROMS
X35 = X30 * X20
X36 = X31 * X20
X37 = X32 * X20

; CREATE THE CCP4 FILE
; NOTE: FOR DISPLAY, "CHIMERA" READS THE ORIGIN WHEREAS "O" READS THE STARTING 
; POINTS OF XYZ. ORIGIN IS GIVEN IN ANGSTROMS, STARTING POINTS ARE GIVEN IN PIXELS.
CP TO CCP4
_3
<2> 
X21
-X30,-X31,-X32
X20,X20,X20    
-X35,-X36,-X37
 
EN

