; <html><head><title>Normalizes stacks and creates star file</title></head><body><pre>
;
; PURPOSE: Normalizes groups of spider stacks and creates 'star' file for relion.
;  
; SOURCE:  denorm_n_star.spi  
; 
; USAGE:   spider spi/mi2 @denorm_n_star

 [id] = '1'                              ; New stack id letter

; ------------------ Input files -----------------------

 [sel_grp]   = 'sel_group'                   ; Group selection file

 [sel_part]  = 'Alignment/sel_particles_{***[grp]}'  ; Particle selection files (one/group) 

 [part]      = 'Alignment/data{***[grp]}'    ; Particle stacks (one/group)

; ----------------- Output files -----------------------

 [starfile]  = 'relion_[id]_data.star'       ; New star files (one) 

 [norm_part] = 'input/data_norm_{***[grp]}'  ; New particle stacks (one / group)

 [select]    = '{*****[n]}@[norm_part]'

; -------------- END BATCH HEADER -------------------------------

 MD
   VB OFF

 VM
   \rm [starfile]
 VM 
   echo data_                    >> [starfile]
 VM 
   echo loop_                    >> [starfile]
 VM 
   echo _rlnImageName            >> [starfile]
 VM 
   echo _rlnDefocusU             >> [starfile]
 VM 
   echo _rlnDefocusV             >> [starfile]
 VM 
   echo _rlnDefocusAngle         >> [starfile]
 VM 
   echo _rlnVoltage              >> [starfile]
 VM 
   echo _rlnAmplitudeContrast    >> [starfile]
 VM 
   echo _rlnSphericalAberration  >> [starfile]


 [ngrps] = 0
 [ntot]  = 0

 DO                                ; Loop over all groups ------------

   UD NEXT [key], [grp],[np],[def] ; Get input group number
     [sel_grp]                     ; Group selection doc file (input)
   IF ([key] .LE. 0) EXIT

   VM
     echo ' '"Group: {***[grp]}   Images: {*****[np]}   Defocus: {*****[def]}"
   
   [ntot]  = [ntot]  + [np]
   [ngrps] = [ngrps] + 1

   NORM
     [part]@*                      ; Spider stack file     (input)
     [sel_part]                    ; Selection file        (input)
     *                             ; No mask file      
     [norm_part]@*                 ; Spider stack file     (output)
     N                             ; No flat field

   DO                              ; Loop over all particles ------------

     UD NEXT [key], [n]            ; Get input image number
       [sel_part]                  ; Selection file        (input)
     IF ([key] .LE. 0) EXIT

     VM
       echo [select].spi  {*****[def]} {*****[def]} 0.0 200 0.1 2.00 >> [starfile]
   ENDDO

   UD NEXT E                       ; Get input image number
     [sel_part]                    ; Selection file        (input)

 ENDDO

 UD NEXT E                       ; Get input image number
   [sel_grp]                     ; Group selection doc file (input)

    VM
     echo ' '"Groups: {***[ngrps]} Images: {*******[ntot]} "
EN D



 FS
   [part]@2                     ; Spider stack file     (input)
 FS
   [norm_part]@2                ; Spider stack file     (input)
