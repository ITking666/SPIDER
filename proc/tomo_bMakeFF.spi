;spi/bMakeFF.spi 
;PROGRAM CREATES A CUBIC 3-D Fourier Filter FILE BASED ON THE JEOL4000
;THE 3-D CTF IS controlled by the underfocus setting in angstroems of THE JEOL4000
;RECONSTRUCTION IS DONE VIA BP 3D WITH THE SNR VALUE SUPPLIED BY X50
;FINAL VOLUME IS FILTERED VIA LOW AND HIGH PASS FILTER STEPS WITH FERMI FALL OFFS 
;**********************************************************************
MD
SET MP
0
;DESCRIBE THE TRANSFER FUNCTION  *********************************************************
X20=52	 	;ARRAY DIMENSION IN PIXELS
X30=10.OE4	;DEFOCUS IN ANGSTROEMS 1UM = 1E4 ANGSTROEMS
X31=10.6	;PIXEL SIZE IN ANGSTROEMS

;DESCRIBE THE FOURIER FILTER PARAMETERS (WILL USE FERMI FALL OFF) ***********************
X70=80		;LOW PASS FILTER RADIUS IN ANGSTROEMS
X71=350		;HIGH PASS FILTER RADIUS IN ANGSTROEMS
X72=0.05	;FERMI FALL OFF FACTOR FOR LOW PASS FILTER
X73=0.001		;FERMI RISE FACTOR FOR HIGH PASS FILTER

;DESCRIBE THE ANGULAR SAMPLING FOR THE TOMOGRAM ******************************
x60=-90		;START ANGLE	DOUBLE AXIS WILL HAVE IDENTICAL TILT ANGLES
x61=90		;END ANGLE
x62=1		;INCREMENT
X63=1		;NUMBER OF TILT SETS 1 = SINGLE AXIS; 2 = DOUBLE AXIS; 3 = USE FILE

FR G
[IN_ANG]ANG001		;IF USING AN EXTERNAL FILE (X63 = 3) GIVE NAME HERE
;SET OUTPUT FILES ********************************************************
FR G
[OUT_3D_FF]t4_array_ff001
;********** REAL VERSION WILL HAVE "-R" APPENDED TO NAME **************************
FR G
[REAL_3D_FF][OUT_3D_FF]-amp
FR G
[ROT_AVE_DOC][OUT_3D_FF]-rot_doc
FR G
[ROT_AVE_IMG][OUT_3D_FF]-rot_img



;*********** CREATE THE 2-D CTF FUNCTION THAT WILL BE USED TO CREATE THE
;	     3-D FOURIER FILTER

X40=1/(2*X31)	;1/2*ANGSTROMS PER PIXEL
TF C
_20
1.8		;SPHERICAL ABERATION CONSTANT MM
X30, 0.0164	;DEFOCUS(angstroems), LAMBDA
X20,X20		;DIMENSION OF REAL SPACE ARRAY
X40		;MAXIMUM SPACIAL FREQUENCY 1/(2*PIXEL SIZE)
0.005, 51.9 	;SOURCE SIZE (1/ANGSTROEMS), DEFOCUS SPREAD (ANGSTROEMS)
0,0		;ASTIGMATISM (ANGSTROEMS), AZIMUTH (DEGREES)
0.07,0.15	;AMPLITUDE CONTRAST RATIO, GAUSSIAN ENVELOPE HALFWIDTH
-1		;SIGN

;********************************************************************************
;*********      NOW CREATE A TOMOGRAM FROM THE 2-D CTF
x69=X63*((X61-X60)/X62+1)			;NUMBER OF ANGLES SAMPLED
IF(X63.EQ.3)THEN		;READ [IN_ANG] IF SUPPLIED
UD N,X69
[IN_ANG]
ENDIF
MS				;MAKE AN IN LINE STACK OF THE 2-D CTF
_10@
X20,X20,1
X69

SD IC NEW			;CREATE THE IN CORE ANG FILE
IN_CORE_ANG
3,X69

X23=INT(X20/2)			;HALF DIMENSION OF FOURIER FILTER

FT
_20
_1
;NEED TO ADJUST THE ORIGIN OF THE PSF ************************************
SH
_1
_2
X23,X23
;COPY THE 2-D CTF TO X69 IMAGES ********************************************
DO LB1 X90=1,X69
    CP
    _2
    _10@{***X90}
    IF(X63.EQ.1)THEN		;IF X63 = 1 THEN ANGLES GO SIMPLY FROM X60 TO X61
        X64=0			;PHI
        X65=X60+(X90-1)*X62	;THETA
	X66=0			;PSI
        SD IC X90,X64,X65,X66
        IN_CORE_ANG
    ELSE
       IF(X63.EQ.2)THEN			; X63 = 2 THEN TWO SETS OF ANGLES 
            X64=90			;PHI
            X65=X60+(X90-1)*X62         ;THETA
            IF(X65.GT.X61)X65=X65-(X61+X62-X60)	;THETA IF X65 > X61
            X66=0			;PSI
            SD IC X90,X64,X65,X66
            IN_CORE_ANG
       ENDIF
       IF(X63.EQ.3)THEN			;IF X63 = 3 THEN COPY [IN_ANG]
          UD X90,X64,X65,X66
          [IN_ANG]
          SD IC X90,X64,X65,X66
          IN_CORE_ANG
       ENDIF
    ENDIF
LB1
;NOW DO THE RECONSTRUCTION *************************************************
BP 3D
_10@***
1-X69
IN_CORE_ANG
X20,X20,X20
1,X20
10E8
_1
; ADJUST THE ORIGIN OF THE PSF FOR FT'ING BACK TO CTF **********************
SH
_1
_2
-X23,-X23,-X24

FT
_2
_1
;****************************************************************************
;	APPLY LOW AND HIGH PASS FILTER
X74=X31/X70		;LOW PASS FILTER RADIUS IN FREQUENCY UNITS 1/PIXELS
X75=X31/X71		;HIGH PASS FILTER RADIUS IN FREQUENCY UNITS 1/PIXELS

FF	;FIRST APPLY THE LOW PASS FILTER				
_1
_2
(5)	;CODE FOR FERMI LOW PASS FILTER
X74	;LOW PASS FILTER RADIUS 
X72	;FERMI FALL OFF FACTOR

FF	;SECOND APPLY THE HIGH PASS FILTER
_2
[OUT_3D_FF]
(6)	;CODE FOR FERMI HIGH PASS FILTER
X75	;HIGH PASS FILTER RADIUS
X73	;FERMI RISE FACTOR

PW
[OUT_3D_FF]
[REAL_3D_FF]

;RO I
;[REAL_3D_FF]
;[ROT_AVE_IMG]

RO
[REAL_3D_FF]
_1
LI D
_1
[ROT_AVE_DOC]
I

SD IC COPY
IN_CORE_ANG
TEST_ANG
EN

