; fit_pdb_to_ccp4.spi

; THIS PROCEDURE ORIENTS AND SHIFTS A FITTED PDB FILE THAT HAS BEEN USED TO
; FIT A BRIX VOLUME CREATED FROM A SPIDER VOLUME BY "CP TO BRIX". THE 
; FITTING IS DONE USING "O" OR REAL SPACE REFINEMENT (RSR). THE OUTPUT
; PDB FILE WILL BE AT THE CORRECT POSITION AND ORIENTATION OF THE CCP4
; VOLUME THAT IS ALSO GENERATED BY THIS PROCEDURE. ONE CAN VISUALIZE
; THE CCP4 VOLUME AND THE MODIFIED PDB FILE BY USING "O" OR "CHIMERA" OR "PYMOL". 
; ADDITIONALLY, THE PROCEDURE ALSO CREATES A BRIX VOLUME FROM THE SPIDER VOLUME
; WHICH CAN BE VIEWED ALONG WITH THE UNMODIFIED PDB FILE USING "O" OR
; "CHIMERA" OR "PYMOL".  

; ALTERNATIVELY, 

; (1) ONE CAN ALSO ORIENT AND SHIFT THE SPIDER VOLUME, THEN THE OUTPUT 
; CAN BE CONVERTED TO A CCP4 VOLUME THAT WILL BE AT THE CORRECT LOCATION 
; AND ORIENTATION AS THAT OF THE FITTED PDB STRUCTURE. THIS CAN BE VISUALIZED 
; USING "O" OR "CHIMERA" OR "PYMOL". USE PROCEDURE "fit_ccp4_to_pdb.spi" IN THE SPIDER
; PROCEDURE DIRECTORY "spider/proc/".
 

; (2) ONE CAN USE "CP TO BRIX" TO CONVERT THE SPIDER VOLUME TO A BRIX VOLUME 
; WHICH CAN BE USED TO VISUALIZE THE UNMODIFIED FITTED PDB STRUCTURE 
; IN "O" OR "CHIMERA" OR "PYMOL".



FR L
?INPUT SPIDER VOLUME NAME ?<1>
vol_spi

FR L
?OUTPUT CCP4 VOLUME NAME ?<2>
VOL_CCP4.ccp4

FR L
?OUTPUT BRIX VOLUME NAME ?<3>
vol_brix.brix


;PIXEL SIZE "X20"
X20 = 2.82

;DATA LENGTH FOR CCP4 FILE "X21"
X21 = 32

; CREATE THE CCP4 FILE
; NOTE: FOR DISPLAY, "CHIMERA" READS THE ORIGIN WHEREAS "O" READS THE STARTING POINTS OF XYZ.
; ORIGIN IS GIVEN IN ANGSTROMS, STARTING POINTS ARE GIVEN IN PIXELS.
CP TO CCP4
<1> 
<2>
X21
1,1,1
X20,X20,X20    
1,1,1

CP TO BRIX
<1> 
<3>
X20
N 
 
; FIND NSAM, NROW AND NSLICE
FI [NSAM],[NROW],[NSLICE]
<1>
12,2,1

; FIND THE CENTER IN PIXELS, FOR EVEN DIMENSION, IT IS NSAM/2 ETC..
; FOR ODD DIMENSION, IT IS (NSAM/2 + 1) ETC..

X25 = INT([NSAM]/2 )
X26 = INT([NROW]/2 )
X27 = INT([NSLICE]/2 )

X30 = ([NSAM]  - 2*X25)   + X25
X31 = ([NROW]  - 2*X26)   + X26
X32 = ([NSLICE]- 2*X27)   + X27

; CALCULATE THE CENTER IN ANGSTROMS
X35 = X30 * X20
X36 = X31 * X20
X37 = X32 * X20

X35
X36
X37

; IMPORTANT NOTE : 
; YOU SHOULD RUN THIS PROGRAM ONCE AND THEN LOOK AT THE 
; RESULTS FILE TO FIND THE VALUES OF X35, X36, X37. YOU HAVE TO RERUN THE 
; PROCEDURE AFTER, 1) UNCOMMENTING THE FOLLOWING COMMANDS, 2) EDITING THE 
; INPUT AND OUTPUT PDB FILE NAMES, AND 3) REPLACING THE VALUES OF X35,X36,X37.

;VM
;/net/bali/usr1/spider/docs/tips/pdbfit.py input_fitted.pdb output_modified.pdb trans=X35,X36,-X37
 
 
 
 
EN
