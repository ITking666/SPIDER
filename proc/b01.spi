;
; SOURCE:  b01.spi    ArDean Leith July 2001
;
; PURPOSE: Test retrieval of SPIDER register variable
;          and procedure communication         
;
; USAGE:   spider spi @b01
; 
; --------------------------------------------------

  [n]=12            ; Test retrieval of SPIDER register variable         
  [n]               ; List SPIDER register variable

  FR L              ; Set a SPIDER symbolic parameter         
    ?File name?[f1] ; 
    jnk

  MO                ; Create model test file using variable
    jnk{***[n]}     ; File name
    64,64           ; File dimensions
    T               ; Model type

  IQ FI [exists]    ; SPIDER pipe test
    jnk012          ; test existance of this file

  ; Conditional expression test example
  IF ([exists] .LE. 0) THEN
     SYS 
       echo "  File: [f1] does not exist" 
  ELSE
     SYS 
       echo "  File: [f1] exists" 
  ENDIF

  [maxsum] = 0        ; Set a SPIDER register variable

  DO [it]=1,2         ; File loop example

     @b02([it],[ret]) ; invoke a native SPIDER procedure

     SYS              ; Echo procedure return value
       echo "  On Iter: {**[it]} Procedure: b02 returned: {***[ret]}"

     [maxsum]=[maxsum]+[ret]  ; Create sum of maximum values
  ENDDO

  [maxsum]                    ; Echo maximum sum to results file

  EN               ; End SPIDER session



