
 ; SOURCE: /usr8/spider/docs/exa/fd.spi
 ; USAGE:  clean ; spider spi/dat @fd

 ; ----------------- Input files -----------------------------------------

 [img]         = 'rib_879'   ; Ribosome image

 [ctf_cor_doc] = 'fd_doc'    ; CTF correction doc file

 ; ----------------- Output files ---------------------------------------- 
                                  
 [outimg]      = 'fd'        ; CTF-corrected image
  
 ; ----------------- END BATCH HEADER ------------------------------------ 


 SYS
  echo ' Apply CTF filter xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'

 ; Find image size
 FI H [size]                       ; Find image size
   [img]                           ; Original image         (input)
   NX

 [pad-size] = 2*[size]             ; Padded size

 ; Pad Image with right & bottom border
 PD                                ; Pad Image 2X
   [img]                           ; Image                   (input)
   _1                              ; Padded image            (output)
   [pad-size],[pad-size]           ; Padded size
   B                               ; Use border average for background
   1,1                             ; Top-left coords

 ; Filter using CTF correction doc file
 FD                                ; Filter  
   _1                              ; Padded image            (input)
   _2                              ; CTF corrected image     (output)
  [ctf_cor_doc]                    ; CTF correction doc file (input)

 ; Window back to original size
 WI                                ; Window  
   _2                              ; CTF corrected image     (input)
   [outimg]                        ; CTF corrected image     (output)
   [size],[size]                   ; X,Y dimensions
   1,1                             ; Top-left coords
 
 
 EN



 CP TO JPG
   [img]                           ; Image         (input)
   [img]                           ; JPEG Image    (output)
 CP TO JPG
   [outimg]                        ; Image         (input)
   [outimg]                        ; JPEG Image    (output)

 SYS
   display [img].jpg      &
 SYS
   display [outimg].jpg   &

 EN
