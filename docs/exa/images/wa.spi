
 ; PURPOSE:  Tests 'WA' use from Microscope distortion file                                  
                                                                                            
 ; USAGE: spider txt @testwa                                                                 
                                                                                            
 ; ----------------- Input files -------------------------------------                       

 [warp_doc]     = 'sav_warpdoc'      ; Warping doc file

 ; ----------------- Output files ----------------------------------                         

 [unwarped_img] = 'wa_chess'         ; Image file

 [warped_img]   = 'wa_warped'        ; Warped image file

 ; ----------------- END BATCH HEADER ---------------------------- 

 MD
   TR OFF                  ; Decreases results file output
 MD                                                      
   SET MP                                                  
   0                       ; Use all processors           

 [nx] = 1024
 [ny] = 1024

 ; Create grid pattern -----------------------------

 [dist] = 40                ; Grid line seperation distance

 BL
   [unwarped_img]           ; Test image chessboard   (output)
   [nx],[ny]                ; Size                            
   N                        ; Do not use average              
   0.0                      ; Background                      

 DO [yloc] = 10,1020,[dist]
   PT                      
     [unwarped_img]         ; Test image chessboard   (output)
     L                      ; Pattern code                    
     1,[yloc]               ; Starting point                  
     [nx],[yloc]            ; Ending point                    
     N                      ; Do not continue                 
 ENDDO                                                      

 DO [xloc] = 10,1020,[dist]
   PT                      
     [unwarped_img]         ; Test image chessboard   (output)
     L                      ; Pattern code                    
     [xloc],1               ; Starting point                  
     [xloc],[ny]            ; Ending point                    
     N                      ; Do not continue                 
 ENDDO                                                      

 lb99

 VM 
   echo ' Warping: [unwarped_img] ------------------------ '

 WA                         ; Two pass mesh warping
   [warp_doc]               ; Doc file with warp info  (input)
   [unwarped_img]           ; File to be warped        (input)
   [warped_img]             ; Warped file              (output)

 FS
   [warped_img]             ; Warped file              (output)



 CP TO JPG
   [unwarped_img]           ; File to be warped        (input)
   [unwarped_img]           ; File to be warped        (input)

 CP TO JPG
   [warped_img]             ; Warped file              (output)
   [warped_img]             ; Warped file              (output)

 EN
