 ; SOURCE: /usr8/spider/docs/exa/apca.spi 
 ; AUTHOR:  ArDean Leith   Feb 2014
 ; USAGE:   spider spi/dat @apca 

 ;  ----------------- Input files -----------------------------------------UNFINISHED

  [in]    = 'sav_face_rtsh_stk@***' ; Rotated shifted particle stack  (output)

 ;  ----------------- Output files ----------------------------------------

 [outsel] = 'apca_sel_doc_**'       ; Image selection doc file

 [outali] = 'apca_doc'              ; Alignment doc file

 ;  ----------------- END BATCH HEADER ------------------------------------

 
 SYS
   echo " Testing  'AP C' -----------------------------------------" ; echo

 DE
   [outsel]         ; Selection doc files        (removed)
 DE
   [outali]         ; Alignment doc file         (removed)

 AP C               ; Alignment & Classification 
   [in]             ; Data file stack            (input)
   1-80             ; Input images
   5, 29, 1         ; First & last rings, ring skip
   F                ; Full ring
   N                ; No normalization under mask
   8,60             ; Groups and iterations
   0                ; Do not check mirrored positions
   [outsel]         ; Group selection doc files   (output)
   [outali]         ; Alignment doc file          (output)
 
 EN


 
 ! ----------------------- UNUSED BELOW HERE -------------------------
 
 SYS
   echo " Creating rotated image set -- 80 images -------------"

 [faces] = 'sav_face_stk@***'  ; Image file template (80 images 130x130)

 [ran]   = 1           ; Value is unused, variable must exist

 [n2]    = 0           ; Output image number

 DO [n] = 1,80         ; Loop over all images

   !DO [sh] = 1,2

     [ang] = RAN([ran]) * 360 
     [shx] = 0         ; RAN([ran]) * 3.0
     [shy] = 0         ; RAN([ran]) * 2.0

     [n2]  = [n2] + 1  ; Output image number
     RT SF             ; Rotate, shift
       [faces][n]      ; Unaligned particle     (input)
       [in][n2]        ; Rotated particle image (output)
       [ang], 1        ; Angle, scale
       [shx],[shy]     ; Shifts

   !ENDDO
 ENDDO

 SYS                             
   echo " Creating montage -----------------------------------------"; echo " "
 
 [mon] = 'sav_face_rtsh_stk_mon' ; Rotated particle stack (output)

 MN                    ; Make montage of images      
   [in]                ; Image stack template   (input)               
   1-80                ; File numbers for montaging 
   10, 3               ; Images / row, margin width
   0                   ; Margin intensity           
   [mon]               ; Montage file           (output)    

  CP TO JPG
   [mon]               ; SPIDER  file           (input)
   [mon]               ; JPG     file           (output)
  SYS                  ; Display image
   display [mon].jpg &

 EN
