 ;
 ; SOURCE:  ca.spi 
 ; PURPOSE: Run MDA on 80 noisy faces
 ;
 ; OUTPUT --> _EIG, _IMC, _SEQ, _MAS, _SEQ, _SET
 ;            postcript files
 ;
 ;----------------------------- Input files ---------------------------

 [sma]       = 'sav_face_stk'  ; Image file template  

 ;----------------------------- Output files --------------------------

 [mask]          = 'face_mask'     ; Mask file     

 [cas_c]         = 'cas_c'         ; Correspondence analysis file prefix
 
 [clhc_den]      = 'clhc_den'      ; Dendrogram file 
 [clhc_den_doc]  = 'clhc_den_doc'  ; Dendrogram doc file 

 [clcla_clu]     = 'clcla_clu'     ; Cluster file for 'CA SM" use  
 [clcla_den]     = 'clcla_den'     ; Dendrogram plot file  
 [clcla_den_doc] = 'clcla_den_doc' ; Dendrogram doc file  

 [clhd_doc]      = 'clhd_doc'      ; Class number doc file   

 [clhe_sel]      = 'clhe_sel'      ; Selection doc file prefix                   

 ; ---------------------- END BATCH HEADER ----------------------------


 SYS
   echo " Create mask file xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx "
 DE
   [mask]

 PT                   ; Create mask
   [mask]             ; Mask file name   (output)
   64, 64             ; Size
   C                  ; Circle
   33,33              ; Circle center
   30                 ; Circle radius
   N                  ; Mask finished

 MD
   VB OFF
 SYS
   echo " " ;echo " Analyze using transposed CORAN xxxxxxxxxxxxxxxxxxxxxxxxxxx "

 CA S                 ; Analyze using  CORAN 
   [sma]@*            ; Image file template      (input)
   1-80               ; Images
   [mask]             ; Mask input file (big)    (input)
   15                 ; No. of factors
   C                  ; CORAN analysis
   0.0                ; Additive constant
   [cas_c]            ; File prefix              (output)


 MD
   VB ON
 SYS
   echo "  ";echo " Automatic Classification xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx "
 
 MD
   SET SEED          ; So dendrogram is always same
   77,77

 SYS                 ; Make sure there is no cluster file
   \rm -f [clcla_clu].$DATEXT

 CL CLA              ; Classification analysis
   [cas_c]           ; IMC file prefix (must exist)     (input)
   [clcla_clu]       ; Cluster file for 'CA SM"         (output)
   1-3               ; Factors used in cluster search
   5,4               ; No. of of iterations, No. of centers
   8                 ; No. of initial clusters
   0                 ; Level for class occupancy
   [clcla_den].ps    ; Dendrogram postscript plot file (output)
   1                 ; Threshold for class cutoff for dendrogram plot
   [clcla_den_doc]   ; Dendrogram doc file             (output)


 SYS                  ; View the output postscript plot file
   echo gs [clcla_den].ps 
 SYS                  ; Show dendrogram doc file
   echo xedit [clcla_den_doc].$DATEXT &


 VM
   echo ; echo " Controlled Classification  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx "
 
 CL HC                ; Hierarchical Clustering
   [cas_c]_IMC        ; IMC file  (must exist)          (input)
   1-3                ; Factors used in cluster search
   1                  ; Factors weight = 1 for all
   1                  ; Factors weight = 1 for all
   1                  ; Factors weight = 1 for all
   2                  ; Clustering criterion (1...5)
   [clhc_den].ps      ; Dendrogram postscript plot file (output)
   0                  ; Cutoff
   [clhc_den_doc]     ; Dendrogram doc file             (output)

 VM                 ; View the dendrogram postscript plot file
   okular [clhc_den].ps &
   !evince [clhc_den].ps &
 VM                 ; Show the dendrogram doc file
   nedit [clhc_den_doc].$DATEXT &
 


lb99


 SYS
   echo "  Using: 'CL HD' for 'CL CLA' With Thresh xxxxxxxxxxxxxxxxx "

 DE
   [clhd_doc]          ; File name                       (deleted)


 CL HD
   20                  ; Threshold
   [clcla_den_doc]     ; Dendrogram File name            (input)
   [clhd_doc]          ; Class number doc file name       ( output)

 SYS
    nedit [clhd_doc].$DATEXT &
   ;gedit --new-window [clcla_doc].$DATEXT &


 SYS
   echo "  Using: 'CL HE' for 'CL CLA' With Thresh xxxxxxxxxxxxxxxxxx "

 SYS
   \rm -f [clhe_sel]*.$DATEXT   

 CL HE
   64                  ; Threshold
   [clcla_den_doc]     ; Dendrogram File name            (input)
   [clhe_sel]_***      ; File names                      (output)

 SYS
   nedit  [clhe_sel]_001.$DATEXT &
   ;gedit --new-window jnk_cla_[clcla_sel]_002.$DATEXT &

 EN
