 ; SOURCE:  /usr8/spider/docs/exa/bpw2.spi 
 ; AUTHOR:  ArDean Leith    Jan 2014
 ; USAGE:   spider spi/dat  @bpw2 

 ; ----------------- Input files -----------------------------------------

 [projfile] = 'sav_bpw2_proj'    ; Projections of vol.

 [angfile]  = 'sav_bpw2_ang_doc' ; Angles file

 ; ----------------- Output files ----------------------------------------                             ; Output

 [out]      = 'bpw2'             ; Backprojected vol.
                                 
 ; ----------------- END BATCH HEADER ------------------------------------ 

 SYS
   echo " ---------- Testing:  'BP W2' -----------------------------------"

 BP W2
   [projfile]@**      ; Template for 2-d projections (input)
   1-11               ; Projection numbers 
   [angfile]          ; Angles doc file             (input)
   78, 160            ; Radius & height of reconstructed object
   1,160              ; Reconstruction Y range
   0.3                ; Cut-off frequency for parzen filter
   [out]              ; Back projected file         (output)

 EN




 ! ---------------------  UNUSED BELOW HERE ------------------------------------

 [datafile] = 'sav_vol_ax_pd'    ; Axis test volume 
 [angfile]  = 'sav_bpw2_ang_doc' ; Angles file
 [projfile] = 'sav_bpw2_proj'    ; Projections of vol.
 [out]      = 'bpw2'             ; Backprojected vol.
 

 SYS
   echo " ---------- Create projection angles file  ---------------------------"

 VO EA [nang]                ; [nang] = No. of projections
   15                        ; Theta angular step
   -75,75                    ; Theta angle range 
   0, 0                      ; Phi angle range 
   [angfile]                 ; Angles doc file    (output)

 IQ REG                      ; Check # of angles
   [nang],11

 SYS
   echo " ---------- Create {***[nang]} projections using: 'PJ 3Q' ------------"

 DE
   [projfile]                ; Template for 2-d projections (removed)

 FI H [nx],[ny]
   [datafile]  
   NX,NY 

 [radius] = ([nx]/2)-2

 PJ 3Q
   [datafile]                ; Original volume         (input)
   [radius]                  ; Radius
   1-[nang]                  ; Enter angle numbers 
   [angfile]                 ; Angles doc file         (input)
   [projfile]@**             ; Template for 2-d projections (output)


 MN                                     ; Make montage of images      
   [projfile]@**                        ; Template for 2-d projections (input)
   1-11                                 ; File numbers for montaging 
   6, 3                                 ; Images / row, margin width
   200                                  ; Margin intensity           
   sav_vol_ax_pd_bpw2_proj              ; Montage file           (output)    

  CP TO JPG
   sav_vol_ax_pd_bpw2_proj              ; SPIDER  file       (input)
   sav_vol_ax_pd_bpw2_proj              ; JPG     file       (output)
  SYS                 ; Display image
   display sav_vol_ax_pd_bpw2_proj.jpg &

