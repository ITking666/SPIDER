 ; SOURCE: /usr8/spider/docs/exa/images/du.spi
 ; AUTHOR: ArDean Leith    
 ; USAGE:  spider spi/dat @du

 ;  ----------------- Input files -----------------------------------------

 [in]  = 'savpad_nik_sli_45' ; SPIDER Image file 

 ;  ----------------- Output files ----------------------------------------

 [noisy] = 'savpad_nik_sli_45_noisy'  ; SPIDER Image file 

 [out]   = 'du'                       ; SPIDER Image file 
 
 ;----------------- END BATCH HEADER ------------------------------------

 SYS
   echo " Test 'DU'  xxxxxxxxx (image) xxxxxxxxxxxx" ; echo

 FI H [min],[max]        ; File info
   [in]                  ; Image file         (input)
   MIN, MAX              ; Header values wanted
 SYS
   echo ' Range: ({%f8.2%[min]} ... {%f8.2%[max]})'; echo 

 CP                      ; Copy input image
   [in]                  ; SPIDER Image file 
   [noisy]               ; SPIDER Image file 

 ; Find location of maximum pixel
 PK M [xs],[ys], [x],[y], [pk],[nm], [mr]  ; Find peak in image
   [noisy]               ; Image                  (input)
   1, 1                  ; Locations relative to upper left corner   

 ; Replace pixels around maximum pixel with strong intensity = noise
 [xm1] = [x] - 1
 [ym1] = [y] - 1
 [xp1] = [x] + 1
 [yp1] = [y] + 1

 [maxt3] = [max] * 1.3
 [maxt3] = [max] * 3.3

 PP                      ; Set pixel density
   [noisy]               ; Image         (input/output)
   [xm1],[y],  [maxt3]   ; Location
   [xm1],[yp1],[maxt3]   ; Location
   [xm1],[ym1],[maxt3]   ; Location
   [x],  [ym1],[maxt3]   ; Location
   [x]  ,[y],  [maxt3]   ; Location
   [xp1],[y],  [maxt3]   ; Location
   [xp1],[yp1],[maxt3]   ; Location
   [x],  [yp1],[maxt3]   ; Location
   0, 0                  ; Stop

 CP TO JPG
   [noisy]               ; Image          (input)
   [noisy]               ; JPEG Image     (output)
 SYS
   display [noisy].jpg   &

 CP                      ; Copy noisy image
   [noisy]               ; SPIDER Image file 
   [out]                 ; SPIDER Image file 
 SYS
   echo " Test 'DU'  xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx" ; echo



 DU   
   [out]                 ; SPIDER file   (input)
   4.3                   ; Standard deviation factor
   2                     ; Truncation: (1) Bottom, (2) Top, (3) Both sides

 CP TO JPG
   [out]                 ; Image         (input)
   [out]                 ; JPEG Image    (output)
 SYS
   display [out].jpg &

 EN
