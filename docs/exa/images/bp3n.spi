
 ; ----------------- Input files -----------------------------------------

 [datafile]  = 'sav_vol_ax_pd'    ; Axis test volume 

 ; ----------------- Output files ----------------------------------------                             ; Output

 [angfile]   = 'jnk_angvoea_all'  ; Angles file

 [projfile]  = 'jnkproj_pj3q'     ; Projections of vol.

 [out]       = 'jnk_bp3n_vol'     ; Backprojected vol.
                                 
 ; ----------------- END BATCH HEADER ------------------------------------ 

 ; Turn off some un-necessary file IO and docfile output
 MD
   VB OFF                        ; Non-verbose
 MD
   SET MP
   0                             ; Use all processors

 FI H [size]
   [datafile]  
   NX

 SYS
   echo " ---------- Create pojection angles file  -------------------------------"

 VO EA [nang]                  ; [nang] = No. of projections
   15                          ; Angular step
   0,90.0                      ; Angles
   0,359.9                     ; Angles
   [angfile]                   ; Angles doc file    (output)

 IQ REG                        ; Check # of angles
   [nang],84

 SYS
   echo " ---------- Create {***[nang]} projections using: 'PJ 3Q' ---------------"

 DE
   [projfile]                  ; Template for 2-d projections (output)

 [radius] = ([size]/2)-2

 PJ 3Q
   [datafile]                  ; Original volume         (input)
   [radius]                    ; Radius
   1-[nang]                    ; Enter angle numbers 
   [angfile]                   ; Angles doc file         (input)
   [projfile]@**               ; Template for 2-d projections (output)


 SYS
   echo " ---------- Testing:  'BP 3N' -----------------------------------"

 BP 3N
   [projfile]@***             ; Template for 2-d projection (input)
   1-20                       ; Projections 
   [angfile]                  ; Angles doc file             (input)
   *                          ; No symmetries
   [out]                      ; Back projected file         (output)

 FS [max],[min],[av],[sd]     ; File statistics
   [out]                      ; Back projected file         (input)
 SYS
   echo ' BP 3N   Range: {%ES9.2%[min]}....{%ES9.2%[max]}'


 EN
