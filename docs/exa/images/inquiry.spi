 ; PURPOSE: Tests Inquiry operations 
 ; SOURCE: /usr8/spider/docs/exa/images/inquiry.spi
 ; USAGE:  $spib/spider tst @inquiry

 [file] = 'sav_avg000'                 ; String variable assignment    
               
 ; Operation not in older versions
 IQ VER [version]                  ; Get SPIDER version 
 IF ([version] < 20.17)  GOTO LB77 ; Can skip operation not in older versions
 
 [n] = 5              
 IQ REG                            ; Test numerical variable value 
   [n],5                           ; Variable name and expected value 
              
 IQ DIR [exists]                   ; Query existence of directory 
   ../images                       ; Current directory 
 IF ([exists] .GT. 0)  THEN        ; Conditional clause 
    SYS                            ; Operating System call 
    echo " Dir: work exists "  
 ENDIF                             ; End of conditional clause 
               
 IQ FI [exists],[isopen]           ; Query existence of file 
    [file]                         ; File name  
 
 IQ GONE [waited]                  ; Wait until file disappears 
   jnk_never_existed               ; File name  
   1, 360000                       ; Seconds between queries & max overall wait
                
 IQ SYNC [waited]                  ; Wait until file exists 
   [file]                          ; File name  
   1, 360000                       ; Seconds between queries & max overall wait
 
 VM                                ; Create empty file
   touch jnkimg.$DATEXT                        
 IQ SYNC D [waited]                ; Wait until file exists then delete it 
   jnkimg                          ; File name  
   1, 360000                       ; Seconds between queries & max overall wait
               
 IQ WO                             ; Lists values occuring at 4 bytes inside a file 
    [file]                         ; File name  
    9                              ; Value in 4 bytes: 9..12
               
 LB 77                             ; Labeled line                          
               
EN
