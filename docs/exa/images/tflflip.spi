 ; SOURCE: /usr8/spider/docs/exa/tflflip.spi 
 ; AUTHOR: ArDean Leith    Oct 2010
 ; USAGE:  spider spi/dat @tflflip 

 ; --------------- Parameters ---------------

 [sp_pixsiz] =       1.34 ; Pixel size, A
 [sp_sph_abb] =      2.00 ; Spherical aberration, mm
 [sp_sourcesiz] =    0.00 ; Source size, 1/A
 [sp_def_spr] =      0.00 ; Defocus spread, A
 [sp_acr] =          0.10 ; Amplitude contrast ratio
 [sp_geh] =       10000.0 ; Gaussian envelope halfwidth, 1/A
 [sp_lambda] =       0.02 ; Electron wavelength - Lambda 
 [sp_maxspfreq] =   0.373 ; Maximum spatial frequency, 1/A

 [defocus-angs] = 48600.5 ;
 [padded-dim]   = 360     ; 

 ; ----------------- Input files -----------------------------------------

 ; ----------------- Output file ---------------------------------------- 

 [out_doc]     = 'tflflip_doc'       ; Phase CTF doc file 
 [out_gnuplot] = 'tflflip_plot.gnu'  ; One Dimensional phase CTF plot file

 ; ------------- END BATCH HEADER ----------------------------------------

 SYS                             
   echo "  Testing  'TF L FLIP' ---------------------------------------"; echo 

 ; Delete any existing doc file
 DE
   [out_doc]

 ; Generate straight CTF doc file
 TF L FLIP
   [sp_sph_abb]                 ; Spherical aberration [mm]
   [defocus-angs],[sp_lambda]   ; Defocus, electron wavelength
   [padded-dim]                 ; Size 
   [sp_maxspfreq]               ; Max spatial frequency
   [sp_sourcesiz],[sp_def_spr]  ; Source size, defocus spread
   [sp_acr],[sp_geh]            ; ACR, envelope half width
   [sp_pixsiz]                  ; Pixel size [A]
   S                            ; Diffractogram / envelope / straight
   [out_doc]                    ; CTF doc file (output)
 

 SYS
   echo "  Plotting  'TF L FLIP' doc file ---------------------------------"; echo 
 SYS 
   rm -f [out_gnuplot]

 SYS 
   echo 'set title "CTF Plots"' > [out_gnuplot]
 SYS 
   echo 'set xzeroaxis' >> [out_gnuplot]
 SYS 
   echo 'set xlabel "Resolution, Angstroms^-1"'     >> [out_gnuplot]
 SYS 
   echo 'set ylabel "Transfer"'     >> [out_gnuplot]
 SYS 
   echo 'plot [0:{%f6.4%[sp_maxspfreq]}][-1.1:1.4] \' >> [out_gnuplot]
 SYS 
   echo '"[out_doc].$DATEXT" using 7:3 title "flipped CTF" with lines, \'  >> [out_gnuplot]
 SYS 
   echo '"[out_doc].$DATEXT" using 7:4 title "straight CTF" with lines, \' >> [out_gnuplot]
 SYS 
   echo '"[out_doc].$DATEXT" using 7:5 title "trapped CTF" with lines'     >> [out_gnuplot]

 SYS 
   gnuplot -persist [out_gnuplot]

 EN
