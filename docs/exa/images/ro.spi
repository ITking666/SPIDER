
 ; SOURCE: /usr8/spider/docs/exa/ro.spi
 ; AUTHOR: ArDean Leith    
 ; USAGE:  spider spi/dat @ro

 [ndup] = 50                      ; Number of duplicated rows

 ; ----------------- Input files -----------------------------------------

 [sli]     = 'sav_crc_sli_033'    ; CRC slice image 

 ; ----------------- Output files ---------------------------------------- 

 [ro]          = 'ro'             ; Single row average image 
 [rodup]       = 'ro_dup'         ; Duplicated row image     

 [rosd]        = 'rosd'           ; Single row average image 
 [rosddup]     = 'rosd_dup'       ; Duplicated row image     
 [rosddoc]     = 'rosd_doc'       ; Rotational average doc file     

 [roi]         = 'roi'            ; Square average image 

 ; ----------------- END BATCH HEADER ------------------------------------ 

 

 SYS
   echo " Testing  'RO SD' ----------------------------------------"

 RO SD            ; Rotational average - single line image, doc file
   [sli]          ; CRC slice image               (input) 
   [rosd]         ; Rotational average image      (output)
   [rosddoc]      ; Rotational average doc. file  (output)

 ; Duplicate the rows for clarity

 FI H [nz],[ny]   ; File header info
   [sli]          ; CRC slice image           (input) 
   NX,NY
 BL
   [rosddup]      ; Blank image               (output) 
   [nz],[ndup]    ; Size
   No             ; Do not use average
   0              ; Background

 DO [iy] = 1,[ndup]
   IN
     [rosd]        ; Rotational average image  (input) 
     [rosddup]     ; Blank image               (input/output) 
     1,[iy]        ; Top left corner of insert
 ENDDO

 CP TO JPG
    [rosddup]      ; SPIDER image               (input) 
    [rosddup]      ; JPEG   image               (output) 

 SYS
   echo " Testing  'RO I' ----------------------------------------"

 RO I              ; Make rotational average image
   [sli]           ; CRC slice image             (input) 
   [roi]           ; Rotationally averaged image (output)
 

 CP TO JPG
    [roi]          ; SPIDER image (input) 
    [roi]          ; JPEG   image (output) 



 ; Duplicates action of 'RO SD' without doc file creation (not needed)

 SYS
   echo " Testing  'RO' ------------------------------------------"

 RO               ; Rotational average - single line image
   [sli]          ; CRC slice image           (input) 
   [ro]           ; Rotational average image  (output) 
 
 ; Duplicate the rows for clarity

 FI H [nz],[ny]   ; File header info
   [sli]          ; CRC slice image           (input) 
   NX,NY
 BL
   [rodup]        ; Blank image               (output) 
   [nz],[ndup]    ; Size
   No             ; Do not use average
   0              ; Background

 DO [iy] = 1,[ndup]
   IN
     [ro]         ; Rotational average image  (input) 
     [rodup]      ; Blank image               (input/output) 
     1,[iy]       ; Top left corner of insert
 ENDDO

 CP TO JPG
    [rodup]       ; SPIDER image              (input) 
    [rodup]       ; JPEG   image              (output) 


 EN
