 ; SOURCE: /usr8/spider/docs/exa/ceharalick.spi 
 ; AUTHOR: ArDean Leith  Sept. 2013   
 ; USAGE:  spider spi/dat @ceharalick 

 [wnx] = 256     ; X Window size
 [wny] = 256     ; Y Window size
                 
 [wx]  = 229     ; Window location
 [wy]  = 187     ; Window location
                  
 ;  ----------------- Input files -----------------------------------------

 [in] = 'sav_bz_sli_75'         ; Image file

 ;  ----------------- Output files ----------------------------------------

 [outorig]  = 'sav_bz_sli_75_win'      ; Original windowed image

 [outstk]   = 'cehar_stk'                ; Labeled stack file

 [out]      = 'cehar_montage'            ; Labeled  montage

 ; ----------------- END BATCH HEADER ------------------------------------

 ; Report input image size, etc ---------------------------------------
 FI H [nx],[ny],[nz]
   [in]                           ; Input image 
   NX,NY,NZ                       ; NX,NY,NZ locations
  
 SYS
   echo " Size:       {****[nx]} x {****[ny]} x {****[nz]}  "
 SYS
   echo " Windowed:   {****[wnx]} x {****[wny]}  "
 SYS
   echo "       At:   {****[wx]},  {****[wy]} "
 SYS
   echo "   "

 ; Window the input image -------------------------------------------  
 WI                             ; Window operation
   [in]                         ; Image                  (input)
   _1                           ; Image                  (output
   [wnx],[wny]                  ; Window size
   [wx],[wy]                    ; Window location

 ; Scale image values: 0...255 ---------------------------------------
 AR SCA                         ; Scale image 0...255
   _1                           ; Image                  (input
   _2                           ; Image                  (output)
   0 255                        ; Scaling range

 [imgnum]= 0

 ; Save original window ---------------------------------------------- 

 CP                             ; Label the image 
   _2                           ; Image                  (input) 
   [outorig]                    ; Image                  (output) 
 CP TO JPG
   [outorig]                    ; SPIDER  file       (input)
   [outorig]                    ; JPG     file       (output)
 SYS                            ; Display image
   display [outorig].jpg &

 [imgnum]=[imgnum]+1            ; Increment stack pointer 
 LA                             ; Label the image 
   _2                           ; Image                  (input) 
   [outstk]@{***[imgnum]}       ; Image                  (output) 
   ORIGINAL

 VM  
   echo " N: {**[imgnum]} Original" 

 
 ; Apply Haralick filters ---------------------------------------------
    
 [off] = 2                       ; Set offset
 DO  [iter]=0,1 
    DO  [mode]=1,6               ; Loop through modes
   
       [radius] = 9 + [iter] * 6 ; Set radius
    
       CE HAR                    ; Haralick 
         _2                      ; Image                  (input)
         _3                      ; Image                  (output)
         [radius],[radius]       ; Neighborhood 
         32                      ; Intensity Levels 
         [off],[off]             ; Offset 
         [mode]                  ; Mode 
   
       [imgnum]=[imgnum]+1       ; Increment stack pointer 
       LA                        ; Label the image 
         _3                      ; Image                  (input) 
         [outstk]@{***[imgnum]} ; Image                  (output) 
         CE HAR M:{*[mode]} R:{**[radius]} O:2 
   
       SYS
         echo " N: {**[imgnum]} CE Haralick Radius: {**[radius]} Mode: {*[mode]} Off: 2"
    ENDDO 
 ENDDO 
   
 [off] = 4                        ; Set offset

 DO  [iter]=0,1 
    DO  [mode]=1,6                ; Loop through modes
   
       [radius] = 11 + [iter] * 6 ; Set radius 
    
       CE HAR                     ; Haralick 
         _2                       ; Image                  (input) 
         _3                       ; Image                  (output) 
         [radius],[radius]        ; Neighborhood 
         32                       ; Intensity Levels 
         [off],[off]              ; Offset 
         [mode]                   ; Mode 
   
       [imgnum]=[imgnum]+1        ; Increment stack pointer 
       LA                         ; Label the image 
         _3                       ; Image                   (input) 
         [outstk]@{***[imgnum]}     ; Image                   (output) 
         CE HAR M:{*[mode]} R:{**[radius]} O:4" 
   
       SYS
         echo " N: {***[imgnum]} CE Haralick Radius: {**[radius]} Mode: {*[mode]} Off: 4"
    ENDDO 
 ENDDO 

 SYS
   echo " Total Frames: {***[imgnum]}"

 MN S                       ; Montage the output images
   [outstk]@***             ; Image template       (input)
   1-[imgnum]               ; File numbers
   5,1                      ; Images / row, margin width
   0                        ; Margin value
   [out]                    ; Montage file         (output)

 FS
   [out]                    ; Output file

 SYS
   echo " Montage in: [out]" ; echo " "

 CP TO JPG
   [out]                     ; SPIDER  file       (input)
   [out]                     ; JPG     file       (output)
 SYS                         ; Display image
   display [out].jpg &

 EN

