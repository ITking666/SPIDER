 ; SOURCE: /usr8/spider/docs/exa/ms.spi 
 ; AUTHOR: ArDean Leith    
 ; USAGE:  spider spi/dat @ms 

 ;  ----------------- Input files -----------------------------------------

 [in]       = 'sav_sine_wave'  ; Sinewave test image

 ;  ----------------- Output files ----------------------------------------

 [outft]     = 'jnk_sine_ft' ; Fourier of sine wave 

 [outstk]    = '_1'          ; Inline image stack file

 [outvolstk] = '_2'          ; Inline volume stack file

 [outstkft]  = '_3'          ; Inline Fourier image stack file

 [outstki]   = '_4'          ; Inline indexed stack file

 [outstkid]  = 'jnk_ind_stk' ; Disk based indexed stack file

 ; ----------------- END BATCH HEADER ------------------------------------


 SYS
   echo " Testing  'FT' ------------------------------------------------------"
 FT                 ; Fourier transform 
   [in]             ; File       (input)
   [outft]          ; Fourier file       (output)

 SYS
   echo " Testing  'MS' ------------------------------------------------------"

 ; Create a 75 X 75 -- X 10 inline stack on inline buffer #1
 MS                  ; Create a new inline stack 
   [outstk]@         ; Inline stack number 1 (output)
   75 75 1           ; Image size within stack
   10                ; Number of images allowed in stack

 ; Put 10 sinewave images into inline stack #1
 DO I=1,10
    CP               ; Model image
    [in]             ; Image         (input)
    [outstk]@{**I}   ; Image         (output)
 ENDDO


 SYS
   echo " Testing  'MS 3' ------------------------------------------------------"

 ; Create a 75 X 75 X 3 -- X 5 inline volume stack on inline buffer #2
 MS                   ; Create a new stack 
   [outvolstk]@       ; Inline stack number 2   (output)
   75 75 3            ; Volume size within stack
   5                  ; Number of volumes allowed in stack

 ; Put 3 sinewave images into inline volume stack #2
 CP TO VOL            ; Model image
   [in]               ; Image         (input)
   [outvolstk]@      ; Image         (output)
   1-3                ; Slices

 SYS
   echo " Testing  'MS F' ------------------------------------------------------"

 ; Create a 75 X 75 -- X 49 inline image stack on inline buffer #3
 MS F         ; Create a new stack 
   [outstkft]@        ; Inline stack number 3 (output)
   75 75 1            ; Image size within stack
   10                 ; Number of images allowed in stack

 ; Put 10 sinewave Fourier images into inline image stack #3
 DO I=1,10
    CP                ; Model image
    [outft]           ; Image         (input)
    [outstkft]@{**I}  ; Image         (output)
 ENDDO


 SYS
   echo " Testing  'MS I' ------------------------------------------------------"

 ; Create a 75 X 75 -- X 10 inline indexed stack on inline buffer #4
 MS I                  ; Make empty indexed stack
   [outstki]@          ; File name     (output)
   75 75 1             ; Image size within stack
   10                  ; Number of images allowed in stack
   100                 ; Highest image number allowed in stack

 ; Put two sinewave images in  indexed stack on inline buffer #4
 CP                    ; Model image
   [in]                ; Image         (input)
   [outstki]@1         ; Image         (output)

 CP                    ; Model image
   [in]                ; Image         (input)
   [outstki]@5         ; Image         (output)

 FI H [maxim]
   [outstki]@ 
   MAXIM
 SYS
   echo " Maximum image number:   {**[maxim]}"



 SYS
   echo " Testing  'MS I' ------------ (disk based indexed stack) --------------"

 ; Create a 75 X 75 -- X 10 disk based indexed stack 
 MS I                  ; Make empty indexed stack
   [outstkid]@         ; File name     (output)
   75 75 1             ; Image size within stack
   10                  ; Number of images allowed in stack
   100                 ; Highest image number allowed in stack

 ; Put two sinewave images in  indexed stack on inline buffer #4
 CP                    ; Model image
   [in]                ; Image         (input)
   [outstkid]@1        ; Image         (output)

 CP                    ; Model image
   [in]                ; Image         (input)
   [outstkid]@6        ; Image         (output)

  FI H [maxim]
   [outstkid]@ 
   MAXIM
 SYS
   echo " Maximum image number:   {**[maxim]}"



 SYS
   echo " Testing  'DE'  on inline indexed stack ------------------------------"

 DE            ; Make sure it does not exist
   [outstki]@            ; File name     (removed)

 SYS
   echo " "
 EN
