(x11,x12,x20,x21,x22)
;------------------------------------------------------------
; SOURCE:  face.spi 
; PURPOSE: Called by makefaces.spi, to create test images for "CA S"
; INPUT:
;    x11,x12 : Width, height of image
;    x20     : Head shape 0 = round, 1 = oval
;    x21     : Eyes  0 = left, 1 = right, 2 = center
;    x22     : Mouth 0 = small, 1 = large
;-----------------------------------------------------------

 x64 = 64                    ; Original size

 ;------- OUTPUT FILES ------------

 FR
   ?output image?[face]	     ; Name the file

 ;-------- END BATCH HEADER---------------

 x66 = INT(x11/2 + 1)        ; Center coordinates
 x67 = INT(x12/2 + 1)
 x68 = INT(x11 * 20/x64)     ; Radius

 ; ----- The eyes -----
 x72 = INT(x11 * 24/x64)     ; Coords for left eyehole
 x73 = INT(x11 * 28/x64)

 x82 = INT(x11 * 42.0/x64)   ; Coords for right eyehole
 x83 = x73
 x75 = INT(x11 * 5.0/x64)    ; Radius of eyehole

 IF (x21.EQ.0) THEN          ; Eyes left
   x76 = INT(x11 * 21/x64)   ; Pupil x coords
   x78 = INT(x11 * 39/x64)
 ENDIF
 IF (x21.EQ.1) THEN          ; Eyes right
   x76 = INT(x11 * 27/x64)   ; Pupil x coords
   x78 = INT(x11 * 45/x64)
 ENDIF
 x77 = INT(x11 * 29/x64)     ; Pupil y coord
 x79 = INT(x11 * 3/x64)      ; Pupil radius

 ; ----- The mouth -----
 IF (x22.EQ.0) THEN          ; Small mouth
   x52 = INT(x11 * 30/x64)
   x53 = INT(x11 * 41/x64)   ; Upper left coords
   x54 = INT(x11 * 5/x64)
   x55 = INT(x11 * 5/x64)    ; X & y offsets
 ENDIF
 IF (x22.EQ.1) THEN          ; Large mouth
   x52 = INT(x11 * 24/x64)
   x53 = INT(x11 * 41/x64)   ; Upper left coords
   x54 = INT(x11 * 17/x64)
   x55 = INT(x11 * 5/x64)    ; X & y offsets
 ENDIF

 ;;DE                          ; Essential!!
 ;;  [face]

 PT                          ; Make the round head
   [face]
   x11, x12                  ; Dimensions
   C                         ; Circle
   x66,x67                   ; Center coords
   x68                       ; Radius
   N


 IF (x20.eq.1) THEN          ; For oval head
   IP
     [face]
     _2
     INT(x11*55/x64),x11

   PD
     _2
     [face]
     x11,x11
   M
     (INT(x11*5/x64),1)
 ENDIF

 ; Then do the eyes and mouth
 PT
   [face]
   CS                     ; Left eye
   x72,x73
   x75
   Y
   C
   x76,x77
   x79
   Y
   CS                     ; Right eye
   x82,x83
   x75
   Y
   C
   x78,x77
   x79
   Y
   RS                      ; Mouth
   x52,x53
   x54,x55
   N

 RE
