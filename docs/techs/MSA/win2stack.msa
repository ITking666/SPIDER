; COMBINES A SERIES OF SINGLE IMAGES
; INTO A SINGLE STACK

; REQUIRES backup.msa

; ---------------- Parameters -----------------

[lastPart]         = 5646                     ; last particle number (there can be gaps)
[progressInterval] = 500                      ; progress message written every Nth particle

; ------------------ Input ------------------
FR L
[input_images]Particles/win*****              ; unstacked images

; ------------------ Outputs ------------------
FR L
[combined_stack]stk2-unaligned                ; combined stack (w/o "@")
FR L
[stack_copy]stk1-unaligned_backup             ; backup stack (w/o "@")
FR L
[particle_list]listparticles                  ; list of particles

; ------------- END BATCH HEADER -------------

DE 
[combined_stack]@
@backup   ; DE 
[particle_list]

[micNum]  = 0
[counter] = 0

DO LB1 [partNum] = 1,[lastPart]
    IF (int([partNum]/[progressInterval]) .EQ. [partNum]/[progressInterval]) THEN
        VM
        echo "Working on particle #{%i0%[partNum]} out of {%i0%[lastPart]}"
    ENDIF
    
    ; Check if file exists
    IQ FI [partExists]
    [input_images][partNum]

    IF ([partExists] .EQ. 1) THEN
        [counter] = [counter] + 1
        
        CP
        [input_images][partNum]
        [combined_stack]@{*****[counter]}

        SD [counter], [counter],[partNum],[micNum]
        [particle_list]
    ENDIF
LB1
; End particle-loop

; make backup copy of stack (for high-pass filtering, etc.)
CP
[combined_stack]@
[stack_copy]@

SD /      SLICE_NUM     UNSTACKED
[particle_list]
SD E
[particle_list]

EN D

; Modified 2015-08-20
