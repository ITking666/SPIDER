;------------------------------------------------------------------------------!
; Adapted from b12.fed/hbl : 3D Reconstruction from the tilted-speci;en images 
;        corresponding to the three image classes obtained from MSA :
;------------------------------------------------------------------------------!

; NOTE:
;   Make sure that the view of the untilted class numbers in this batch file
;   are the ones you want, and that they correspond to the correct symmetry file.
;   For example, untilted class average #4 may not be the end-on view in your
;   own image set

; --------------------- Parameters ---------------------
[single-class]  = 0   ; class number to calculate reconstruction (0==all)
[multiref-iter] = -1  ; iteration of multireference alignment to use as input (<0==last)
[flipflop-yn]   = 0   ; compute reconstructions for "flip" and "noflip" subclasses?
[numProcs]      = 0   ; number of CPUs to use (0==ALL)

; -------------------- Input files --------------------
fr l
[class_dir]Zerodegr/Multiref{*[iter]}         ; input directory
fr l
[class_list][class_dir]/goodviews             ; list of classes (if reconstructing multiple classes)
fr l
[class_select_doc][class_dir]/goodsel***      ; class selection doc filename template
;;;[class_select_doc][class_dir]/goodsel{***[class-num]}  ; class selection doc filename template
fr l
[multiref_tracking_doc]Zerodegr/docmultiref   ; list of multireference alignment iterations 
fr l
[aligned_tilted]Tilted/stk3-cent@*****        ; tilted aligned tilted image filename template
fr l
[angles_doc]Tilted/dangles                    ; Euler angle doc file
fr l
[symmetry_doc]*                               ; no symmetry doc file

; -------------------- Output files --------------------
fr l
[volume_dir]Volumes                            ; output reconstruction directory
fr l
[class_vol][volume_dir]/vcla***                ; class volume filename template
;;;[class_vol][volume_dir]/vcla{***[class-num]}   ; class volume filename template

; ---------------------- END BATCH HEADER ----------------------

vm
echo "if(! -d [volume_dir]) mkdir [volume_dir]"|csh


; GET ITERATION NUMBER

; if input iteration-specified
if([multiref-iter].gt.0) then
    [iter] = [multiref-iter]

; if input iteration not specified
else
    ; look for last iteration number
    ud n [last-iter-key]
    [multiref_tracking_doc]
    
    ; get iteration number
    ud [last-iter-key], [iter]
    [multiref_tracking_doc]
    ud e  ; close doc
endif

vm
echo "Using classification/alignements from [class_dir]"


; check if all classes or single class specified
if([single-class].eq.0) then
    ; get #classes
    ud n [num-classes]
    [class_list]
    
    vm
    echo "Reconstructing all {***[num-classes]} classes listed in [class_list]" ; echo
else
    vm
    echo "Reconstructing only class #{***[single-class]}" ; echo
    
    [num-classes] = 1
    [class-num]   = [single-class]
endif

md
set mp
[numProcs]

; loop through classes
do lb1 [class-key] = 1,[num-classes]
    ; get class# if not a single class
    if([single-class].eq.0) then
        ud ic [class-key], [class-num]
        [class_list]
    endif
    
    @bp3f
    [class-num]         ; PARAMETER: class number
    [class_select_doc]  ; INPUT: selection doc
    [angles_doc]        ; INPUT: angles doc
    [aligned_tilted]    ; INPUT: tilted-image template
    [symmetry_doc]      ; INPUT: symmetry doc (optional)
    [class_vol]         ; OUTPUT: reconstruction
    
    ; check for flip+noflip files
    iq fi [flipflop-exists]
    [class_select_doc]-flip
    
    ; if it exists...
    if([flipflop-exists].eq.1) then
        ; if requested, compute reconstructions
        if([flipflop-yn].eq.1) then
            @bp3f
            [class-num]                 ; PARAMETER: class number
            [class_select_doc]-noflip   ; INPUT: selection doc
            [angles_doc]                ; INPUT: angles doc
            [aligned_tilted]            ; INPUT: tilted-image template
            [symmetry_doc]              ; INPUT: symmetry doc (optional)
            [class_vol]-noflip          ; OUTPUT: reconstruction
            
            @bp3f
            [class-num]               ; PARAMETER: class number
            [class_select_doc]-flip   ; INPUT: selection doc
            [angles_doc]              ; INPUT: angles doc
            [aligned_tilted]          ; INPUT: tilted-image template
            [symmetry_doc]            ; INPUT: symmetry doc (optional)
            [class_vol]-flip          ; OUTPUT: reconstruction
        else
            vm
            echo "Found [class_select_doc]-flip but reconstruction not requested." ; echo
        endif
    endif
lb1
; end class-loop

vm
echo ; echo "Done" ; date

en d

; Modified 2015-08-05
;    2015-07-16 (trs) -- separated from reconstruction procedure for worm hemoglobin, bpwormhb.rct
;    2014-03-06 (trs) -- reading images from stacks
;    2014-03-04 (trs) -- looks for last iteration of multireference alignment
