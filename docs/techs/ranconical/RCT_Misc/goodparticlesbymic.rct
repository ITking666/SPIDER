; GENERATE PARTICLE-LIST FOR EACH MICROGRAPH

; ------------- Input files -------------
fr l
[mic2particle_table]order_picked         ; micrograph-to-particle lookup-table
fr l
[selection_list]listparticles            ; particle list

; ------------- Output files -------------
fr l
[good_dir]Micrographs/good               ; particle-list output directory
fr l
[tot_particle_list][good_dir]/docall***  ; total-particle list
fr l
[good_particle_list][good_dir]/ngood***  ; good-particle list

; ----------- END BATCH HEADER -----------

vm
echo "if(! -d [good_dir]) mkdir [good_dir]"|csh

; get #micrograph-pairs
ud n [num-pairs]
[mic2particle_table]

; loop through micrographs
do lb1 x10=1,[num-pairs]
    ; get micrograph#, first & last particle #s
    ud ic x10,[micro-num],x99,x99,[first-part],[last-part]
    [mic2particle_table]
    
    ; create a list of all particles
    doc create
    [tot_particle_list][micro-num]
    (1)  ; column# to fill
    [first-part]-[last-part]
    
    ; intersect with total particle list
    doc and
    [selection_list]
    [tot_particle_list][micro-num]
    [good_particle_list][micro-num]_unrenum
    (1)  ; column# to intersect

    doc ren
    [good_particle_list][micro-num]_unrenum
    [good_particle_list][micro-num]

    de
    [good_particle_list][micro-num]_unrenum
lb1
; end micrograph-loop

en d
