;----------------------------------------------------------------------------------!
; Adapted from b05.fed/hbf : - after reference free alignment and visual checking
;               - apply solution No.1 or 2. to all original files
;----------------------------------------------------------------------------------!

; ----------------- Parameter -----------------

[solutionNum] = 2   ; solution number 1-4 for final orientation of particles

; ---------------- Input files ----------------
fr l
[parameter_doc]params                          ; parameter doc file
fr l
[selection_list]listparticles                  ; particle list
fr l
[align_dir]Zerodegr/Pairwise                   ; alignment input-output directory
fr l
[untilted_images]Zerodegr/stk2-winrot@*****    ; unaligned-image template
fr l
[align_solution_doc][align_dir]/dsolalign      ; doc file with the two solutions
;      INPLANEANGLE    XSHIFT       YSHIFT
;fr l
;[old_align_doc][align_dir]/docalign            ; untilted-alignment doc
;   KEY       PSI,    THE,    PHI,   REF#,    EXP#,  CUM.{ROT,   SX,    SY},  NPROJ,   DIFF,      CCROT,    ROT,     SX,     SY,   MIR-CC
;;;       MIRRORFLAG     CC_ROT    INPLANEANGLE    XSHIFT       YSHIFT    PARTICLE_NUM
fr l
[old_align_copy][align_dir]/docalign-copy      ; untilted-alignment doc

; ---------------- Output files ----------------
fr l
[new_align_doc][align_dir]/docalign            ; untilted alignment doc file
;   KEY       PSI,    THE,    PHI,   REF#,    EXP#,  CUM.{ROT,   SX,    SY},  NPROJ,   DIFF,      CCROT,    ROT,     SX,     SY,   MIR-CC
;;;;       MIRRORFLAG     CC_ROT    INPLANEANGLE    XSHIFT       YSHIFT    PARTICLE_NUM
fr l
[aligned_stack][align_dir]/stkaligned          ; aligned-image stack (w/o "@", old version named to "*-bak")
fr l
[aligned_untilt][aligned_stack]@*****          ; aligned-image template (check number of digits)
fr l
[master_aligned]Zerodegr/stk3-aligned          ; master aligned-image stack (w/o "@")
fr l
[untilted_avg][align_dir]/avgu001              ; untilted centered average filename template
fr l
[untilted_var][align_dir]/varu001              ; untilted variance template for centered avg

; -------------- END BATCH HEADER --------------

; get window dimension
ud 17,x50
[parameter_doc]
ud e

;x51=(x50/2)+1  ; image-center coordinates

; get rotation and shifts from the designated solution:
ud [solutionNum] ,[solnAngle],[solnXShift],[solnYShift]
[align_solution_doc]
UD E

; clean up pre-existing files
de a
[new_align_doc]

;de
;[aligned_stack]@
VM
mv -v [aligned_stack].$DATEXT [aligned_stack]-bak.$DATEXT

vm
echo "Realigning images" ; date

; get #particles
ud n [numParts]
[selection_list]

; loop through images
do lb5 [partKey] = 1,[numParts]
    ; get particle#
    ud ic [partKey], [partNum]
    [selection_list]

    ; get mirror-flag ([mirrorFlag]), angle, x-shift, y-shift
    ud ic [partNum], x31,x32,x33,x34,x35,[oldAngle],[oldXShift],[oldYShift],x39,x40,x83,x42,x43,x44,[mirrorFlag]
    [old_align_copy]

    ; if mirroring not required, simply sum alignments & apply
    if([mirrorFlag].eq.1) then
        ; sum alignment
        sa p,[oldAngle],[oldXShift],[oldYShift], [solnAngle],[solnXShift],[solnYShift], [newAngle],[newXShift],[newYShift]
        ; (New SA P syntax calls for input parameters to not be entered on the command line, but this syntax still works.)

        ; rotation & centering of raw untilted images
        rt sq
        [untilted_images][partNum]
        [aligned_untilt][partNum]  ; OUTPUT
        [newAngle]                 ; combined angle
        [newXShift],[newYShift]    ; combined x,y-shifts

    ; else invert shifts and then apply+mirror
    else
        mr
        [untilted_images][partNum]
        _3   ; OUTPUT
        Y    ; y-axis mirror

        [oldXShift] = -[oldXShift]
        [oldAngle]  = -[oldAngle]   ; WAS [oldYShift] = -[oldYShift]

        ; sum alignment
        sa p,[oldAngle],[oldXShift],[oldYShift], [solnAngle],[solnXShift],[solnYShift],[newAngle],[newXShift],[newYShift]

        ; rotation & centering of raw untilted images
        rt sq
        _3
        [aligned_untilt][partNum]   ; OUTPUT (temp)
        [newAngle]                  ; combined angle
        [newXShift],[newYShift]     ; combined x,y-shifts
    endif

    ; save new rotation and shifts in an additional alignment doc file
;;;    sd [partKey], [mirrorFlag],x83,[newAngle],[newXShift],[newYShift],[partNum]
    sd [partNum], x31,x32,x33,x34,x35,[newAngle],[newXShift],[newYShift],x39,x40,x83,x42,x43,x44,[mirrorFlag]
    [new_align_doc]
lb5

; close doc files
ud ice  
[old_align_copy]
SD /     MIRRORFLAG     CC_ROT    INPLANEANGLE    XSHIFT       YSHIFT    PARTICLE_NUM
[new_align_doc]
sd e
[new_align_doc]

; copy aligned images to central location (will be overwritten by subsequent alignments)
cp
[aligned_stack]@
[master_aligned]@

; Computation of the average and variance maps of the last cycle
as dc
[aligned_untilt]  ; INPUT: aligned-image template
[selection_list]  ; INPUT: selection file
A                 ; average _A_ll images
[untilted_avg]    ; OUTPUT: average
[untilted_var]    ; OUTPUT: variance

vm
echo "Done" ; date

en d

; Modified 2015-10-02
;    2015-10-02 (trs) -- backs up old aligned images
;    2015-07-30 (trs) -- writes AP SH format alignment doc
;    2015-07-29 (trs) -- operates on backup copy of alignment doc
;    2014-03-05 (trs) -- making master copy of aligned stack
;    2012-09-10 (trs) -- replaced AP MQ (obsolete) with AP SH
;    2009-07-26 (trs) -- alignment doc now has same format as from alignpairwise.spi
;    2009-07-25 (trs) -- checks for mirror, based on grploop.pam, ca. 2004
;    2009-07-25 (trs) -- no longer masks aligned images
;    2009-07-24 (trs) -- keys in AP MQ doc file are assumed to be consecutive (not particle#)
