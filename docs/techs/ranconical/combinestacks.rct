; COMBINES A SERIES OF STACKS (e.g., one per micrograph)
; INTO A SINGLE STACK

; ----- Parameter ------

[numMics] = 816   ; last micrograph number (there can be gaps)

; ------- Input -------
FR L
[input_stack]FromEMAN/winser_*****  ; SPIDER-format stacks (w/o "@")

; ------ Outputs ------
FR L
[combined_stack]Zerodegr/stk1-winrot_backup  ; combined stack w/o "@"
FR L
[particle_list]listparticles                 ; list of particles
;      PARTICLE_NUM  MICROGRAPH

; -- END BATCH HEADER -- 

DE 
[combined_stack]@
DE 
[particle_list]

[partNum] = 0

DO LB1 [micNum] = 1,[numMics]
    ; Check if file exists
    IQ FI [stackExists]
    [input_stack][micNum]@

    IF ([stackExists] .EQ. 0) THEN
        VM
        echo "[input_stack][micNum].$DATEXT doesn't exist"

        GOTO LB1
    ENDIF

    VM
    echo "Working on micrograph {%i4%[micNum]} out of {%i4%[numMics]}"

    ; get #slices
    FI H [numSlices]
    [input_stack][micNum]@
    MAXIM

    ; loop through slices
    DO LB2 [sliceNum] = 1,[numSlices]
        [partNum] = [partNum] + 1

        CP
        [input_stack][micNum]@{****[sliceNum]}
        [combined_stack]@{*****[partNum]}

        SD [partNum], [partNum],[micNum]
        [particle_list]
    LB2
    ; End slice-loop
LB1
; End micrograph-loop

SD /    PARTICLE_NUM  MICROGRAPH
[particle_list]
SD E
[particle_list]

EN D
