; LINK TITLED-SPECIMEN MICROGRAPH WITH CORRESPONDING TILTED-SPECIMEN MICROGRAPH

; --------- Inputs ---------
fr l
[FILENUMS]filenums         ; micrograph list

; --------- Output ---------
fr l
[mic_pair_doc]docmicpairs  ; micrograph-pairing list

; ---- END BATCH HEADER ----

; FILENUMS keys not necessarily consecutive
doc ren
[FILENUMS]
[FILENUMS]_renum

; get number of micrographs
ud n [num-mics]
[FILENUMS]_renum

[num-pairs] = [num-mics]/2

; if not an even number, then end
if ([num-pairs].ne.int([num-pairs])) then
    vm
    echo "Odd number of micrographs. Fix [FILENUMS] and re-start"
endif

de
[mic_pair_doc]
SD /       UNTILTED      TILTED
[mic_pair_doc]

; loop through pairs
do lb1 [pairKey] = 1,[num-pairs]
    ; calculate first key in pair
    [tiltedKey] = [pairKey]*2 - 1

    ; read tilted-specimen micrograph#
    ud ic [tiltedKey], [tiltedNum]
    [FILENUMS]_renum

    ; calculate second key in pair
    [untiltedKey] = [pairKey]*2

    ; read untilted-specimen micrograph#
    ud ic [untiltedKey], [untilted-num]
    [FILENUMS]_renum

    ; write to doc file
    sd [pairKey],[untilted-num],[tiltedNum]
    [mic_pair_doc]
lb1
; end pair-loop

; clean up
ud ice
[FILENUMS]_renum
de
[FILENUMS]_renum

vm
echo "Done -- Paired {***[num-pairs]} micrographs"

en d

; Modified 2015-07-29
