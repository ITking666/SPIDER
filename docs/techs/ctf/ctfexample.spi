 ; 2D CTF CORRECTION

 FT            ; Fourier transform of original image (a projection of a 11.5a ribosome vol)
   image001    ; Input original image
   fft001      ; Output FFT of original image

 TF C          ; Generate complex CTF (i.e. real part= ctf; imaginary part=0)
   ctf001      ; Output complex CTF
   2           ; CS[mm]
   10000,0.025 ; Defocus(a), Lambda(a)
   125         ; Dimension of the 2d array
   0.17        ; Maximum spatial frequency
   0.005,0     ; Source size[1/A], defocus spread[A]
   0,0         ; Astigmatism[a], azimuth[deg]
   .1,10000    ; Amplitude contrast ratio, Gaussian envelope halfwidth
   -1          ; Sign (+1 or -1)

 MU            ; Multipiy ft of original image with ctf
   fft001      ; FT of original image
   ctf001      ; Complex ctf from above
   mu001       ; Result of multiplication
   *

 FT            ; Fourier(inverse) product
   mu001       ; Input fourier file
   dis001      ; Output real space version of convoluted image(="em image")

 MO            ; Create noise image
   noi001      ;
   125, 125    ; Dimensions of output file
   R           ; Random or uniform
   Y           ; Gaussian distribution
   1,200       ; Mean value and standard deviation of the Gaussian distribution 

 AD            ; Add noise to creat more realistic em image
   dis001      ; Noise-free image file from above
   noi001      ; Noise file
   add001      ; Resulting file

 TF CTS        ; CTF correction
   add001      ; Template for 2-d image file
   1           ; File numbers (in this case only one) 
   ctf001      ; 2-d ctf file
   100         ; Signal-to-noise ratio
   res001      ; Restored image file

 FQ            ; Filter result to the bandwidth of the signal
   res001      ; Input file
   fil001      ; Output file
   5           ; Filter type = fermi low-pass
   0.25        ; Filter radius
   0.05        ; Temperature parameter for the fermi filters 

 EN
