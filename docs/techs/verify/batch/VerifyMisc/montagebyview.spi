; CREATE MONTAGE OF GOOD PARTICLES 
; AND OPTIONALLY SAVE AS TIFF
;
; REQUIRES mnsfromdoc.spi

; ------------------- Parameters -------------------
[imgs-row]    = 2   ; images per row
[save-spider] = 0   ; save as SPIDER? (1==save)
[save-tiff]   = 1   ; save as TIFF? (1==save)

; --------------------- Inputs ---------------------
fr l
[view_select]../Reconstruction/select/docgoodviews  ; view-selection doc
fr l
[prj_dir]../Reconstruction/select/prj{***x80}       ; view directory-pattern
fr l
[particle_select][prj_dir]/newgoodsel               ; particle-selection doc
fr l
[particle_template][prj_dir]/stkfilt@*****          ; particle template

; --------------------- Outputs ---------------------
fr l
[spider_montage]viewmontage_{***x80}                ; SPIDER montage
fr l
[tiff_montage]viewmontage_{***x80}.tif              ; TIFF montage

; ---------------- END BATCH HEADER ----------------

; get #views
ud n [num-views]
[view_select]

; loop through views
do lb8 x81 = 1,[num-views]
    ; get projection# (x80) from selection doc
    ud ic x81,x80
    [view_select]
    
    ; check if doc file exists
    iq fi [doc-exists]
    [particle_select]
    
    ; if exists then...
    if([doc-exists].eq.1) then
        vm
        echo "Working on view #{***x80}, {***x81} out of {***[num-views]}"

        ; create montage
        @mnsfromdoc
        [particle_select]    ; particle-selection doc
        [particle_template]  ; image template
        [imgs-row]           ; images per row
        [save-spider]        ; save as SPIDER? (1==Y)
        [spider_montage]     ; SPIDER output (optional)
        [save-tiff]          ; save as TIFF? (1==Y)
        [tiff_montage]       ; TIFF output (optional)
    else
        vm
        echo "Doc file not found for view #{***x80}"
    endif
lb8
; end view-loop

ud ice
[view_select]

en d

; Modified 2009-03-13

