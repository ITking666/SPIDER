; GENERATES GOODCLASSES FILE
; (IN CASE YOU FORGOT TO SAVE ONE OR MORE)

; ---------------- Inputs ----------------
fr l
[ref_view_list]../Alignment/projlist      ; list of projection views (optional -- won't loop w/o) 
fr l
[prj_dir]select/prj{***x80}               ; subdirectory template, for each reference-view
fr l
[byhand_list][prj_dir]/byhand             ; hand-picked list, for each class
fr l
[tot_class_list][prj_dir]/classes_by_ccc  ; list of classes w/class number in 1st column

; ---------------- Output ----------------
fr l
[good_classes_list][prj_dir]/goodclasses  ; for each reference-view

; ----------- END BATCH HEADER -----------

x11=1  ; dummy variable

; check if view-list exists
iq fi x81
[ref_view_list]

; if it exists...
if(x81.eq.1) then
    ; then get number of reference-views (x83)
    ud n,x83
    [ref_view_list]
else
    x83=1
endif

; loop through reference-views
do lb1 x80=1,x83
    de
    [good_classes_list]

    x17=0  ; initialize good-class counter

    ; get number of classes (x20)
    ud n,x20
    [tot_class_list]

       ; loop through good classes
        do lb5 x21=1,x20
            ; get class number (x16)
            ud ic,x21,x16
            [tot_class_list]

            ; check if by-hand list exists
            iq fi x28
            [byhand_list]{***x16}

            if(x28.eq.1) then
                x17=x17+1

                sd x17,x16,x11
                [good_classes_list]
            endif
    lb5
    ; end class-loop

    ; clean up
    sd e
    [good_classes_list]
    ud ice
    [tot_class_list]
lb1
; end view-loop

ud ice
[ref_view_list]

en d
