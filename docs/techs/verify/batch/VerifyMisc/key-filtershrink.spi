;PFILT.BAT  apply low pass filter to and shrinks a set of particle images

; PARAMETERS:
; NOTE: filter radii are in reciprocal pixels on the full-size image
x11 = 7      ; filter type (7==Butterworth)
x21 = 0.075  ; filter parameter 1 (pass-band if Butterworth, else filter radius)
x22 = 0.10   ; filter parameter 2 
             ; stop band for Butterworth, temperature for Fermi, else not required
x26 = 2      ; reduction/decimation factor (integer)

; INPUTS:
fr l
[filenums]../filenums
fr l
[neg_particle_table]../Particles/order
fr l
[unfiltered]ali/sar

; OUTPUT:
fr l
[filtered]../Particles/flt/flt

; END BATCH HEADER

md
set mp
0

; get the filenumbers
ud n,x19
<filenums>

DO LB2 x10=1,x19
    ud x10,x55
    <filenums>
    ; x55 is now the micrograph number

    vm
    echo "On micrograph #{***x55}, {***x10} out of {***x19}"; date

    ud x55,x91,x77,x76,x78,x79
    [neg_particle_table]
    ; x77==num. particles, x78==first particle, x79==last particle

    ; loop through particles
    do lb1 x44=x78,x79  ; x80=1,x77
;        x44=x78+x80-1  
;        ; x44==cumulative particle number, x78==first particle in micrograph

        IF (x11.eq.7) THEN
            FQ np
            [unfiltered]{******x44}  ; input file
            _4                ; output file
            x11               ; filter type
            x21,x22           ; passband, stopband
        endif
        IF (x11.eq.5) THEN
            FQ np
            [unfiltered]{******x44}  ; input file
            _4                ; output file
            x11               ; filter type = Fermi
            x21               ; filter radius
            x22               ; temperature cutoff
        endif
        IF (x11.le.3) THEN
            FQ np
            [unfiltered]{******x44}  ; input file
            _4                ; output file
            x11               ; filter type = top hat, Gaussian
            x21               ; filter radius
        ENDIF

        dc s
        _4
        [filtered]{******x44}
        x26,x26  ; reduction factor in x,y
    lb1  ; end particle loop
LB2  ; end micrograph loop

vm
echo "Done"; date

EN D

; Modified 2004-05-06
;     2004-02-10 (trs) -- gets reduction factor as parameter
