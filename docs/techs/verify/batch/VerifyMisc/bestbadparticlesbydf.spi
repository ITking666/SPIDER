; GENERATE BEST BAD-PARTICLE LIST
; WILL ATTEMPT TO USE SAME NUMBER AS GOODPARTICLES

; --------------------- Input ---------------------
fr l
[df_group_list]../Reconstruction/sel_group_cclim  ; defocus-group selection list
fr l
[df_dir]../Reconstruction/df***                          ; input-output defocus-group directory
fr l
[bad_particle_list]badparticles                   ; bad-particle list, in DF_DIR
;     GROUP_SLICE   PARTICLE       CCROT
fr l
[good_particle_list]goodparticles                 ; good-particle list, in DF_DIR

; -------------------- Output --------------------
fr l
[best_bad_particle_list]bestbadparticles          ; best-bad-particle list, in DF_DIR
;     GROUP_SLICE   PARTICLE       CCROT

; --------------- END BATCH HEADER ---------------

; get #groups
ud n [num-groups]
[df_group_list]

; loop through defocus-groups
do lb7 [group-key] = 1,[num-groups]
    ; get group#
    ud ic [group-key],[group-num]
    [df_group_list]
    
    ; sort bad particles
    doc sort
    [df_dir][group-num]/[bad_particle_list]
    [df_dir][group-num]/[bad_particle_list]_sort
    (3)  ; column# to sort: ccrot
    Y    ; renumber keys?
    
    de
    [df_dir][group-num]/[[best_bad_particle_list]
    SD /    GROUP_SLICE   PARTICLE       CCROT
    [df_dir][group-num]/[best_bad_particle_list]
    
    ; get #bad-particles
    ud n [num-bad]
    [df_dir][group-num]/[bad_particle_list]
    
    ; get #good-particles
    ud n [num-good]
    [df_dir][group-num]/[good_particle_list]
    
    ; if more bad particles than good, keep #good
    if([num-bad].gt.[num-good]) then
    [num-keep]=[num-good]
    ; if fewer bad particles than good, keep all
    else
    [num-keep]=[num-bad]
    endif
    
    ; loop through particles
    do lb5 x55 = 1,[num-keep]
        ; start from end
        x45 = [num-bad] - x55 + 1

        ; read particle info
        ud ic x45,[slice-num],[particle-num],[ccrot]
        [df_dir][group-num]/[bad_particle_list]_sort
        
        ; write to output doc
        sd x55,[slice-num],[particle-num],[ccrot]
        [df_dir][group-num]/[best_bad_particle_list]
    lb5
    ; end particle-loop
    
    ; close docs
    ud ice
    [df_dir][group-num]/[bad_particle_list]_sort
    de
    [df_dir][group-num]/[bad_particle_list]_sort
    sd e
    [df_dir][group-num]/[best_bad_particle_list]
    
    vm
    echo "Kept {*****[num-keep]} bad versus {*****[num-good]} good particles"
lb7
; end group-loop

ud ice
[df_group_list]

en d
