; SEPARATES GOOD PARTICLES BY DEFOCUS-GROUP

; ------------ Parameter ------------
x56 = 2  ; prints progress message to screen every (x56)th defocus-group

; ------------- Inputs -------------
fr l
[good_list_by_view]../Particles/good/sel***     ; good-particle list file pattern, for each reference view
fr l
[ref_view_list]../Alignment/projlist            ; list of reference projections
fr l
[df_list]../Power_Spectra/order_defgrps         ; list of defocus-group numbers
fr l
[tot_particle_list]../Alignment/part_by_group_  ; file pattern for list of all particles, for each defocus-group

; ------------- Outputs -------------
fr l
[combined_good_list]combinedgood    ; combined list of good particles
fr l
[good_dir]df                        ; directory template where defocus-group lists will go
fr l
[df_good_list]seltotal              ; in GOOD_DIR, for each defocus-group
fr l
[df_odd_list]selodd                 ; for each defocus-group, in GOOD_DIR
fr l
[df_even_list]seleven               ; for each defocus-group, in GOOD_DIR
fr l
[particles_per_group]order_defocus  ; doc showing particles per defocus group

; -------- END BATCH HEADER --------

de  ; clean up
[particles_per_group]

vm
echo "Making combined, good-particle list"; date

; combine "good" selection lists for all reference views
doc combine
[good_list_by_view]
[ref_view_list]
tmp_combined_good_list

doc sort
tmp_combined_good_list
[combined_good_list]  ; OUTPUT
1                     ; register to sort by
Y                     ; renumber?

de
tmp_combined_good_list

; get number of good particles (x83)
ud n,x83
[combined_good_list]

vm
echo "Making good-particle list for each defocus-group"; date

x63=0  ; initialize particle-counter

; get number of defocus-groups (x76)
ud n,x76
[df_list]

; loop through defocus-groups
do lb7 x70=1,x76
    ; get current defocus-group number (x71), defocus (x72)
    ud ic,x70,x71,x72
    [df_list]

    vm
    echo "if(! -d [good_dir]{***x71}) mkdir [good_dir]{***x71}"|csh

    if (int(x70/x56).eq.x70/x56) then
        vm
        echo "Working on {***x70}th defocus-group out of {***x76}"
    endif

    ; find union of negative-particle list and combined-good list
    doc and
    [tot_particle_list]{***x71}
    [combined_good_list]
    [good_dir]{***x71}/[df_good_list]  ; OUTPUT
    (1)                                  ; register# to check

    doc split
    [good_dir]{***x71}/[df_good_list]  ; INPUT: total selection file
    [good_dir]{***x71}/[df_odd_list]   ; OUTPUT: odd selection file
    [good_dir]{***x71}/[df_even_list]  ; OUTPUT: even selection file

    ; get number of good particles (x62)
    ud n,x62
    [good_dir]{***x71}/[df_good_list]

    x63=x63+x62  ; increment good-particle counter

    sd x71,x62,x63,x72
    [particles_per_group]
    ; x62==#particles, x63==#cum.particles, x72==defocus
lb7
; end group-loop

vm
echo "Total particles in selection docs: {******x83}; total particles sorted by defocus-group: {******x63}"

ud ice  ; close document
[df_list]

vm
echo "Done"; date

en d

; Modified 2004-12-31
;    2004-12-30 (trs) -- creates order_defocus
