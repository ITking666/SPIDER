; SHRINKS MICROGRAPHS

; ------------- Parameters -------------
[progress-interval] = 3  ; progress message written every Nth micrograph
[decimation-factor] = 9  ; reduction factor

; --------------- Inputs ---------------
fr l
[FILENUMS]../filenums               ; micrograph list
fr l
[input_micros]mic{****[micro-num]}  ; micrographs

; --------------- Output ---------------
fr l
[small_micros]sm-mic{****[micro-num]}  ; reduced micrographs

; ---------- END BATCH HEADER ----------

; get #micrographs
ud n [num-micros]
[FILENUMS]

vm
echo "Shrinking micrographs"; date

; loop through micrographs
do lb1 [index] = 1,[num-micros]
    ; get micrograph#
    ud ic [index], [micro-num]
    [FILENUMS]

    if (int([index]/[progress-interval]).eq.[index]/[progress-interval]) then
        vm
        echo "Working on micrograph #{****[micro-num]}, {****[index]} out of {****[num-micros]}"
    endif

    ; reverse contrast
    dc s
    [input_micros]
    _1  ; WAS [small_micros]
    [decimation-factor],[decimation-factor]

    ; get average, s.d.
    fs
    _1  ; INPUT: shrunken micrograph
    fi [img-avg],[img-sd]
    _1
    (9,10)  ; header positions for avg, s.d.

    ; normalize
    ar
    _1
    [small_micros]  ; OUTPUT
    (p1-[img-avg])/[img-sd]
lb1
; end micrograph-loop

ud ice
[FILENUMS]

vm
echo "Done"; date

en d

; Modified 2009-05-05
;    2009-04-14 (trs) -- normalizes shrunken micrographs
