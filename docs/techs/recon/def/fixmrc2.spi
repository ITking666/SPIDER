 ; SOURCE:   spider/docs/techs/recon/def/fixmrc2.spi 
 ;
 ; PURPOSE : Cut and paste the Spider-generated power spectrum 
 ;           onto the model file generated by ctffind

 ; ----------- Input files ---------------

 [num] = 4078                        ; Micrograph number

 [mrc] = 'output/powchk{****[num]}'  ; Power spectrum file generated by ctffind2
            
 [spi] = 'output/pw_avg{****[num]}'  ; Power spectrum file generated by Spider

 ; ----------- Output files --------------

 [out] = 'output/fix{****[num]}'     ; SPIDER PS file

 ; -------------------- END BATCH HEADER -----------------------

 FI H [nsam],[nrow]    ; Get SPIDER PS image size
   [spi]               ; Spider PS     (input)
   NSAM,NROW

 ; Estimate background and subtract it from 
 ; the power spectrum for better visualization

 [nsamd2] = ([nsam]/2)

 FQ                    ; Quick filter
   [spi]               ; Spider PS     (input)
   _2   
   7                   ; Filter type (BW LP)
   0.002,0.025         ; Passband, stopband

S U                    ; Subtract
   [spi]               ; Spider PS      (input)
   _2                  ;                (input)
   _3                  ;                (output)
   *

 MA                    ; Mask
   _3                  ; Filtered file           (input)
   _4                  ; Filt file center masked (output)
   0.0,35              ; Radius of central dot
   D                   ; Disk
   E                   ; External background
   0.0                 ; Center coord.
   [nsamd2]+1,[nsamd2]+1 

 ; Resize and recale the background -subtracted spider image

 FI H [nsam],[nrow]    ; Get image size
   [mrc]               ; MRC PS        (input)
   NSAM,NROW           ; Header locs

 [xc] = ([nsam]/2) + 1  ; Image center
 [yc] = ([nrow]/2) + 1


 IP                   ; Rescale image
   _4                 ;                (input)
   _1                 ;                (output)
   [nsam],[nrow]             ; Scaling factor

 WI                   ; Extract left half of mrc image
   [mrc]              ; MRC PS         (input)
   _5
   [nsam]/2, [nrow]   ; Dimensions
   1,1                ; Upper left coordinates

 FS [fmax],[fmin]     ; Get max & min values
   _5                 ;                (input)

 WI                   ; Extract right half of spider image
   _1                 ;                (input)
   _2                 ;                (output)
   [nsam]/2, [nrow]   ; Dimensions
   [xc],1             ; Upper left coordinates

 [v40] = [fmax] - [fmin]    ; Range of values
 [v41] = [fmax] + ([v40]/2) ; Rescaled max
 [v42] = [fmin] - ([v40]/2) ; Rescaled min

 AR SCA              ; Rescale the spider window
   _2                ;                (input)
   _3                ; Rescaled file  (output)
   [v42],[v41]

 CP                  ; Copy
   [mrc]             ; MRC PS         (input)
   [out]             ; Enhanced PS    (output)

 IN                  ; Insert image
   _3                ; Rescaled file  (input)
   [out]             ; Enhanced PS    (output)
   [xc],1

 VM                  
   echo ' '  ; echo " Created [out].$DATEXT" ; echo ' ' 

 EN

