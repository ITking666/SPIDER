; <html><head><title>Prepare particle stack files and alignment parameters file</title></head><body><pre>
;
; SOURCE: stack.spi      Merged with sel_align.spi Feb 2005 al
;
; PURPOSE: Prepare stack files that contain the selected 
;          original & aligned particles and doc files with
;          corresponding alignment parameters. 
;
; MASTER COPY: /net/bali/usr1/spider/docs/techs/recon/newprogs/
;
; I/O PARAMETERS AND FILES ARE SET HERE:
;
; <b> ------------ Input files ---------------------------------------

[defocus_group_sel] = '../Power_Spectra/order_defgrps3'     ; Defocus groups selection file

[part_by_group] = '../Reconstruction/df{***[grp]}/seltotal' ; Particle selection file by defocus group

[ser] = '../Particles/win/ser{******[p]}'                   ; Original particle images

[ali] = '../Alignment/ali/sar{******[p]}'                   ; Aligned particle images 

[align_parameters] = '../Alignment/align_01_{***[grp]}'     ; Alignment parameter file template

; --------------- Output files  -------------------------------------

[data] = 'input/data{***[grp]}@{******[j]}'                 ; Stack files with original images

[dala] = 'input/dala01_{***[grp]}@{******[j]}'              ; Stack files with aligned  images

[new_align_parameters] = 'input/align_01_{***[grp]}'        ; Alignment file template

; -------------- END BATCH HEADER ------------------------------</b>

MD
TR OFF                          ; Decreases results file output
MD
VB OFF                          ; Decreases results file output

UD N [n]                        ; Get number of defocus groups 
[defocus_group_sel]

DO [i]=1,[n]                    ; Loop over all defocus groups

   ; Get current group number 
   UD [i],[grp]                 ; Current defocus group number = x77
   [defocus_group_sel]          ; Defocus group selection file
 
   UD N [np]                     ; Get number of particles in this group
   [part_by_group]

   DO [j]=1,[np]                ; Loop over all particles in group 

      UD IC [j], [p]            ; Get current particle number=x55
      [part_by_group]

      CP                        ; Copy original image to data stack
      [ser]                     ; Original image for this particle                    
      [data]

      CP                        ; Copy aligned image to dala stack
      [ali]                     ; Aligned image for this particle
      [dala]

      UD IC [p], [r1],[r2],[r3],[r4],[r5],[r6],[r7],[r8],[r9],[r10],[r11],[r12],[r13],[r14],[r15]  ; Get alignment parameters for particle
      [align_parameters]        ; Old alignment file template

      SD    [j], [r1],[r2],[r3],[r4],[r5],[r6],[r7],[r8],[r9],[r10],[r11],[r12],[r13],[r14],[r15]  ; Save alignment parameters for particle
      [new_align_parameters]    ; New alignment file template
   ENDDO

   ; Close doc file accesses
   UD ICE                       ; Finished with this defocus group
   [part_by_group]

   UD ICE  
   [align_parameters]          

   SD E
   [new_align_parameters]    
ENDDO

EN
; </body></pre></html>
