head	1.5;
access;
symbols
	GPL_update:1.1;
locks; strict;
comment	@# @;


1.5
date	2012.08.31.15.41.59;	author leith;	state Exp;
branches;
next	1.4;

1.4
date	2012.08.31.15.40.33;	author leith;	state Exp;
branches;
next	1.3;

1.3
date	2012.08.29.16.05.07;	author leith;	state Exp;
branches;
next	1.2;

1.2
date	2012.08.29.14.30.59;	author leith;	state Exp;
branches;
next	1.1;

1.1
date	2010.01.19.19.06.52;	author leith;	state Exp;
branches;
next	;


desc
@   co -u  recon.spi
   chmod ug+w  recon.spi
   chmod +r    recon.spi
   mv  old/recon.spi   jnkdir/recon.spi
@


1.5
log
@removed debug info
@
text
@; <html><head><title>Computes Initial Reconstruction</title></head><body><pre>
;
; SOURCE: spider/docs/techs/recon/newprogs/recon.spi 
;         'FSC', 'BP RP 3'                         ArDean Leith  Aug 2012
;
; PURPOSE: Computes initial reconstruction using multiple defocus groups.
;          For each defocus group in the reconstruction:
;             Divides particle data randomly into two subsets.
;             Computes two subset reconstructions for the group reconstructions.
;             Computes Fourier Shell Correlation curve for the group.
;             Creates CTF correction file for the group reconstruction.
;          For overall reconstruction:
;          Applies  CTF correction to the defocus group subset volumes.
;          Combines all defocus group subset volumes.
;          Computes resolution of the combined reconstruction at 0.5 threshold.
;
; I/O PARAMETERS AND FILES ARE SET HERE:
;
; --------------------- Parameters -----------------------------------

 [rad]        = -1        ; Radius of restored object (Use: -1 for 95% winsize / 2)
 [bpweight]   = 0.2e-5    ; Back projection weighting factor 
 [bplimit]    = 0.0       ; Back projection correction limit
 [iterlimit]  = 60        ; Back projection iteration limit
 [snr]        = 3         ; SNR for CTF correction using 'TF CTS'

 [do_bp]      = 1         ; Skip group back projections    (if = zero)
 [do_combine] = 1         ; Skip computing combined volume (if = zero)

 ; -------------------- Input files -----------------------------------

 [params]   = '../params'                             ; Reconstruction parameter file

 [sel_grp]  = 'sel_group_cclim'                       ; Defocus group selection file
 [sel_part] = 'sel_particles_{***[grp]}'              ; Particle selection files (one / defocus group)

 [ali]      = '../Alignment/dala01_{***[grp]}@@******' ; Aligned particle images (one stack / defocus group)
 [angles]   = '../Alignment/align_01_{***[grp]}'      ; Alignment parameter doc files (one / defocus group)

 ; -------------------------- Output files ---------------------------

 [vol_sub1] = 'vol01_sub1'                   ; Volume created from subset 1 particles (one)
 [vol_sub2] = 'vol01_sub2'                   ; Volume created from subset 2 particles (one)

 [grpvol_1] = 'df{***[grp]}/[vol_sub1]'      ; Volumes for subset 1 particles (one per group)
 [grpvol_2] = 'df{***[grp]}/[vol_sub2]'      ; Volumes for subset 2 particles (one per group)
 [grpfsc]   = 'df{***[grp]}/fscdoc'          ; FSC curve doc  files (one per group)
 [ctf]      = 'df***/ctffile'                ; CTF correction files (one per group)

 [vol]      = 'vol01'                        ; CTF-corrected overall volume  
 [combfsc]  = 'combires'                     ; FSC curve doc  file for overall volume
 [res_file] = 'resolution'                   ; Resolution doc file for all volumes

 ; -------------- END BATCH HEADER -------------------------------------------</b>

 MD
   TR OFF                         ; Decrease results file output
 MD
   VB OFF                         ; Decrease results file output
 MD 
   SET MP 
   0                              ; 0 = use all available processors  

 ; Get parameters from reconstruction parameter file -----------------------------
 UD 5,[sp_pixsiz]                 ; Get pixelsize (A)  
   [params]                       ; Parameter doc file      (input)
 UD 6,[sp_kev]                    ; Electron energy (kV)
   [params]                       ; Parameter doc file      (input)
 UD 7,[sp_sph_abb]                ; Spherical Abberation
   [params]                       ; Parameter doc file      (input)
 UD 8,[sp_sourcesiz]              ; Source size
   [params]                       ; Parameter doc file      (input)
 UD 9,[sp_def_spr]                ; Defocus spread
   [params]                       ; Parameter doc file      (input)
 UD 12,[sp_acr]                   ; Amplitude contrast ratio
   [params]                       ; Parameter doc file      (input)
 UD 13,[sp_geh]                   ; Gaussian envelope
   [params]                       ; Parameter doc file      (input)
 UD 14,[sp_lambda]                ; Lambda (electron wavelength)
   [params]                       ; Parameter doc file      (input)
 UD 15,[sp_maxspfreq]             ; Max. spatial frequency
   [params]                       ; Parameter doc file      (input)
 UD 17,[sp_winsiz]                ; Window size
   [params]                       ; Parameter doc file      (input)

 ; Calculate lambda (A) from electron voltage now 
 [sp_lambda] = 12.398 / SQR([sp_kev] * (1022.0 + [sp_kev]))

 IF ([rad] .EQ. -1) THEN          ; Check reconstruction radius
   [rad] = INT( (0.95*[sp_winsiz])/2.0 )
 ENDIF

 IF ([do_bp].LE.0) GOTO LB98      ; Can skip group back-projections

 [grploop] = 1                    ; Set flag for looping over groups

 ; ----------------------  Extract PubSub group number (When given) ------------
 IF ([grp] .GT. 0) THEN           ; Group # sent on invocation line
   UD FIND [key],[grp],[p],[def]  ; Find defocus value for given group   
     [sel_grp]                    ; Group selection file       (input)
     1,[grp]                      ; Search col. & value

   UD FIND E                      ; End doc file use   
     [sel_grp]                    ; Group selection file       (ends)
   [grploop]    = 0               ; Set flag for NOT looping over groups

 ELSEIF ([grp] .LT. 0) THEN       ; Negative group # sent on invocation line
    GOTO LB98                     ; Skip computing group back-projections 

 ELSE
    ; Make header for  resolution doc file
    SD /        GROUP,       NORM FREQ,  RESOLUTION (ANG.)   (CUTOFF=50%) 
      [res_file]                  ; Resolution doc file      (output)
    VM
      echo  ' '"Commencing back projection, please wait." ; echo  ' '    

 ENDIF

 MY FL

 DO   ; -----------------------------  Loop over all defocus groups ---------------

   IF ([grploop] .GT. 0) THEN        ; Loop over defocus groups 
      UD NEXT [key],[grp],[p],[def]  ; Get group from group sel. file
        [sel_grp]                    ; Group selection file          (input)
      IF ([key] .LE. 0) EXIT         ; End of groups in doc file
   ENDIF

   VM                                ; Create output dir if needed
     mkdir -p df{***[grp]}          

   MY FL

md
set seed
12345

   ; Compute the group subset reconstructions. --------------------------------
   BP RP 3 [niter]                ; [niter] returns number of iterations used
     [ali]                        ; Template for input image files  (input)
     [sel_part]                   ; Selection file for images       (input)
     [rad]                        ; Radius of restored object
     [angles]                     ; Angles doc file                 (input)
     *                            ; Symmetries doc file (* = none)  (input)
     *                            ; NO overall reconstructed volume (none)
     [grpvol_1]                   ; Reconstructed subset volume 1   (output)
     [grpvol_2]                   ; Reconstructed subset volume 2   (output)
     [bpweight],[bplimit]         ; Weight lambda, limit
     [iterlimit], 0               ; Iteration limit, constraint mode
     .5, .5                       ; Minimum, maximum constraints
     .5                           ; Smoothing constant (unused value)

   MY FL                          ; Flush results file

   ; Compute  Fourier Shell Correlation for group reconstruction -----------------
   FSC [half],[spfreq],[res]      ; Find phase residual &  shell correl.
     [grpvol_1]                   ; First input volume              (input)
     [grpvol_2]                   ; Second input volume             (input)
     0.5                          ; Shell width
     [sp_pixsiz]                  ; Pixel size
     [grpfsc]                     ; FSC doc file                    (output)
     *                            ; No Gnuplot file
   VM                                      
      echo  ' 'Group: {****[grp]}'  'Defocus: {%f7.0%[def]}'  'Particles: {******[p]}'  'Resolution: {%f7.2%[res]}    

   ; Save group reconstruction resolution in doc file  
   SD [grp], [grp],[spfreq],[res]
     [res_file]                   ; Resolution doc file             (output)
   SD E 
     [res_file]                   ; Resolution doc file             (closed)

   MY FL

   TF C3                         ; Create CTF correction file for this group ----
     [ctf][grp]                  ; CTF correction file               (output)
     [sp_sph_abb]                ; Spherical abberation
     [def],[sp_lambda]           ; Defocus, electron wavelength 
     [sp_winsiz]                 ; Dimension of volume
     [sp_maxspfreq]              ; Max. spatial frequency
     [sp_sourcesiz],[sp_def_spr] ; Source size, defocus spread
     0, 0                        ; Astigmatism, azimuth
     [sp_acr],[sp_geh]           ; Amplitude contrast ratio, Gaussian halfwidth
     -1                          ; Sign

   IF ([grploop] .LE. 0) GOTO LB99 ; For handling single defocus group only

 ENDDO                           ; End of defocus group loop -----------------------


 UD NEXT END                     ; Finished  with group selection file
   [sel_grp]                     ; Group selection file         

 VM                                      
   echo ' '

 LB98                            ; Entry point after skipping back projections

 IF ([do_combine].LE.0) GOTO LB99  ; Can skip combination & resolution 

 ; Apply CTF correction to the defocus group subset volumes  -----------------------
 TF CTS                          ; CTF correction 
   df***/[vol_sub1]              ; Template for image file      (input)
   [sel_grp]                     ; Group selection file          (input)
   [ctf]                         ; Template for ctf file         (input)
   [snr]                         ; SNR
   _1                            ; Temp. inline file             (output)

 TF CTS
   df***/[vol_sub2]              ; Template for image file       (input)
   [sel_grp]                     ; Group selection file          (input)
   [ctf]                         ; Template for ctf file         (input)
   [snr]                         ; SNR
   _2                            ; Temp. inline file             (output)

 ; Add  CTF corrected subset volumes to get combined overall volume   --------------
 AD                              ; Add  volumes
   _1                            ; Volume                        (input)
   _2                            ; Volume                        (input)
   [vol]                         ; Volume                        (output) 
   *                             ; Finished adding

 ; Compute combined FSC resolution curve ------------------------------------------
 FSC [half],[spfreq],[res]       ; Find phase residual &  shell correl.
   _1                            ; First sample volume           (input)
   _2                            ; Second sample volume          (input)
   0.5                           ; Shell width
   [sp_pixsiz]                   ; Pixel size
   [combfsc]                     ; FSC doc. file                 (output)
   *                             ; No Gnuplot file
 VM                                      
   echo ' 'Reconstruction: [vol].$DATEXT'   'Resolution: {%f5.2%[res]} Angstroms  ; echo ' '

 [key] = [grp]+1
 if ([grp] < 0) [key] = 999

 [grt] = 0
 SD // '      OVERALL,    NORM FREQ,    RESOLUTION (ANG.)   (CUTOFF=50%)' 
   [res_file]                    ; Resolution doc file        (output)

 SD [key],[grt][spfreq],[res]    ; Put resolution in file
   [res_file]                    ; Resolution doc file        (output)


 LB99
 EN 

; </body></pre></html>
@


1.4
log
@[key] = [grp]+1
@
text
@a247 11


 Group: 0001  Defocus: 16677.  Particles: 001214  Resolution: 24.38
 Group: 0006  Defocus: 21506.  Particles: 001724  Resolution: 29.89
 Group: 0012  Defocus: 26444.  Particles: 001933  Resolution: 29.96
 Group: 0015  Defocus: 31202.  Particles: 001213  Resolution: 22.62

 Reconstruction: vol01.pic   Resolution: 21.98 Angstroms



@


1.3
log
@'FSC', 'BP RP 3'
@
text
@d233 3
a235 1
 [key] = -99
d237 1
a237 1
 SD // '          OVERALL,    NORM FREQ,    RESOLUTION (ANG.)   (CUTOFF=50%)' 
@


1.2
log
@'FSC', 'BP RP 3' rewrite
@
text
@d11 1
a11 1
;             Creaates CTF correction file for the group reconstruction.
a92 4
 ; Make header for  resolution doc file
 SD /    GROUP,  NORM'D FREQ,  RESOLUTION (ANG.)   (CUTOFF=50%) 
   [res_file]                     ; Resolution doc file      (output)

d106 2
a107 1
 ELSEIF ([grp] .LT. 0) THEN       ; Negative group # sent on invokation line
d109 1
d111 3
d169 2
d233 1
a233 1
 [key] = [grp] + 1
d235 1
a235 1
 SD /    OVERALL,  NORM'D FREQ,  RESOLUTION (ANG.)   (CUTOFF=50%) 
d238 1
a238 1
 SD [key],[grt][spfq],[res]      ; Put resolution in file
@


1.1
log
@simplify-rewrite
@
text
@d4 1
d8 4
a11 5
;             Divides particle data into two sets, odd vs even-numbered particles.
;             Computes odd and even sample 3D reconstructions.
;             Computes Fourier Shell Correlation for each group reconstruction.
;             Computes CTF correction file for each group reconstruction.
;             Apply CTF correction to the defocus group odd/even sample volumes
d13 2
a14 2
;          Applies  CTF correction to the defocus group odd/even sample volumes.
;          Combines defocus group odd/even sample volumes.
d21 64
a84 66
[rad]        = -1        ; Radius of restored object (Use: -1 for 95% winsize / 2)
[bplambda]   = 0.2e-5    ; Back projection weighting lambda 
[climit]     = 0.0       ; Back projection correction limit
[iters]      = 60        ; Back projection iteration limit
[snr]        = 3         ; SNR for CTF correction using 'TF CTS'

[do_bp]      = 1         ; Skip group back projections    (if = zero)
[do_combine] = 1         ; Skip computing combined volume (if = zero)

; -------------------- Input files -----------------------------------

[params]     = '../params'                             ; Reconstruction parameter file

[sel_grp]    = 'sel_group_cclim'                       ; Defocus group selection file
[sel_part]   = 'sel_particles_{***[grp]}'              ; Particle selection files (one / defocus group)

[ali]        = '../Alignment/dala01_{***[grp]}@@******' ; Aligned particle images (one stack / defocus group)
[angles]     = '../Alignment/align_01_{***[grp]}'      ; Alignment parameter doc files (one / defocus group)

; -------------------------- Output files ---------------------------

[selodd]     = 'df{***[grp]}/sel_odd'         ; Selection files for odd-numbered particles (one / defocus group)
[seleven]    = 'df{***[grp]}/sel_even'        ; Selection files for even-numbered particles (one / defocus group)

[volodd]     = 'vol01_odd'                    ; Volume created from "odd" particles
[voleven]    = 'vol01_even'                   ; Volume created from "even" particles

[grpvolodd]  = 'df{***[grp]}/[volodd]'        ; Volumes for even-numbered particles (one per group)
[grpvoleven] = 'df{***[grp]}/[voleven]'       ; Volumes for odd-numbered particles (one per group)
[grpfsc]     = 'df{***[grp]}/fscdoc'          ; FSC doc files (one per group)
[ctf]        = 'df***/ctffile'                ; CTF correction files (one per group)

[vol]        = 'vol01'                        ; CTF-corrected combined volume  
[combfsc]    = 'combires'                     ; FSC doc file with FSC curve for combined volume
[res_file]   = 'resolution'                   ; Resolution doc file for combined volume

[tmpfsc]     = 'jnktmpfsc'                    ; Temp. FSC file (deleted)

; -------------- END BATCH HEADER -------------------------------------------</b>


MD
TR OFF                            ; Decrease results file output
MD
VB OFF                            ; Decrease results file output
MD 
SET MP 
0                                 ; 0 = use all available processors  

; Get parameters from reconstruction parameter file -----------------------------
UD 7,[sp_sph_abb]                 ; Spherical Abberation
[params]
UD 8,[sp_sourcesiz]               ; Source size
[params]
UD 9,[sp_def_spr]                 ; Defocus spread
[params]
UD 12,[sp_acr]                    ; ACR
[params]
UD 13,[sp_geh]                    ; Gaussian envelope
[params]
UD 14,[sp_lambda]                 ; Lambda
[params]
UD 15,[sp_maxspfreq]              ; Max. spatial frequency
[params]
UD 17,[sp_winsiz]                 ; Window size
[params]
d86 4
a89 1
IF ([rad] .EQ. -1) THEN           ; Check reconstruction radius
d91 1
a91 1
ENDIF
d93 3
a95 3
; Make header for  resolution doc file
SD /    GROUP,  NORM'D FREQ,  RESOLUTION (ANG.),  (CUTOFF=50%) 
[res_file]
d97 1
a97 1
IF ([do_bp].LE.0) GOTO LB98       ; Can skip group back-projections
d99 1
a99 1
[grploop]    = 1                  ; Set flag for looping over groups
d101 2
a102 2
; ----------------------  Extract PubSub group number (When given) ------------------
IF ([grp] .GT. 0) THEN            ; Group # sent on invokation line
d104 2
a105 2
   [sel_grp]                      ; Group selection file       (input)
   1,[grp]                        ; Search col. & value
d108 1
a108 1
   [sel_grp]                      ; Group selection file       (ends)
d110 1
a110 1
ELSEIF ([grp] .LT. 0) THEN        ; Negative group # sent on invokation line
d112 5
a116 1
ENDIF
d118 1
a118 5
VM
echo  ' 'Commencing back projection, please wait.     
VM
echo  ' '    
MY FL
d120 1
a120 1
DO   ; -----------------------------  Loop over all defocus groups -----------------------
d124 1
a124 1
      [sel_grp]                      ; Group selection file       (input)
d129 1
a129 1
   mkdir -p  df{***[grp]}          
d131 1
a131 1
   ; Create particle selection doc files for odd and even particle sets -----------------
d133 35
a167 9
   DE                             ; Delete any existing output file
   [seleven]
   DE                             ; Delete any existing output file
   [selodd]

   DOC SPLIT                      ; Alternately split doc. file into two 
   [sel_part]                     ; Particle selection file              (input)
   [selodd]                       ; particle selection file              (output)
   [seleven]                      ; Particle selection file              (output)
d171 14
a184 25
   ; Compute the odd sampled 3D reconstruction. -----------------------------------
   BP RP  [niter]                 ; Gets number of BP iterations
   [ali]                          ; Template for input image files       (input)
   [selodd]                       ; Selection doc file for odd images    (input)
   [rad]                          ; Radius of restored object
   [angles]                       ; Angles doc file                      (input)
   *                              ; Symmetries doc file (* = none)       (input)
   [grpvolodd]                    ; Reconstructed 3D volume              (output)
   [bplambda],[climit]            ; Correction lambda, limit
   [iters],0                      ; Iteration limit, mode
   (.5,.5)                        ; Minimum, maximum
   (.5)                           ; Smoothing constant

   ; Compute the even sampled 3D reconstruction. ----------------------------------
   BP RP  [niter]                 ; Gets number of BP iterations
   [ali]                          ; Template for input image files       (input)
   [seleven]                      ; Selection doc file for even images   (input)
   [rad]                          ; Radius of restored object
   [angles]                       ; Angles doc file                      (input)
   *                              ; Symmetries doc file (* = none)       (input)
   [grpvoleven]                   ; Reconstructed 3D volume              (output)
   [bplambda],[climit]            ; Correction lambda, limit
   ([iters],0)                    ; Iteration limit, mode
   (.5,.5)                        ; Minimum, maximum
   (.5)                           ; Smoothing constant
a185 1
   MY FL
d187 2
a188 28
   ; Compute  Fourier Shell Correlation for group reconstruction --------------------------------
   DE                             ; Remove any existing FSC file
   [grpfsc]                       ; Doc file

   RF 3 [unused],[spfq]           ; Fourier Shell Correlation
   [grpvolodd]                    ; First input volume              (input)
   [grpvoleven]                   ; Second input volume             (input)
   (0.5)                          ; Ring width
   (0.2,2)                        ; Lower, upper scale factors
   C                              ; C = missing cone
   (90)                           ; Max. tilt angle
   (3)                            ; Factor for noise comparison
   [grpfsc]                       ; FSC doc file                    (output)

   DOC REN                        ; Renumber doc keys (1...)
   [grpfsc]                       ; FSC doc file                    (input)
   [grpfsc]                       ; FSC doc file                    (output)

   ; Add footer in FSC doc file
   SD /     NORM-FREQ         DPH         FSC        FSCCRIT        VOXELS
   [grpfsc]                       ; FSC doc file                    (output)
   SD E
   [grpfsc]

   [resol] = 0.5/([sp_maxspfreq]*[spfq]) ; Resolution = pixel size / spatial freq.
   VM
   echo  ' 'Group: {****[grp]}'  'Defocus: {%f7.0%[def]}'  'Particles: {******[p]}'  'Resolution: {%f7.2%[resol]}    
   MY FL
d190 2
a191 3
   ; Save resolution in doc file ----------------------------------------------------
   SD [grp], [grp],[spfq],[resol]
   [res_file]
d193 41
a233 1
   MY FL
d235 2
a236 10
   TF C3                         ; Create CTF correction file ---------------------
   [ctf][grp]                    ; Output file                  (output)
   [sp_sph_abb]                  ; Cs
   [def],[sp_lambda]             ; Defocus, lambda
   [sp_winsiz]                   ; Dimension of volume
   [sp_maxspfreq]                ; Max. spatial freq.
   [sp_sourcesiz],[sp_def_spr]   ; Source size, defocus spread
   (0,0)                         ; Astigmatism, azimuth
   [sp_acr],[sp_geh]             ; Amplitude contrast ratio, Gaussian halfwidth
   (-1)                          ; Sign
a237 1
   IF ([grploop] .LE. 0) GOTO LB99 ; For handling single defocus group only
d239 2
a240 1
ENDDO                            ; End of defocus group loop ----------------------
d242 1
a242 2
UD NEXT END                      ; Finished  with group selection file
[sel_grp]                        ; Group selection file         
a243 2
VM                                      
echo ' '
d245 4
a248 76
LB98                             ; Entry point after skipping back projections
IF ([do_combine].LE.0) GOTO LB99  ; Can skip combination & resolution 

; Apply CTF correction to all defocus group volumes  ------------------------------
TF CTS                          ; CTF correction 
df***/[voleven]                 ; Template for image file       (input)
[sel_grp]                       ; Group selection file          (input)
[ctf]                           ; Template for ctf file         (input)
[snr]                           ; SNR
_1                              ; Temp. inline file             (output)

TF CTS
df***/[volodd]                  ; Template for image file       (input)
[sel_grp]                       ; Group selection file          (input)
[ctf]                           ; Template for ctf file         (input)
[snr]                           ; SNR
_2                              ; Temp. inline file             (output)

; Add  odd & even volumes to get combined volume   --------------------------------
AD                              ; Add  volumes
_1                              ; Volume                        (input)
_2                              ; Volume                        (input)
[vol]                           ; Volume                        (output) 
*                               ; Finished adding

DE                              ; Delete existing FSC file
[tmpfsc]

RF 3 [unused],[spfq]            ; Compute combined FSC resolution curve -----------
_1                              ; First sample volume           (input)
_2                              ; Second sample volume          (input)
0.5                             ; Ring width 
0.2, 2.0                        ; Lower, upper scale factors
C                               ; C = missing cone    
90.0                            ; Max. tilt angle
3                               ; Factor for noise comparison
[tmpfsc]                        ; FSC  doc file                (output)

; Compute reconstruction resolution -----------------------------------------------

[resol] = 0.5/([sp_maxspfreq]*[spfq])  ; Resolution = pixel size / spatial freq.

VM                                      
echo ' 'Reconstruction: [vol].$DATEXT'   'Resolution: {%f5.2%[resol]} Angstroms 
VM                                      
echo ' '

[key] = [grp] + 1
[grp] = 0
SD /    OVERALL,  NORM'D FREQ,  RESOLUTION (ANG.),  (CUTOFF=50%) 
[res_file]                      ; Resolution doc file        (output)

SD [key],[grp][spfq],[resol]    ; Put resolution in file
[res_file]                      ; Resolution doc file        (output)

; Reformat FSC curve file (removes first line?) -----------------------------------

DOC REN                         ; Renumber the doc file
[tmpfsc]                        ; Doc file                     (input)
[tmpfsc]                        ; Doc file                    (output)

UD N [nline]                    ; Number of lines in temp doc file
[tmpfsc]                        ; Doc file                    (input)

DE                              ; Delete existing FSC file 
[combfsc]                       ; Doc file                    (input)

SD /    NORM'D FREQ    DPR        FSC        FSCCRIT     VOXELS
[combfsc]                       ; Doc file                   (output)

DO  [line] = 1,[nline]
   UD [line], [v62],[v63],[v64],[v65],[v66]
   [tmpfsc]                     ; Doc file                   (input)
   SD [line], [v62],[v63],[v64],[v65],[v66]
   [combfsc]                    ; Doc file                   (output)
ENDDO
d250 1
a250 2
DE                              ; Delete temp. file
[tmpfsc]
a251 2
LB99
EN 
a252 1
; </body></pre></html>
@
