([rad])

; <html><head><title>Gaussian mask a volume</title></head><body><pre>
; 
; SOURCE: spider/docs/techs/recon/newprogs/softmask.pam 
;         G2 falloff                      ArDean Leith Jan 2012
;
; PURPOSE : Gaussian (soft) mask of 2 sampled volumes
;
; CALLED FROM:
;    endrefine.pam  <a href="./endrefine.pam">endrefine.pam</a>  
;
; -------------------------------------------------------

 FR 
   ? First half-set volume? [sub1_vol]      ; 1'st sample vol.  (input)

 FR 
   ? Second half-set volume? [sub2_vol]     ; 2'nd sample vol.  (input)

 FR 
   ? Soft mask? [gauss-mask]                ; Mask vol.         (output)
  
 FR 
   ? Masked First sample volume? [msk_sub1_vol]  ; 1'st sample  (output)

 FR 
   ? Masked Second sample volume? [msk_sub2_vol] ; 2'nd sample  (output)


 FI H [nx]            ; Get row length
   [sub1_vol]         ; Half-set volume       (input)
   NSAM               ; Row length

 ; Create a soft Gaussian volume mask
 [cen] = [nx]/2 + 1   ; Center of volume

 MO 3                 ; Create Gaussian model volume
   [gauss-mask]       ; Gaussian volume       (output)
   [nx],[nx],[nx]     ; Dimensions
   G2                 ; Super-Gaussian sphere 0...1
   [cen],[cen],[cen]  ; Volume center 
   [rad],[rad],[rad]  ; Gaussian radius

 ; Multiply sample volumes with mask volume
 MU                   ; Multiply by mask
   [sub1_vol]         ; Half-set volume        (input)
   [gauss-mask]       ; Mask volume            (input)
   [msk_sub1_vol]     ; Masked half-set volume (output)
   *

 MU                   ; Multiply by mask
   [sub2_vol]         ; Half-set volume        (input)
   [gauss-mask]       ; Mask volume            (input)
   [msk_sub2_vol]     ; Masked half-set volume (output)
   *

 RE  
 ; </body></pre></html>


