 ; <html><head><title>Calculate reconstructions</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/bp-inseries.spi   
 ;
 ; PURPOSE: Calculate two half-set reconstructions for each group
 ;          Merge half-set reconstructions into overall reconstruction
 ;          Create output volume by backprojection
 ;          Apply matched filter to output volume
 ;
 ; USAGE:       clean ; ./spider spi/dat @bp-inseries
 
; --------------------- END BATCH HEADER ---------------------

 ; Set parameter and file names
 @bp-settings([stk-opt],[r-fsc],[bp-type],[r-bpn],[cg-err],[cg-chi], [cg-iter],[cg-mode],[cg-lam],[rp-lam],[rp-cor],[rp-iter], [sp_pixsiz],[incore-yn])

 ; Temporary files

 IF ( [bp-type] == 1 ) THEN
   SYS
     echo " Backprojection using: 'BP CG'"
 ELSEIF ( [bp-type] == 2 ) THEN
   SYS
     echo " Backprojection using: 'BP 32F'"
 ELSEIF ( [bp-type] == 3 ) THEN
   SYS
     echo " Backprojection using: 'BP RP'"
 ELSEIF ( [bp-type] ==  4 ) THEN
   SYS
     echo " Backprojection using: 'BP 3N'"
 ENDIF

 ; Put label in new resolution doc file
 DE
   [resol_doc]                 ; Resolution doc file    (removed)
 SD / '      GROUP,    NORM-FREQ,    RESOL.(ANG.)  MASKED-RESOL.  (CUTOFF=50%)' 
   [resol_doc]                 ; Resolution doc file    (output)

 [num-grps] = 0
 
 DO                            ; ------  Loop over all groups ----------

   UD NEXT [key],[grp]         ; Get group from selection file
     [sel_group]               ; Group selection file       (input)
   IF ([key] <= 0) EXIT        ; End of groups in selection file

   [num-grps] = [num-grps] + 1 ; Increment

   @bp-main                    ; Call main procedure for this group
      [grp]                    ; Group number read by bp-main
    
 ENDDO                         ; End group-loop --------------------------
 

 ; If more than one group, sum and compute resolution
 IF ( [num-grps] > 1 ) THEN
   @bp-merge([num-grps])

   SYS
     echo -n " Back projections merged   "; date '+ TIME: %x  %X' ; echo
 ENDIF

 ; Create matched filter
 @make-matched-filt           ; Create filter doc file

 ; Apply matched filter to output volume
 @matched-filt                ; Filter to specified resolution

 SYS
   echo -n " Done          " ; date '+ TIME: %x  %X' ; echo

 EN
 ; </pre></body></html>

