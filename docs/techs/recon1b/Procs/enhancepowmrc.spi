; <html><head><title>Enhanced CTFFIND power spectra</title></head><b>
;
; SOURCE:   spider/docs/techs/recon?/Procs/enhancepowmrc.spi 
;
; PURPOSE : Cut and paste the Spider-generated power spectrum 
;           onto the model file generated by ctffind

; ----------- Input files ---------------

 [sel_mic]   = '../sel_micrograph'       ; Micrograph selection doc file 

 [input_dir] = 'power'                   ; Dir. for input files

 [mrc] = [input_dir]'/powchk{****[num]}' ; Power spectrum file from ctffind
            
 [spi] = [input_dir]'/pw_avg{****[num]}' ; Power spectrum file from Spider

; ----------- Output files --------------

 [output_dir] = 'power'                              ; Dir. for output files
 [out]        = [output_dir]'/powchk-enh{****[num]}' ; SPIDER PS file

; -------------------- END BATCH HEADER -----------------------

DO                        ; Loop over all files

   UD NEXT [key],[num]    ; Get micrograph  number from sel. file
     [sel_mic]            ; [num] is now the micrograph file number
   IF ([key] <= 0) EXIT   ; End of images in selection doc file

   FI H [nx],[ny]         ; Get SPIDER PS image size
     [spi]                ; Spider PS     (input)
     NX,NY

   [nxd2] = ([nx]/2)

   ; Estimate background and subtract it from 
   ; the power spectrum for better visualization

   FQ                     ; Quick filter
     [spi]                ; Spider PS     (input)
     _2   
     7                    ; Filter type (BW LP)
     0.002, 0.025         ; Passband, stopband

   SU                     ; Subtract
     [spi]                ; Spider PS      (input)
     _2                   ;                (input)
     _3                   ;                (output)
     *

   MA                     ; Mask
     _3                   ; Filtered file           (input)
     _4                   ; Filt file center masked (output)
     0.0, 35              ; Radius of central dot
     D                    ; Disk
     E                    ; External background
     0.0                  ; Center coord.
     [nxd2]+1, [nxd2]+1 

   ; Resize and recale the background -subtracted spider image

   FI H [nx],[ny]         ; Get image size
     [mrc]                ; MRC PS        (input)
     NX,NY                ; Header locs

   [xc] = ([nx]/2) + 1    ; Image center
   [yc] = ([ny]/2) + 1


   IP                     ; Rescale image
     _4                   ;                (input)
     _1                   ;                (output)
     [nx],[ny]            ; Scaling factor

   WI                     ; Extract left half of mrc image
     [mrc]                ; MRC PS         (input)
     _5
     [nx]/2, [ny]         ; Dimensions
     1,1                  ; Upper left coordinates

   FS [fmax],[fmin]       ; Get max & min values
     _5                   ;                (input)

   WI                     ; Extract right half of spider image
     _1                   ;                (input)
     _2                   ;                (output)
     [nx]/2, [ny]         ; Dimensions
     [xc],1               ; Upper left coordinates

   [v40]=[fmax]- [fmin]    ; Range of values
   [v41]=[fmax]+ ([v40]/2) ; Rescaled max
   [v42]=[fmin]- ([v40]/2) ; Rescaled min

   AR SCA                  ; Rescale the spider window
     _2                    ;                (input)
     _3                    ; Rescaled file  (output)
     [v42], [v41]

   CP                      ; Copy
     [mrc]                 ; MRC PS         (input)
     [out]                 ; Enhanced PS    (output)
   
   IN                      ; Insert image
     _3                    ; Rescaled file  (input)
     [out]                 ; Enhanced PS    (output)
     [xc], 1

   SYS                  
     echo ' '  ; echo " Created [out].$DATEXT"  
   
ENDDO

SYS                  
  echo ' '  

EN

