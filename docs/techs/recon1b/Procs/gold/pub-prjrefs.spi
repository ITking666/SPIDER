 ([iter],[num-grps],[prj-radius])

 ; <html><head><title>Creates reference projections on parallel cluster</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/gold/pub-prjrefs.spi
 ;         Reference projections parallelized   ArDean Leith  Jan 2016
 ;
 ; PURPOSE: Procedure for creating reference projections on parallel cluster.
 ;          Each data group uses one parallel process.
 ;
 ; I/O Registers & files are set in: <a href="./refine-settings.spi">refine-settings.spi</a>
 ;
 ; INPUT REGISTERS:
 ;   [iter]                Current iteration
 ;   [num-grps]            Number of parallel groups 
 ;   [prj-radius]          Projection radius 
 ;
 ; INPUT FILES: (##' denotes iteration,  '%' denotes subset, and '***' denotes group)
 ;   [iter_refangs]        work/ang_refs_##       Reference angles doc file        (one/iter)      
 ;   [current_vol]         final/vol_##_s%        Current subset reference volume  (two/iter) 
 ;
 ; OUTPUT FILES: 
 ;   [iter_refangs_grp]    work/ang_refs_##_***    Reference angles doc file    (one/group)      
 ;   [ref_projs_s]         work/refproj_##_s%      Reference projections stacks (two/iter)
 ;   [ref_projs_s_grp]     work/refproj_##_s%_***@ Temp ref projections stack   (two/iter/group) Deleted  
 ;
 ; PROCEDURES CALLED:
 ;    pub-submit             <a href="pub-submit.spi">         pub-submit.spi</a>
 ;      publish              <a href="publish.perl">           publish.perl</a>
 ;    ... pub-refine-start   <a href="pub-refine-start.spi">   pub-refine-start</a>          
 ;    ..... refine-settings  <a href="refine-settings.spi">    refine-settings.spi</a>            
 ;    ..... refine-prjloop   <a href="refine-prjloop.spi">     refine-prjloop.spi</a>            
 ;
 ; -------------------------------- END BATCH HEADER ----------------------------

 ; Create stacks holding projections from both current reference volumes in parallel.

 UD N [num-angs]                 ; Get number of reference images used
   [iter_refangs]                ; Reference images angles doc. file (input)

 SYS
   echo  "  Iteration: {%I0%[iter]}  Projecting: [vol]_s1 and [vol]_s2  Angles: {%I0%[num-angs]}" 

 IF ( [num-grps] <= 1) THEN
   ; No need for parallel jobs just use a single projection and return

   [s] = 1                       ; Subset
   PJ 3F                         ; Projection operation
     [vol_s]                     ; Current reference volume             (input) 
     [prj-radius]                ; Radius of object
     1-[num-angs]                ; Reference angles used    
     [iter_refangs]              ; Reference angles doc file            (input)
     [ref_projs_s]@******        ; Reference projection stack template  (output) 

   [s] = 2                       ; Subset
   PJ 3F                         ; Projection operation
     [vol_s]                     ; Current reference volume             (input) 
     [prj-radius]                ; Radius of object
     1-[num-angs]                ; Reference angles used    
     [iter_refangs]              ; Reference angles doc file            (input)
     [ref_projs_s]@******        ; Reference projection stack template  (output) 

   RE                            ; Return now
 ENDIF

 ; Project reference volume in parallel and wait for all subscriber jobs to finish
 [task]   = 2                      ; pub-refine-start will start: refine-prjloop 
 [script] = 'publish ./spider $PRJEXT/$DATEXT @pub-refine-start'

  @pub-submit([iter],[num-grps],[task])
    [script]                      ; Script that runs projection in parallel

 SYS
    echo "  Merging group reference projection stacks into single stack for each subvolume "

 DO [grp] = 1,[num-grps]         ; Loop over all projection groups ----------------

   MD                           
     RESULTS OFF                 ; Discard further results file info
   DO                            ; Loop over all projections in group 
     UD NEXT [in],[psi],[theta],[phi],[out],[igo],[iend]
       [iter_refangs_grp]        ; Reference projection angles doc file   (input)
     IF ( [in] == 0 ) EXIT

     [s] = 1
     CP
       [ref_projs_s_grp]{****[in]}; Template for ref. projections          (input) 
       [ref_projs_s]@*****[out]   ; Template for ref. projections          (output)
 
     [s] = 2
     CP
       [ref_projs_s_grp]{****[in]}; Template for ref. projections          (input) 
       [ref_projs_s]@*****[out]   ; Template for ref. projections          (output)
 
     IF ( [in] == 1 ) THEN
       [s] = 1
       SYS
         echo "  Copy: [ref_projs_s_grp]{****[in]} -->  [ref_projs_s]@{*****[out]}"
       [s] = 2
       SYS
         echo "  Copy: [ref_projs_s_grp]{****[in]} -->  [ref_projs_s]@{*****[out]}"
     ENDIF

   ENDDO                         ; End of:  DO   Loop over all projections 

   MD                           
     RESULTS ON                  ; Save further results file info

   UD NEXT END                   ; Close doc file
     [iter_refangs_grp]          ; Reference projection angles doc file   (input)

 ENDDO                           ; End of: DO [grp] = 1,[num-grps] ----------------

 RE
 ; </pre></body></html>




 
