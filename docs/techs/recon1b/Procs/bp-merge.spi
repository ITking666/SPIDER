([num-grps])
 ; <html><head><title> Calculate two half-set reconstructions and overall reconstruction</title></head><body><pre>

 ; SOURCE: spider/docs/techs/recon1/Procs/bp-merge.spi   
 ;
 ; PURPOSE: Calculate two half-set reconstructions and overall reconstruction 
 ;          Find FSC and resolution

 ; Set parameter and file names
 @bp-settings([stk-opt],[r-fsc],[bp-type],[r-bp],[cg-err],[cg-chi],[cg-iter],[cg-mode],[cg-lam],[rp-lam],[rp-cor],[rp-iter],[sp_pixsiz],[incore-yn])

 SYS
   echo ; echo " Summing reconstructed volumes"

 ; Average subset 1 volumes
 AS S                        ; Add volumes
   [group_vol_1]             ; Volume template (input)
   [sel_group]               ; Selection file
   A                         ; Find average
   [vol_1]                   ; Average volume  (output)

 ; Average subset 2 volumes 
 AS S                        ; Add volumes
   [group_vol_2]             ; Volume template (input)
   [sel_group]               ; Selection file
   A                         ; Find average
   [vol_2]                   ; Average volume  (output)

 ; Average combined volumes
 AS S                        ; Add volumes
   [group_vol]               ; Volume template (input)
   [sel_group]               ; Selection file
   A                         ; Find average
   [vol]                     ; Average volume  (output)

 ; Calculate unmasked and masked resolution curves

 ; Determine masked reconstruction resolution
 FSC [half],[spfr],[m-res]   ; Find FSC
   [vol_1]                   ; Volume                   (input)
   [vol_2]                   ; Volume                   (input)
   0.5, [r-fsc]              ; Shell width, mask radius
   [sp_pixsiz],0.5           ; Pixel size, 50% level
   [fsc]_m                   ; Masked FSC doc file      (output)
   *                         ; No Gnuplot file wanted

 ; Determine unmasked reconstruction resolution
 FSC [half],[spfr],[u-res]   ; Find FSC
   [vol_1]                   ; Volume                   (input)
   [vol_2]                   ; Volume                   (input)
   0.5, 0                    ; Shell width, mask radius
   [sp_pixsiz],0.5           ; Pixel size, 50% level
   [fsc]_u                   ; Unmasked FSC doc file    (output)
   *                         ; No Gnuplot file wanted

 SYS
   echo " Reconstruction, Masked resolution:'  '{%f6.2%[m-res]} UnMasked:' '{%f6.2%[u-res]} (Angstroms)"

 SD / '      NORM-FREQ,    RESOL.(ANG.)  MASKED-RESOL.  (CUTOFF=50%)' 
   [resol_doc]               ; Resolution doc file       (output)

 [key]   = 1
 [itert] = 0

 SD [key], [itert],[spfr],[u-res],[m-res]
   [resol_doc]
 SD E
   [resol_doc]

 RE
 ; </pre></body></html>
