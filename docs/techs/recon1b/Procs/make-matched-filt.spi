 ; <html><head><title>Generate matched filter</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/make-matched-filt.spi   
 ;
 ; PURPOSE:  Generate matched filter
 ;           FSC-masking adapted from JSL's makegauss.bat

 ; ------------------- Parameter -------------------

 [radius] = -1         ; Object radius (pixel) for masking (<0 gets radius using parameter doc file)

 ; -------------------- Inputs --------------------

 [parameter_doc]      = '../params'                      ; Parameter doc file

 [recon_dir]          = '.'                              ; Reconstruction directory

 [odd_half_volume]    = '[recon_dir]/vol_01_one'         ; First half-set volume

 [even_half_volume]   = '[recon_dir]/vol_01_two'         ; Second half-set volume

 ; -------------------- Output --------------------

 [matched_filt_doc] = '[recon_dir]/matched_filt_doc_01'  ; Matched-filter doc file

 ; ---------------- END BATCH HEADER ----------------

 SYS
   echo -n " Generating matched-filter doc file   " ; date '+ TIME: %x  %X' ; echo

 [fsc_doc]   = 'tmp_fscdoc'

 ; Get pixel size
 UD 5, [sp_pixsiz]
   [parameter_doc]               ; Doc file (input)

 ; Calculate radius, if necessary
 IF ( [radius] <= 0 ) THEN
    ; Get diameter from PARAMS
    UD 18, [sp_partsiz]
      [parameter_doc]            ; Doc file (input)

    [radius] = [sp_partsiz] / 2
 ENDIF
 UD E                            ; Doc file (closed)
 
 FSC [half],[sf],[res]           ; FSC calculation
   [odd_half_volume]             ; Volume     (input)
   [even_half_volume]            ; Volume     (input)
   1, [radius]                   ; Shell width (MUST BE 1 unless volume padded),mask radius
   [sp_pixsiz], 0.5              ; Factor for noise comparison
   [fsc_doc]                     ; FSC doc file    (output)

 SYS
   echo " Nominal resolution at 0.5 FSC: {%f6.2%[res]} pixels^-1" ; echo


 ; Get number of lines in FSC doc file
 UD N  [num-fsc-lines]
   [fsc_doc]                      ; FSC doc file   (input)

 DE                               ; Clean up pre-existing file
   [matched_filt_doc]             ; Filter doc file (removed)

 ; Header for filter doc file
 SD /     TRANSFER      R,PX^-1      R,A**-1
   [matched_filt_doc]             ; Filter doc file (input/output)

 DO [fsc-line]=3,[num-fsc-lines]  ; Loop through Radii ---------------------

    ; Get FSC (third column)
    UD IC [fsc-line], [spat-freq], [dummy], [fsc-value]
      [fsc_doc]                   ; FSC doc file    (input)
      
    ; Write FSC to filter doc file
    SD [fsc-line], [fsc-value], [spat-freq]
      [matched_filt_doc]          ; Filter doc file (output)
 ENDDO                            ; End Radius-loop ------------------------

 ; Clean up
 UD ICE
   [fsc_doc]                      ; FSC doc file    (closed)
 DE
   [fsc_doc]                      ; FSC doc file    (removed)
 SD E
   [matched_filt_doc]             ; Filter doc file (closed)

 RE                               ; Save results file to refer to nominal resolution

 ; Modified 2012-06-05
 ;    2013-10-10 (agl) -- Rewritten for use with masked FSC operation
 ;    2012-05-21 (trs) -- reports spatial frequency in MATCHED_FILTER_DOC in Angstroms
 ;    2012-03-22 (trs) -- reports nominal resolution in Angstroms
 ;    2008-01-15 (trs) -- now stand-alone batch file
 ; </pre></body></html>
