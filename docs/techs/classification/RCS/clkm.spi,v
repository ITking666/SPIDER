head	1.1;
access;
symbols;
locks; strict;
comment	@# @;


1.1
date	2013.02.07.13.30.22;	author leith;	state Exp;
branches;
next	;


desc
@@


1.1
log
@Initial revision
@
text
@;-----------------------------
; SOURCE:  clkm.spi
; PURPOSE: Invoke the 'CL KM' operation for K-means classification
;          Creates class membership doc file and class selection doc files.
;          Creates montage of class average images.
; ASSUMES: Equal weight to all factors, using continuous factors,
; ----------------------------

[f1]   = 1       ; Beginning factor
[f2]   = 3       ; Ending factor
[nc]   = 8       ; Number of classes REQUIRED
[seed] = 453     ; Random number seed

; ---------- INPUT files --------

 [dir]      = 'data'                   ; Directory
               
 [sufx]     = 'IMC'                    ; Data: suffix = either SEQ, IMC, or PIX

 [infile]   = '[dir]/cas_ca_[sufx]'    ; Data file from 'CA S' output

 [img_temp] = '[dir]/face_nois_stk@@*'  ; Template for images to be averaged 

 ; ----------- OUTPUT files ------

 [doc]      = '[dir]/clkm_[sufx]_clas_{***[seed]}'   ; Name for class membership doc file

 [template] = '[dir]/clkm_[sufx]_clas_{***[seed]}_'  ; Class selection doc file prefix

 [avg]      = '[dir]/clkm_aver_'                     ; Name for class average files      

 [var]      = '[dir]/clkm_varn_'                     ; Name for class variance files   

 [montage]  = '[dir]/clkm_clas_montage'              ; Name for class average montage file    

 ; ----------- END BATCH HEADER -----

 VM
   \rm -f [template]* [doc]*

 VM
   echo " Running 'CL KM' xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx"

 CL KM [B],[W],[C],[H],[DB]
   [infile]        ; CA/PCA file                     (input)
   [nc]            ; Number of classes
   [f1]-[f2]       ; Which factors
   0               ; Equal weight with factors
   [seed]          ; Random starting #
   [template]***   ; Template for doc series         (output)
   [doc]           ; Cluster membership doc file     (outpuy)

   ; Echo operation return values to results file
   [B]             ; Tr(B), trace of between-groups sum of squares matrix, 
   [W]             ; Tr(W), trace of within-groups  sum of squares matrix, 
   [C]             ; C = Tr(B)*Tr(W), Coleman criterion, 
   [H]             ; H = (Tr(B)/(k-1))/(Tr(W)/(nobj-k)), Harabasz criterion
   [DB]            ; DB, Davies-Bouldin criterion. 

 VM
   echo " Displaying class doc files xxxxxxxxxxxxxxxxxxxxxxxxxxxxx"
 VM
   xedit [template]*.$DATEXT &
 VM
   xedit [doc].$DATEXT &


 VM
   echo " Creating class average files  xxxxxxxxxxxxxxxxxxxxxxxxxx"

 [nclass]=8
 DO [i]=1,[nclass]
   AS R                  ; Averaging
     [img_temp]          ; Image file template          (input)
     [template]{***[i]}  ; Image selection file
     All                 ; All images
     [avg]{***[i]}       ; Average files               (output)
     [var]{***[i]}       ; Variance files              (output)
 ENDDO
 
 VM
   echo " Creating montage of class averages  xxxxxxxxxxxxxxxxxxxx"

 MN                      ; Montage the averages
   [avg]***              ; Average files               (input)
   1-[nclass]            ; File numbers
   [nclass],4            ; Images / row, margin
   0                     ; Margin value
   [montage]             ; Montage file                (output)


 VM
   echo " "
 EN
@
