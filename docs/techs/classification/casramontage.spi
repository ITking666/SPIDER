;-----
; SOURCE:  casramontage.spi 
; PURPOSE: Create a montage of one varying factor for X factors
;-----

 ;--- INPUTS ----

 [nf] = 3                   ; How many factors
 [nr] = 9                   ; How many/row

 ;--- INPUT FILES ----

 [dir]    = 'data'                 ; Directory
               
 [prefix] = '[dir]/cas_ca'         ; Prefix for SEQ, IMC, PIX, and EIG files from 'CA S'

 ;--- OUTPUT FILES -----

 [montage] = '[dir]/casra_27fade'  ; Name for final montage

 [jnkfile] = 'jnkqwerty'           ; Name for jnkfile files to be deleted

 ;----- END BATCH HEADER

 [n]=1                      ; Image counter, use this in jnkfile files

 DO  [i]=1,[nf]             ; Loop for each factor, also # of rows
  
  [vlow] = -0.2             ; Lowest value per row, repeats each row, coordinate
  
  DO  [j]=1,[nr]            ; Loop each coordinate/vector, # of columns
    
    CA SRA
      [prefix]              ; Input prefix
      [i]                   ; Which factor
      [vlow]                ; Factor coordinate
      [jnkfile]{**[n]}      ; File name           (output)
    
    [vlow] = [vlow] + 0.05  ; Increase coordinate by 0.25
    [n]    = [n]+1
   ENDDO
 ENDDO

 VM
  echo " Creating Montage xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx"
 
 [n] = [n]-1
 MN S                       ; Montage them all
   [jnkfile]**              ; Image file template (input)
   1-[n]                    ; Image numbers
   [nr],5                   ; Files per row, margin
   2                        ; Margin value
   [montage]                ; Montage file        (output)

 VM     ; Remove jnkfile
   rm -f [jnkfile]**.$DATEXT

 CP TO TIF                  ; Convert to TIFF
   [montage]
   [montage]
 
 VM                         ; Convert to JPEG
   convert [montage].tif [montage].jpg

 VM                         ; Display JPEG image
   display [montage].jpg
 
 EN
