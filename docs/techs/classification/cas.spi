;---------------------------------------------------------------------------
; cas.spi 
; PURPOSE: Automates running of "CA S" operation for CORAN (CA) with circular mask
;
; OUTPUT :
;   CA S : cor_EIG, cor_IMC, cor_PIX, cor_SEQ (where 'cor' is template)
;
; USAGE:   spider spi/dat @cas

;---------------------------------------------------------------------------

 [n1] = 1 	; First number of image/micrograph to analyze
 [n2] = 80      ; Final number
 [nf] = 8 	; Number of factors to search for 

 [x]  = 64      ; X size of mask
 [y]  = 64      ; Y size of mask
 [cx] = 33	; X center of circular mask
 [cy] = 33      ; Y center of circular mask
 [r]  = 30	; Radius of mask, take into account center and size of mask

 ; ----- INPUT FILES ------

 [dir]         = 'data'                   ; Directory
               
 [ima]         = '[dir]/face_nois_stk@*' ; Input image file template

 ; ----- OUTPUT FILES -----

 [mask]        = '[dir]/face_mask'       ; Mask file for images

 [prefix]      = '[dir]/cas_ca'          ; Prefix for SEQ, IMC, PIX, and EIG files from 'CA S'

 [doc_eigcoor] = '[dir]/cas_ca_cor_doc'  ; Lists coords. for each image, from _IMC

 ; ----- END BATCH HEADER ----------------------------------------

 VM
   echo " Creating mask file xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx"

 DE
   [mask]

 PT               ; Create mask
   [mask]         ; Mask file name      (output)
   [x],[y]        ; Size
   C              ; Circle pattern for mask
   [cx],[cy]      ; Circle center
   [r]            ; Circle radius
   N              ; No more input

 VM
   echo " Running correspondence analysis xxxxxxxxxxxxxxxxxxxxxx"

 CA S
   [ima]          ; Image template      (input)
   [n1]-[n2]      ; File numbers
   [mask]         ; Mask file           (input)
   [nf]           ; Number of factors
   C              ; Coran or PCA
   0.0            ; Additive constant
   [prefix]       ; Output FILE prefix  (output)

 DE
   [doc_eigcoor]
  
 SD C             ; Collect image coordinates for factors from _IMC
   [prefix]       ; Prefix for _IMC file
   1-2            ; Factor numbers
   [doc_eigcoor]  ; Doc file               (output)

 VM
   echo " Displaying factors: 1 & 2  doc. file xxxxxxxxxxxxxxxxx" ; echo " "
 VM
   xedit [doc_eigcoor].$DATEXT &

 EN
