;
; PURPOSE: Kmeans  based alignment 
; SOURCE:  spider/docs/techs/align2d/rotkm-ali.spi
;
; I/O PARAMETERS AND FILES ARE SET HERE:

[npart] = 15    ; Set number of particles

[ngrp] = 3      ; Set number of groups

;  ------------ Input files ---------------------------------------

[unaligned] = 'input/savface_stk_rot@' ; Unaligned image stack

; --------------- Output files  -----------------------------------

[aligned]   = 'output/rotkm_aligned'         ; Aligned images stack         

[apcasel]   = 'output/rotkm_grp_select_doc'  ; Group selection file

[apcadoc]   = 'output/rotkm_ali_doc'         ; Alignment doc file
 
[avgfile]   = 'output/rotkm_avg_bygrp'       ; Average file 

; -------------- END BATCH HEADER ---------------------------------

MD
  VB OFF

DE A
  [apcasel]              ; Group selection files      (removed)
DE
  [apcadoc]              ; Alignment filename         (removed)

AP C 
  [unaligned]****        ; Template for image files   (input)
  1-[npart]              ; File numbers 
  1,25                   ; First and last ring radius
  0                      ; Ring skip
  F                      ; Full or half ring data
  Y                      ; Normalize under the mask 
  [ngrp],100             ; # of groups, # of iterations
  1                      ; Check mirrored images
  [apcasel]***           ; Group select doc files     (output)
  [apcadoc]              ; Alignment doc file         (output)

DE
  [aligned]              ; Aligned image stack      (removed)


RT SFM                   ; Rotate and shift image stack
  [unaligned]****        ; Unaligned image stack     (input)
  1-[npart]              ; File numbers 
  2,0,0,0,0              ; Rot, scale, & shift registers
  [apcadoc]              ; Align. doc file          (input)
  [aligned]@****         ; Aligned image stack      (output)


; Calculate the average for each group (If > 2 images)

DO [grp]=1,[ngrp]

   ; Check whether the file exists
   IQ FI [exists]
     [apcasel]{***[grp]}   ; Selection file        (input)
   IF ([exists] .EQ. 0.0) CYCLE

   AS S
     [aligned]@****        ; Aligned file template (input)
     [apcasel]{***[grp]}   ; Group selection file  (input)
     A                     ; All images
     [avgfile]{***[grp]}   ; Average file          (output)
ENDDO

EN
