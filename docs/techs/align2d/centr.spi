([nimg])
; [nimg] = Number of images

; PURPOSE: Center an image series 
; SOURCE:  spider/docs/techs/align2d/centr.spi

FR
?Input file series?[input]

FR
?Output file series?[centered]

FR
?Document file to store the shifts?[shifts]

[zero] = 0.01           ; Almost zero

SD /      x-shift       y-shift       iterations
  [shifts]

DO [i]=1,[nimg]

  CP
    [input][i]          ; Unaligned     (input)
    _1                  ; Unaligned     (output)

  [xsh] = 0
  [ysh] = 0

  ; Iterative rotational alignment vs rotational avg.
  DO [iter]=1,100       ; Max is 100 iterations

    SH                  ; Shift
      _1                ; Unaligned     (input)
      _5                ; Shifted       (input)
      -[xsh],-[ysh]     ; Shifts

    RO I                ; Rotational average
      _5                ; Shifted        (input)
      _2                ; Rotational avg (output)

    CC N                ; Cross correlation
      _5                ; Shifted        (input)
      _2                ; Rotational avg (input)
      _3                ; CC             (output)

    PK M [a],[b],[xpk],[ypk] ; Peak search
      _3                ; CC             (input)
                        ; Default center

    [xab] = ABS([xpk]) 
    [yab] = ABS([ypk]) 
    IF ([xab] .LE. [zero]) THEN
       IF ([yab] .LE. [zero]) EXIT
    ENDIF

    [xsh] = [xsh] + [xpk]
    [ysh] = [ysh] + [ypk]
  ENDDO

  ; Store shifts x-y and # of iterations
  SD [i],[xsh],[ysh],[iter]
    [shifts]

  CP
    _5
    [centered][i]
ENDDO

SD E
  [shifts]
DE
  _1
DE
  _2
DE
  _3
DE
  _5

RE

