;
; PURPOSE: Reference free alignment 
; SOURCE:  spider/docs/techs/align2d/ref-free-apsr-ali.spi
; USAGE:   clean ; spider spi/dat @ref-free-apsr-ali

; I/O PARAMETERS AND FILES ARE SET HERE:

[npart] = 40       ; Set number of particles

;  ------------ Input files ---------------------------------------

[unaligned] = 'input/savface_stk_rot4' ; Unaligned image stack

; --------------- Output files  -----------------------------------

[aligned]  = 'output/apsr_aligned_stk' ; Aligned images stack         

[alignavg] = 'output/apsr_avg_stk'     ; Averaged images stack         
[apsrdoc]  = 'output/apsr_align_doc_'  ; Alignment doc file

[casout]   = 'output/apsr_caspca'      ; PCA classification file stem  
       
[dend]     = 'output/apsr_dend_doc'    ; Dendrogram doc file
[classel]  = 'output/apsr_classel_doc' ; Group selection file stem
[grpavg]   = 'output/apsr_avg'         ; Group averages stack
[groups]   = 'output/apsr_group_doc'   ; Group summary doc file
[dres]     = 'output/apsr_dres_doc_'   ; FRC doc files

; -------------- END BATCH HEADER ---------------------------------

MD
  VB OFF

FI H [nx],[ny]           ; Get image dimensions
  [unaligned]@1
  NX,NY

[rad] = [nx]/2-1         ; Set radius to NX/2-1
[rad]
[rad2] = [rad] - 2 

AP SR [niter]
  [unaligned]@****       ; Template for image files  (input)
  1-[npart]              ; File numbers 
  [rad]                  ; Object radius
  3,[rad2]               ; First and last ring radii
  *                      ; No image for centering average
  [alignavg]@***         ; Aligned images stack         
  [apsrdoc]***           ; Alignment doc file        (output)

[niter]

; Rotate & shift  images according doc file angles
RT SF                    ; Rotate and shift image stack
  [unaligned]@****       ; Unaligned image stack    (input)
  1-[npart]              ; File numbers 
  1,0,2,3                ; Rot, scale, & shift reg
  [apsrdoc]{***[niter]}  ; Align. doc file         (input)
  [aligned]@****         ; Aligned image stack     (output)


; Create mask file
MO                       ; Create mask file
  _1                     ; Mask file     (output)
  [nx],[ny]              ; Size
  C                      ; Circle
  [rad]                  ; Radius

; Perform PCA with 20 factors
CA S
  [aligned]@****         ; Aligned image stack     (output)
  1-[npart]              ; File numbers 
  _1                     ; Mask file              (input)
  20                     ; Number of factors
  P                      ; Use PCA
  [casout]               ; File prefix            (output)


; Hierarchical Clustering
CL HC
  [casout]_IMC           ; File prefix            (input)
  1-15                   ; Number of factors
  0                      ; Factor weight
  2                      ; Clustering criterion
  *                      ; No dendrogram postscript file
  [dend]                 ; Dendrogram doc. file  (output)
 

; Calculate number of classes for a given threshold
CL HD
  70                     ; Cutoff threshold % (0-100)
  [dend]                 ; Dendrogram doc. file   (input)
  [groups]               ; Document file

; Create selection docfiles for given threshold
CL HE
  70
  [dend]                 ; Dendrogram doc. file   (input)
  [classel]***           ; Class selection doc files (output)

; Find number of groups
UD N [ngrp]
  [groups]

[ngrp]


DO  [grp]=1,[ngrp]
  ; Calculate group averages
  AS S
    [aligned]@****       ; Aligned images        (input)
    [classel]{***[grp]}  ; Group select file     (input)
    AS                   ; Average and subsets
    [grpavg]@{***[grp]}  ; Group average         (output)
    _1                   ; Sub average 1         (output)
    _2                   ; Sub average 1         (output)

  ; Calculate relative resolution for each group
  FRC
    _1                   ; Sub average 1         (input)
    _2                   ; Sub average 2         (input)
    0.5                  ; Ring width
    1                    ; Pixel size (arbitrary)
    [dres]{***[grp]}     ; FRC doc file        (output)
    *                    ; No gnuplot file     (output)
ENDDO

DE
  _1
DE
  _2                   

EN
