 ; <html><head><title>CTF corrects selected images using CTF correction doc file</title></head><body><pre>
 ;
 ; SOURCE:  spider/docs/techs/recon1/Procs/filtctf.spi
 ;
 ; PURPOSE: CTF corrects selected images using CTF correction doc file
 ;          Not necessary if using:  restack-n-ctf
 ;
 ; USAGE:   clean ; spider spi/dat @filtctf

 ; ------------------ Parameters ------------------
 
 [progress-n]  = 1           ; Print progress message every Nth micrograph

 ; -------------------- Inputs --------------------

 [params]      = '../params'                                   ; Parameter doc file        (one)

 [sel_mic]     = '../sel_micrograph'                           ; Micrograph selection file (one)

![sel_part]    = 'good/ngood****'                              ; Particle selection file   (one / micrograph)
 [sel_part]    = 'good/sel_part_****'                          ; Particle selection file   (one / micrograph)

![unfilt_imgs] = 'win/winser_{****[mic]}@******'               ; Windowed image stacks     (one / micrograph)
 [unfilt_imgs] = 'win/data_bymic_{****[mic]}@******'           ; Windowed image stacks     (one / micrograph)

 [ctf_cor]     = '../Power_Spectra/power/flipctf_{****[mic]}'  ; CTF correction files      (one / micrograph)

 ; -------------------- Outputs --------------------

 [ctf_dir]     = 'win'                                         ; Output directory

![ctfd_imgs]   = '[ctf_dir]/winctf_{****[mic]}@*****'          ; CTF-corrected image stacks (one / micrograph)
 [ctfd_imgs]   = '[ctf_dir]/data_ctfd_bymic_{****[mic]}@*****' ; CTF-corrected image stacks (one / micrograph

 ; --------------- END BATCH HEADER ---------------

 MD
   SET MP
   0
 MD
   VB OFF

 SYS
   mkdir -p [ctf_dir]

 ; Get number of micrographs
 UD N [num-mics]
   [sel_mic]

 ; Loop through micrographs ----------------------------------------------------
 DO [mic-key] =1,[num-mics]

    UD IC [mic-key],[mic]
      [sel_mic]                        ; Micrograph selection  file (input)

    IF (int([mic-key]/[progress-n]) == [mic-key]/[progress-n]) THEN
      SYS
        echo " Micrograph: {%I4%[mic]}, {%I4%[mic-key]} out of: {%I4%[num-mics]}"
    ENDIF

    ; Check if particle selection file exists
    IQ FI [exists-yn]
      [sel_part][mic]                   ; Micrograph selection file (input)
    IF ([exists-yn] == 0) CYCLE         ; Skip empty micrographs

    ; Loop through selected particles -----------------------------------------
    DO                                  ; Loop through selected particles 
      UD NEXT [key],[num]               ; Get particle number
        [sel_part][mic]                 ; Particle selection file     (input)
      IF ([key] <= 0) EXIT              ; End of particles in this micrograph 
   
      ; Filter particle using CTF correction file
      TF COR
        [unfilt_imgs][num]              ; Existing particle in stack  (input)
        [ctf_cor][mic]                  ; CTF correctionfile          (input)
        [ctfd_imgs]                     ; New particle in stack       (output)

    ENDDO                               ; End particle loop   -----------------

    ; Clean up
    UD ICE
      [sel_part][mic]
 ENDDO                                 ; End micrograph loop  -----------------

 SYS
   echo ; echo -n " Done    "; date

 EN

; Modified 2015-11-16
;    2015-11-16 (agl) -- use 'TF COR' not 'FD'!
;    2013-10-16 (agl) -- modernized
;    2013-10-11 (agl) -- Filenames & cosmetic
;    2011-08-19 (trs) -- operating on (micrograph) stacks
;    2009-09-28 (trs) -- skips empty micrographs
 ; </body></pre></html>
