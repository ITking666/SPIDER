 ; <html><head><title>Convert MRC file to SPIDER</title></head><body><pre>
 ;    
 ; SOURCE: spider/docs/techs/recon1/Procs/cptomrc.spi 
 ; 
 ; PURPOSE: Convert SPIDER format files to MRC format.
 ;          MRC file has same name with .mrc or .mrcs extension 
 ;          Takes list of filenames
 ;
 ; AUTHOR:  ArDean Leith                                                  
 ; 
 ; FEED WITH: ls -1 files* > spilist.dat ,  Edit out: file extensions
 ;
 ; USAGE:   spider spi/dat @cptomrc 
 ; 
 ;  ----------------- Input files -----------------------------------------
 
 [filelist] = 'spilist'          ; MRC file list text file            

 [params]   = '../data/params'   ; Parameter doc file
 
 ; ----------------- Output files ----------------------------------------    

 ; Filelist files converted to MRC format with .mrc extension

 ;----------------- END BATCH HEADER ------------------------------------
 
 UD 5, [pxsiz]                 ; Get pixel size (A)
   [params]                    ; Parameter doc. file

 DO 
   FR N [runoff]               ; Get next line, check input over-run
     [filelist]                ; File name list text file  (input)           
     [file]                    ; Variable name                      

   IF ([runoff] .NE. 0 ) EXIT ; End of list

   FI H [nz],[maxim]           ; Get file header info
     [file]                    ; File name  (input)  
     NZ,MAXIM                                    

   IF ( [maxim] > 1) THEN
     SYS                 
       echo "  Convert SPIDER stack: [file] with {%I0%[maxim]} images to MRC stack: [file].mrc "
                                                                                        
     CP TO MRC                 ; Make MRC stack file
       [file]@                 ; SPIDER file name     (input)
       [file].mrcs             ; New MRC stack file   (output)
       0 0                     ; MRC file version,  Do not flip origin
       [pxsiz],[pxsiz],[pxsiz] ; Angstroms / pixel

   ELSE

     SYS
       echo "  Convert SPIDER image: [file] to MRC image: [file].mrc "

     CP TO MRC                 ; Make MRC stack file
       [file]                  ; SPIDER file name    (input)
       [file].mrc              ; New MRC  file       (output)
       32                      ; MRC data length
       *                       ; Center offset
       [pxsiz],[pxsiz],[pxsiz] ; Angstroms/pixel

   ENDIF
 ENDDO

 EN
; </pre></body></html>

