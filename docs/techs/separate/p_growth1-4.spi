[X33,X50,X92]
;P_GROWTH1-4.SPI

FR
?SEED PROTEIN? <1>
FR
?SEED PROTEIN GROWN?  <2>
FR
?SEED RNA?  <3>
FR
?SEED RNA GROWN?  <4>
FR
?ORIGINAL MAP?  <5>
FR
?HISTOPROTEIN-PERCENT?  <6>
FR
?HISTORNA-PERCENT?   <7>
FR
?SPHERICAL MASK FOR COMPARISON?  <8>


X75=0
X99=0
X80=X92-X50+3
X70=X80
X69=X80
UD IC,X80,X81,X82,X83,X84
<6>           ; histoprotein-percent

; junk5-RNA AND junk5-Protein BOTH SEEMS TO THE SAME MASK !

; LIMITATION MASK FOR GROWTH
TH M
<5>
output/junk5-RNA
B
X81

TH M
<5>
output/junk5-Protein
B
X81


UD IC,X80,X81,X82,X83,X85
<7>            ; historna-percent


CP
<1>
_1

CP
<3>
_6

DO LB1 X60=1,99

   ; DILATE PROTEIN SEED
   DI
   _1
   _2
   B
   (3)
   (2)

   ; DILATE RNA SEED
   DI
   _6
   _7
   B
   (3)
   (2)

   ;----------------------- PROTEIN STUFF START -------------------------------
   ; INVERT DILATED RNA SEED MASK
   AR
   _7
   _9
   -1*(P1-1)
  
   ; ADD INVERTED RNA DILATED SEED MASK WITH PROTEIN SEED MASK (UNDILATED) 
   AD
   _9
   _1
   _3
   *

   ; MAKE BINARY MASK
   TH M
   _3
   _4 
   B
   (0.5)

   ; FIND INTERSECTION OF ALL THESE MASKS
   MU
   _2
   output/junk5-Protein
   _3
   _4 
   *
   
   ; ADD INTERSECTION OF ALL THE MASKS (SEE ABOVE STEP) AND PROTEIN SEED
   ; MASK (BEFORE DILATION)
   AD
   _3
   _1
   _4
   *

   ; MAKE A BINARY MASK
   TH M
   _4
   _3
   B
   (0.5)
   ;----------------------- PROTEIN STUFF END -------------------------------


   ;----------------------- RNA STUFF START   -------------------------------
   
   ; INVERT DILATED PROTEIN SEED MASK
   AR
   _2
   _9
   -1*(P1-1)

   ; ADD INVERTED PROTEIN DILATED SEED MASK WITH RNA SEED MASK (UNDILATED) 

   AD
   _9
   _6
   _8
   *
   
   ; MAKE BINARY MASK
   TH M
   _8
   _4
   B
   (0.5)

   ; FIND INTERSECTION OF ALL THESE MASKS
   MU
   _7
   output/junk5-RNA
   _8
   _4
   *
   
   ; ADD INTERSECTION OF ALL THE MASKS (SEE ABOVE STEP) AND RNA SEED
   ; MASK (BEFORE DILATION)
   AD
   _8
   _6
   _4
   *
   
   ; MAKE A BINARY MASK
   TH M
   _4
   _8 
   B
   (0.5)
   
   ;----------------------- RNA STUFF END----------------------------------
   
   AD
   _1    ; protein seed
   _6    ; rna seed
   _2
   *

   ; X23 = AVG. OF PIXEL VALUES
   ; X33 = NO.OF PIXELS
   X99=0
   FS X21,X22,X23,X24
   _3
   X24=X23*X33
   
   IF(X24.GT.X84) THEN
      CP
      _1
      _3

      ; X70 IS INSIDE THE DO LOOP, SO ITS VALUE INCREASES IN EVERY LOOP
      X70=X70+2
      X68=X70-X80
      
      IF(X68.LE.8)THEN
         CP
         _6
         _8
         X99=X99+1
      ENDIF

      UD IC,X70,X71,X72,X73,X74
      <6>

      TH M
      <5>
      output/junk5-Protein
      B
      X71

   ENDIF

   FS X21,X22,X23,X24
   _8
   X24=X23*X33
   
   IF(X24.GT.X85) THEN
      CP
      _6
      _8


      X69=X69+2
      X68=X69-X80

      IF(X68.LE.8)THEN
         CP
         _1
         _3
         X99=X99+1
      ENDIF

      UD IC,X69,X71,X72,X73,X74
      <6>

      TH M
      <5>
      output/junk5-RNA
      B
      X71
   ENDIF

   X99

   IF(X99.EQ.1)GOTO LB1     ; skip the following and stay inside the do loop
   IF(X99.EQ.2)GOTO LB50    ; get out of the loop
   AD
   _3
   _8
   _7
   *

   CC C,X10
   _2
   _7
   <8>

   X10

   IF(X10.EQ.X75)GOTO LB50   ; no change in ccc, get out of the loop
   IF(X10.EQ.1)GOTO LB50     ; perfect match, get out of the loop
   X75=X10

   CP
   _3
   _1

   CP
   _8
   _6

LB1
LB50

X60

CP
_3
<2>    ; output grown protein

CP
_8
<4>    ; output grown rna


RE
