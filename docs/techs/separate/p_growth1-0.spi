[X31,X32]
; P_GROWTH1_0.SPI
; GROWTH LIMITED TO 20 % OF PROTEIN AND 20 % OF RNA VOLUME

FR
?SEED PROTEIN?  <1>
FR
?SEED PROTEIN GROWN?  <2>
FR
?SEED RNA?  <3>
FR
?SEED RNA GROWN? <4>
FR
?ORIGINAL MAP? <5>
FR
?SPHERICAL MASK FOR COMPARISON?  <6>
FR
?INITIAL MASK PROTEIN?  <7>
FR
?INITIAL MASK RNA? <8>

X75=0
X81=X31


; LIMITATION MASK FOR GROWTH
TH M
<5>
output/junk5-Protein
B
X81


X81=X32
TH M
<5>
output/junk5-RNA
B
X81

; COPY PROTEIN SEED TO MEMORY
CP
<1>
_1

; COPY RNA SEED TO MEMORY
CP
<3>
_6

DO LB1 X60=1,99
 
   ; DILATE PROTEIN SEED
   DI
   _1
   _2
   B
   (3)
   (2)

   ; DILATE RNA SEED
   DI
   _6
   _7 
   B
   (3)
   (2)


;------------------------ PROEIN STUFF BEGIN ----------------------
   ; INVERT RNA MASK
   AR
   _7
   _9
   -1*(P1-1)

   ; ADD INVERTED RNA MASK TO PROTEIN SEED MASK (UNDILATED 
   ; BEFORE EACH LOOP EXECUTION) 
   AD
   _9
   _1
   _3
   *

   ; MAKE A BINARY MASK AFTER ADDITION IN PREVIOUS STEP
   TH M
   _3
   _4
   B
   (0.5)

   ; FIND THE INTERSECTION OF THE MASKS
   MU
   _2                    ; protein mask after dilation
   output/junk5-Protein  ; mask for 20% (or 30 % !!)protein volume
   _3                    ; 
   <7>                   ; protein mask after initial separation
   _4                    ; 
   *

   ; ADD THE INTERSECTION OF ALL THE MASK (SEE ABOVE) 
   ; WITH PROTEIN SEED ( BEFORE DILATION IN EACH LOOP)
   AD
   _3
   _1
   _4
   *

   ; MAKE A BINARY MASK
   TH M
   _4
   _3
   B
   (0.5)
;------------------------ PROEIN STUFF END ----------------------

;------------------------ RNA STUFF BEGIN  ----------------------

   ; INVERT DILATED PROTEIN MASK
   AR
   _2
   _9
   -1*(P1-1)

   AD
   _9
   _6
   _8
   *

   TH M
   _8
   _4 
   B
   (0.5)


   MU
   _7
   output/junk5-RNA
   _8
   <8>
   _4
   *

   AD
   _8
   _6
   _4 
   *

   TH M
   _4
   _8 
   B
   (0.5)
;------------------------ RNA STUFF END----------------------

   AD
   _1     ; protein seed before dilation
   _6     ; rna seed before dilation
   _2
   *


   AD
   _3
   _8
   _7
   *

   CC C,X10
   _2
   _7
   <6>

   X10
   
   ; IF NO CHANGE IN CCC VALUE OR CCC =1 EXIT LOOP
   IF(X10.EQ.X75)GOTO LB50
   IF(X10.EQ.1)GOTO LB50

   X75=X10

   ; CHANGE PROTEIN SEED
   CP
   _3
   _1

   ; CHANGE RNA SEED
   CP
   _8
   _6

LB1
LB50

X60

CP
_3
<2>
CP
_8
<4>


RE
