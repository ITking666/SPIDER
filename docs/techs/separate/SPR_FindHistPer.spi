; --- SPR_FindHistPer ---- FIND HISTOGRAM PERCENTAGE 

;---------------------------------------------------------------
; BEGINING OF INPUT PARAMETERS

FR 
?INPUT VOLUME NAME ?<1>
;output/vol_fq

FR 
?OUTPUT FUNNY VOLUME NAME ?<2>
;output/funny_volume

FR 
?INPUT KNOWN RNA HISTOGRAM DOCUMENT FILE NAME ?<3>
;../histo/output/hist_rna

FR 
?INPUT KNOWN PROTEIN HISTOGRAM DOCUMENT FILE NAME ?<4>
;../histo/output/hist_pro

FR 
?OUTPUT DOCUMENT FILE NAME TO STORE RNA HISTOGRAM PERCENTAGE?<5>
;output/histoRNA-percent

FR 
?OUTPUT DOCUMENT FILE NAME TO STORE PROTEIN HISTOGRAM PERCENTAGE?<6>
;output/histoPROTEIN-percent


RR X91   
? RNA FRACTION FOR 40S ?

RR X92   
? PROTEIN FRACTION FOR 40S ?


; END OF INPUT PARAMETERS
;----------------------------------------------------------------


MD
SET MP
0


; CREATES DOCUMENT FILE FOR FD COMMAND
; FILTRATION WITH A STRAIGHT LINE IN FOURIER SPACE
; CORRESPONDS TO DIFFERENTATION

FI [NSAM],[NROW],[NSLICE]
<1>
12,2,1

[NSAM]   = INT([NSAM])
[NROW]   = INT([NROW])
[NSLICE] = INT([NSLICE])


IF ([NSAM] .NE. [NROW])  THEN
   VM
   echo '\n ******* ERROR: NSAM, NROW AND NSLICE ARE NOT EQUAL ******* \n'
   EN
ENDIF   
IF ([NSAM] .NE. [NSLICE]) THEN
   VM
    echo '\n ******* ERROR: NSAM, NROW AND NSLICE ARE NOT EQUAL ******* \n'
   EN
ENDIF


; DELETE TEMPORARY FILE 'TEMP_FILE_001'
DE A
TEMP_FILE_001

[WI_MID]=([NSAM] - 1)/2

DO LB1 X15=1,[NSAM]
   X16=(X15-1)/[WI_MID]
   SD X15,X16
   TEMP_FILE_001
LB1


; DIFFERENTIATION AND PEAK SEARCH OF ORIGINAL VOLUME
; THE RESULTING "funny_volume = <2>" WILL CONTAIN MANNY FRAGMENTED DENSITY
; CLUSTER THAT WILL SERVE AS SEEDS FOR THE DILATION IN THE REFINEMENT
; PHASE

; CORRESPONDS TO DIFFERENTATION
FD
<1>
_1
TEMP_FILE_001

FS X17,X18
_1

TH
_1
_2
B
0.0

AR
_2
_3
P1/X17+1

FS
_3

AR
_3
_4
P1**10

; PEAK SEARCH
FS
_4

MU
_4
<1>
<2>
*

; DELETE TEMPORARY FILE 'TEMP_FILE_001'
DE A
TEMP_FILE_001



; CALCULATES HISTORNA-PERCENT AND HISTOPROTEIN-PERCENT FROM INPUT HISTOGRAMS
; THIS FILES CONTAIN THE FRACTION OF TOTAL MASS THAT IS OCCUPIED ABOVE
; A CERTAIN THRESHOLD HISTO-RNA IS THE HISTOGRAM DOCUMENT FILE FOR THE KNOWN
; RNA PART OF THE VOLUME. HISTO-PROTEIN IS THE HISTOGRAM DOCUMENT FILE FOR THE KNOWN
; PROTEIN PART OF THE VOLUME.


X75= [NSAM] * [NSAM] *  [NSAM]   ;number of voxel, window size**3


X21=0
X22=0


CP
<1>
_5

DO LB2 X50=1,128

   UD IC,X50,X51,X52
   <3>               ;HISTO-RNA - INPUT FILE, CAN BE CREATED BY USING A MASK AND 'HI D'
   X21=X21+X52

   UD IC,X50,X51,X52 
   <4>               ;HISTO-PROTEIN - INPUT FILE, CAN BE CREATED BY USING A MASK AND 'HI D'
   X22=X22+X52
   
LB2

X31=0
X32=0

; # OF HISTOGRAM BINS = 128
DO LB3 X50=1,128
   UD IC,X50,X51,X52
   <3>                 ;histo-rna
   X53=(X21-X31)/X21
   X31=X31+X52

   UD IC,X50,X61,X62
   <4>                 ;histo-protein
   X63=(X22-X32)/X22
   X32=X32+X62

   TH M
   _5
   _6 
   B
   X61

   FS X71,X72,X73,X74
   _6
   X73=X73*X75

   X93=X53*X91+X63*X92

   IF(X93.GT.0)THEN
      X54=X73*X53*X91/X93
      X64=X73*X63*X92/X93
   ELSE
      X54=0
      X64=0
   ENDIF

   SD X50,X51,X52,X53,X54
   <5>

   SD X50,X61,X62,X63,X64
   <6>

LB3

RE
