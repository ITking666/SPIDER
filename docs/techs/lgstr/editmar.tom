; SOURCE: b60.dat
;
; PURPOSE: Eliminates an unwanted marker from a marker doc file series

x20 = 122    ; Number of marker doc files in series

FR G
[unwanted]1,2    ; ; Comma seperated list of reject marker numbers

; ----------------------- Input files ---------------------------------

FR G                                     
[oldmarker]rotated/d_rot***    ; Template for existing marker doc files

; ---------------------- Output files --------------------------------

FR G                                     
[newmarker]rotated/nd_rot***   ; Template for corrected marker doc files

; ----------------- END BATCH HEADER ------------------------------ </b>

; Make doc file for any unwanted markers
DE                             ; Remove existing reject marker file
badnums                        ; Existing reject marker file

DOC CREATE                     ; Create reject marker doc file
badnums                        ; Reject marker doc file   (output)
1                              ; Register
[unwanted]                     ; List of reject markers 

UD N, x60                      ; Number of reject markers  
badnums

DO LB1 x22=1,x20               ; Loop over all marker doc files

   UD N, x24                   ; Find number of markers in marker doc file
   [oldmarker]x22              ; Marker doc files           (input)

   ; Copy the summary data from old marker doc file to new
   UD -1,x51,x52,x53
   [oldmarker]x22

   SD -1,x51,x52,x53
   [newmarker]x22
   
   DO LB2  x11=1,x24	       ; Loop over all markers
   

      UD IC x11,x20,x30    ; Find marker number
      [oldmarker]x22           ; Marker doc file            (input)

      x62 = 0                  ; Initalize reject flag
      DO LB3  x61=1,x60	       ; Loop over all rejected markers

         UD IC x61,x55             ; Find reject marker number
         badnums            ; Reject marker doc file     (input)

         IF (x11 .EQ. x55) x62=1 ; Reject x11 marker
      LB3
      IF (x62 .EQ. 0) THEN
         SD x11,x20,x30    ; Save marker with this key number
         [newmarker]x22           ; Marker doc files          (output)
      ENDIF
      
      UD ICE                      ; Finished with this doc file
     [oldmarker]x22                 ; Template for marker doc files      
   LB2

LB1                            ; End loop over all marker doc files

DE
badnums                     ; Reject marker doc file     (input)

EN


