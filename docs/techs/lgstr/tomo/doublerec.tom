; SOURCE: doublerec.tom
;
; PURPOSE: Double-tilt reconstruction
;
; NOTES:  1. Makes reconstruction slicewise along X axis.
;         2. Can reconstruct a window from center of full volume.

;<b>------------------- Edit next 5  Input parameters ----------------- 

[ht]   = 400   ; Z size in pixels of reconstruction 
[sgo]  = 1     ; Beginning slice to be reconstructed
[send] = 0     ; Ending slice to be reconstructed (0 if all)

; ----------------------- Input files ---------------------------------

FR G
[aligned]output/rdb_***          ; Aligned image file template   

FR G
[select_both]output/sel003       ; Combined selection  doc file 

FR G
[angles_both]output/angles_both  ; Combined series angle  doc file 

; ---------------------- Output files ---------------------------------

FR G
[vol2]vol001                    ; Reconstructed double-tilt output volume 
           
; ----------------- END BATCH HEADER ---------------------------------- </b>

MD
SET MP
0                   ; Use all processors

UD 1, [n1]          ; Get first image number
[select_both]       ; Selection file                        (input)

FI [size]           ; Query image size (must be square)
[aligned][n1]       ; Aligned image                         (input)
12                  ; NSAM 

IF ([send] .LE. 0) [send] = [ht]   ; Slice range for reconstruction

BP 3D               ; 3D back projection
[aligned]           ; File prefix of aligned images         (input)
[select_both]       ; Selection file                        (input)
[angles_both]       ; Angles file                           (input)
[size],[size],[ht]  ; Output dimensions X, Y, & Z
[sgo],[send]        ; First & last Z-slice reconstructed
1.0E+4              ; SNR weighting factor
[vol2]              ; Reconstructed volume                  (output)

FS [max],[min]      ; Get volume statistics
[vol2]              ; Filename                              (input)

VM                  ; Echo volume statistics
echo ' BP 3D    Range: {%g13.5%[min]}...{%g13.5%[max]}'
;;VM
;;echo ' Correct  Range:  ... '
VM            
echo '  '

EN
