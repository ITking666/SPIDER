;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Bead tracker for SPIDER  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

rr x97,x90,x91
?First, Zero tilt, and Last File numbers?
rr x89,x80,x88
?Number of Markers, Marker diameter (pixels), Density (1 light, 0 Dark)?
fr
?File Series basename (ie img)?<in>
fr 
?Name of Marker files (ie mrk)?<mrk>
fr
?angular doc file?<ang>
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
x81=x80/2
x82=1/x80
x24=0

fi x10,x11
<in>{***x90}
(12,2)

;;;;;BEGIN THE MARKER SELECTION
Do lb12 j=1,2                   ;Divides into plus and minus branches
x21=2*x0-3                      ;Toggles between -1 and 1

if(x21.lt.0)then 
x19=x90-1                       ;Number of images below reference
else
x19=x91-x90             ;Number of images above reference
endif

x11=x21+x90                     ;1st working image number
x10=x90                 ;Starting reference image number


Do lb13 i=x97,x19               ;Specifles File number

ud x11,x23,x40          ;GET TILT ANGLE
<ang>
sd -1,x24,x40,x24               ;WRITE TILT ANGLE INTO MARKER FILE
<mrk>{***x11}

;Accentuate markers in the current image
fq
<in>{***x11}
_4 
(1)
x82
;Accentuate markers in the refence file      
fq
<in>{***x10}
_3 
(1)
x82
;;;;BEGIN SEARCHING FOR ALL MARKERS ON AN IMAGE
Do lb14 x20=1,x89               ;Specify marker number
ud x20,x13,x14          ;GET MARKER COORDINATES
<mrk>{***x10}

x15=x13-25              ;TOP-LEFT X COORDINATE FOR WINDOWING
x16=x14-25              ;TOP-LEFT Y COORDINATE FOR WINDOWING

;;;WINDOW THE REFERENCE IMAGE
wi
_3 
_2
(50,50)
(x15,x16)
;;;WINDOW THE WORKING IMAGE
wi
_4
_1
(50,50)
(x15,x16)
;;;CROSS-CORRELATE THE WINDOWED IMAGES
cc
_1
_2
_5
;DETERMINE THE RELATIVE SHIFT
pk m x45,x46,x47,x48,x49
_5
   ;Keep Blank
if(x88.eq.0)then
neg
_1
_9
goto lb10 
endif
cp
_1
_9
lb10
ma
_9
_2
x81
D
A
x45,x46
pk m x45,x46,x47,x48,x49
_2
   ;Keep Blank
;UPDATE THE MARKER COORDINATES
x17=x47+x13
x18=x48+x14
;SAVE TO THE NEW MARKER FILE
sd x20,x17,x18
<mrk>{***x11}

lb14

 
x10=x11         ;WORKING IMAGE BECOMES NEW REFERENCE
x11=x11+x21     ;WORKING IMAGE UPDATED

lb13

x97=1

lb12

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
EN;;;;;;;; End  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
