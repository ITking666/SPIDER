; SOURCE: tietz2winspi.dat
;
; PURPOSE: Converts 512x512 or 1024x1024 Tietz images to SPIDER.
;          Windows off an adjustable border.
;          Rotates 90 degrees to make the tilt axis vertical.
;          Interpolates 512x512 to 900x900 for more accurate marker picking.

x21 = 1                ; For Tietz 512 sq. set to: 0, 1024 sq. set to: 1

x22 = 122              ; Number of tilt images

; ----------------- Input files --------------------------------------------

FR G                                     
[input]rawinput/raw_***         ; Raw tietz image template

; ----------------- Output files -----------------------------------

FR G
[rotandwind]rotated/rot_***    ; Rotated windowed output template 

; ----------------- END BATCH HEADER --------------------------------

IF (x21 .EQ. 0) THEN
   ; For 512x512 Tietz images
   x30 = 512
   x31 = 507            ; Window size X
   x32 = 507            ; Window size Y
   x33 = 3              ; Window left border    
   x34 = 3              ; Window right border
   x35 = 900            ; Output size
ELSE
   ; For 1024x1024 Tietz images 
   x30 = 1024           ; Input size
   x31 = 1014           ; Window size X
   x32 = 1014           ; Window size Y
   x33 = 5              ; Window left border    
   x34 = 5              ; Window right border
ENDIF

DO LB1 x11=1,x22 
   CP FROM RAW          ; Convert raw image to SPIDER format
   (16)                 ; 16 Bits in image file format                    
   [input]x11           ; Raw input images  filename   (input)
   (x30,x30)            ; Raw image size
   (512)                ; Header offset         
   (1)                  ; Endedness         
   N                    ; Do not fold negatives               
   _1                   ; Temp. file                   (output)

   WI                   ; Window central area
   _1                   ; SPIDER  file                 (input)
   _2                   ; Windowed  file               (output)
   (x31,x32)            ; Output size
   (x33,x34)            ; Cut off margin


   RT 90                ; Rotate 90 degress to align tilt axis
   _2                   ; Windowed  file                (input)
   [rotandwind]x11      ; Rotated windowed file         (output)
                        ; All columns
                        ; All rows
                        ; All slices (1)
   Z                    ; Axis
   (90)                 ; 90 degrees


   IF (x21.EQ.0) THEN
      IP                ; Interpolate larger
      [rotandwind]x11   ; Rotated windowed file               (input)
      [rotandwind]x11   ; Interpolated rotated windowed file  (output)
      (x35,x35)         ; Final size
   ENDIF
LB1

DE
_1
DE
_2

EN
