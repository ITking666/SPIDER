;window.tom
;so that inputs from WEB can use the
;WI command to select important volume

x11=0  ;first x value, 0 for all
x12=0  ;last x value, ignored if above is 0

x13=0   ;first y value, 0 for all
x14=0   ;last y value, ignored if above is 0

x15=0   ;first z value, 0 for all
x16=0   ;last z value, ignored if above is 0

;---- input files -----
FR G
[inputfile]vol001    ;volume to be windowed from

;---- output files ----
FR G
[outputfile]vol002   ;resultant volume

;---- END BATCH HEADER -----

;---get file size x y z
FI x17,x18,x19
[inputfile]
12,2,1

;---default spans
x21=x17
x22=x18
x23=x19

;---calc spans 
IF(x11.GE.1)THEN  ;x values
  x21=x12-x11+1
  IF(x21.LE.0)THEN
    vm
    echo "Must set both x limits"
    EN
  ENDIF
ENDIF

IF(x13.GE.1)THEN  ;y values
  x22=x14-x13+1
  IF(x22.LE.0)THEN
    vm 
    echo "Must set both y limits"
    EN
  ENDIF
ENDIF

IF(x15.GE.1)THEN  ;z values
  x23=x16-x15+1
  IF(x23.LE.0)THEN
    vm
    echo "Must set both z limits"
    EN
  ENDIF
ENDIF

;---operation
WI
[inputfile]
[outputfile]
x21,x22,x23
x11,x13,x15

EN

