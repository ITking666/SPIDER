; Runs verify-class-byview.spi using PBS/Torque
; 
; Requries classify-spider.pbs, gint.spi

; Set common filenames & parameters
@verify-settings

; ------------------- Parameters -------------------

[numJobs]   = 4    ; Number of jobs to divide into
[firstView] = 1    ; First reference-view
[lastView]  = 83   ; Last reference-view (<1 == all)

; -------------------- Input --------------------
;
;  [ref_view_list]    rec_0/sel_proj             List of projection views      (one)
;
; --------------- END BATCH HEADER ---------------

; Temporary files
[temp_job_list] = 'tmpjobs'
[temp_unsorted] = 'tmpsummary'

IF ( [lastView] < 1 ) THEN
    ; Get number of views
    UD N [lastView]
      [ref_view_list]
ENDIF

; Diagnostic 
[lastView]    ; last view

[numViews]    = 1 + [lastView] - [firstView]
[viewsPerJob] = [numViews]/[numJobs]

SD IC NEW
  [temp_job_list]
  1,[numJobs]

; Loop through jobs
DO LB1 [jobNum] = 1,[numJobs]

    ; Calculate first view in current job (requires integer-greater-than function)
    @gint([jobFirstView])
      ([jobNum]-1)*[viewsPerJob]
      
    [jobFirstView] = [jobFirstView] + 1

    ; Calculate last view in current job (requires integer-greater-than function)
    @gint([jobLastView])
      [jobNum]*[viewsPerJob]   ; INPUT

    [jobNumViews] = [jobLastView] - [jobFirstView] + 1
    
    SYS
      echo " Job #{%i0%[jobNum]}: First view {%i0%[jobFirstView]}, Last view {%i0%[jobLastView]}, Total views {%i0%[jobNumViews]}"
    
    ; Keep track of jobs submitted
    SD IC [jobNum], [jobLastView]
      [temp_job_list]

    SYS
      echo "qsub -v workDir=`pwd`,DATEXT='$DATEXT',Vi={%i0%[jobFirstView]},Vf={%i0%[jobLastView]} classify-qsub.sh"
    SYS
      qsub -v workDir=`pwd`,DATEXT='$DATEXT',Vi={%i0%[jobFirstView]},Vf={%i0%[jobLastView]} classify-qsub.sh
    SYS
      echo

LB1
; End job-loop


; Wait for jobs to finish

SYS
  echo ; echo " Waiting for jobs to finish"

; Loop through jobs
DO LB2 [jobNum] = 1,[numJobs]

    ; Get last view in job
    UD IC [jobNum], [LAST]
      [temp_job_list]

    SYS
    echo " Waiting for view #{%i0%[LAST]}"

    IQ SYNC DEL
      [finished_class][LAST]
      5,86400

    SYS
      echo " View #{%i0%[LAST]} completed" ; echo

LB2

SYS
  echo " Finished all jobs" ; echo

UD ICE
  [temp_job_list]

CP
  [summary_doc]
  [temp_unsorted]

DOC SORT
  [temp_unsorted]
  [summary_doc]
  1     ; Column# to sort: view#
  Y,Y   ; Renumber keys? Remove duplicated?

DE
  [temp_unsorted]

SD /       VIEWNUM      NUM_PARTS   MAX_CLASSSIZE  NUM_CLASSES  DAVIESBOULDIN
  [summary_doc]
SD E
  [summary_doc]

EN 
