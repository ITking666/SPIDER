([pixsiz],[lambda],[iter1])

; <html><head><title>Prepare for refinement by initializing input, etc.</title></head><body><pre>
;
; SOURCE:  spider/docs/techs/recon1/procs/refine-prepare.pam
;          New                                           ArDean Leith  Nov 2000
;          Declining group sort                          ArDean Leith  Oct 2010
;          CS from params, DOC STAT                      ArDean Leith  Jan 2011
;          Dala removal                                  ArDean Leith  Jan 2012
;          For CTF corrected images                      ArDean Leith  Oct 2013
;          Removed gmask                                 ArDean Leith  Apr 2015
;
; PURPOSE: Prepare various initial files  etc.
;
; CALLED FROM: refine & pub-refine <a href="./refine.pam">refine.pam</a> & 
;                                  <a href="./pub-refine.pam">pub-refine.pam</a>
;
; Register variables & files are set in: <a href="./refine-settings.pam">refine-settings.pam</a>
;
; INPUT REGISTERS (RECEIVED ABOVE):
;   [pixsiz]                Pixel size (A)
;   [lambda]                Lambda(A)
;   [iter]                  Initial iteration (Usually 1)
;
; '***' denotes group number
; INPUT FILES:
;   [params]              ../params                                  EM parameters file  
;   [vol_orig]              ../Reconstruction/vol_01                 Initial volume file  
;   [sel_group_orig]        ../Reconstruction/input/bp_sel_group     Group selection file
;   [sel_parts_orig]        ../Reconstruction/input/bp_sel_part_***  Group particle selection files 
;   [unaligned_images_orig] ../Reconstruction/input/bp_data_ctfd_*** Unaligned image stacks 
;
; OUTPUT FILES:
;   [input_dir]             input                                Directory
;   [temp_work_dir]         work                                 Directory
;   [final_dir]             final                                Directory
;   [current_vol]           final/vol_01                         Starting volume file  
;   [sel_group]             input/sel_group                      Group selection file
;   [sel_parts]             input/sel_parts_***                  Group particle selection files 
;   [group_align]           final/align_01_***                   Group alignment parameter files 
;   [unaligned_images]      input/data***                        Unaligned image stacks  (usually LINKS!)
;
; INLINE FILES USED: none

 [iter] = [iter1]            ; Needed for naming initial files

 ; Input initial parameters & file names
 @refine-settings([pixsiz],[r2],[alignsh],[prj-radius],[iter1],[iter-end],[lambda],[smallang-yn],[winsize],[incore-yn],[prepare-yn],[bp-type])

 ; Create dir. for temp and output files
 SYS                          
   mkdir -p [temp_work_dir] [final_dir] [input_dir]
   
 ; Copy initial volume to input directory 
 CP                           ; Copy volume  
   [vol_orig]                 ; Initial volume file            (input)
   [current_vol]              ; Current volume file            (output)

; Copy group selection doc file to input dir. 
 SYS                          ; Copy group selection file to input dir. 
   \cp -p [sel_group_orig].$DATEXT   [sel_group].$DATEXT 

 ; Sum total number of particles in all groups
 DOC STAT [nv],[minv],[maxv],[totp]
   [sel_group]                ; Doc file listing groups        (input)    
   2

 UD N [num-grp]               ; Get total number of groups
   [sel_group]                ; Group selection doc file       (input)


 DO [i]=1,[num-grp]           ; Loop over groups  --------------------

   ;         GROUP, PART.,     DEFOCUS              
   UD IC [i],[grp],[num-part] ; Get # of particles 
     [sel_group]              ; Group sel. doc file            (input)  

   ; Link starting unaligned images to input dir  (large files) 
   SYS                        
     ln -sf  ../[unaligned_images_orig].$DATEXT  [unaligned_images].$DATEXT  

   ; Copy particle selection files to input dir. (one/group)
   SYS                       
     \cp -p [sel_parts_orig].$DATEXT  [sel_parts].$DATEXT

   SYS                                      
     echo ' 'Group:{%I3%[grp]}  Particles:{%I6%[num-part]} 
 ENDDO                          ; End of loop over groups  ------------


 UD ICE                         ; Finished with this doc file 
   [sel_group]                  ; Doc file                    (closed)

 SYS
   echo ' Finished initial file preparation' ; echo ' '

 RE
 ; </pre></body></html>
