; PURPOSE: Runs sel-filt-byview.spi using PBS/Torque
; 
; REQUIRES: spider/docs/techs/recon/Procs/separate-spider.pbs
;           spider/docs/techs/recon/Procs/align-combine.spi

; ----------------- Parameters -----------------

[numProcs]  = 4   ; Number of processors to use (0 == all, will override value in sel-filt-byview)
[reduction] = 5   ; Size factor (integer) for optional filtered images (will override value in sel-filt-byview)
[saveAvgYN] = 1   ; Flag to save unfiltered view-average (1 == save, will override value in sel-filt-byview)
[combineYN] = 1   ; Combine alignment files? (0 == no, only needs to be done once)

; ----------- END BATCH HEADER ----------------

; Set common filenames & parameters
@verify-settings

; Check if we need to combine the alignment parameters
IQ FI [combinedExistsYN]
  [combined_align_doc]
  
IF ( [combinedExistsYN] .EQ. 0) THEN
  SYS
    echo " Can't find [combined_align_doc].$DATEXT, will generate it..." ; echo

  [combineYN] = 1
ENDIF
; (If [combined_align_doc] does exist, the option in the header will be used.)

; Combine alignment parameters
@align-combine([totParts],[numViews])
  [combineYN]   ; flag to combine alignment parameters
  

; Get most-populated view

[mostViewParts] = 0

; Loop through views
DO [view] = 1,[numViews]
  ; Get #particles
  UD N [numViewParts]
    [temp_view_sel][view]   ; generated in align-combine

  IF ( [numViewParts] .GT. [mostViewParts]) [mostViewParts] = [numViewParts]
ENDDO
; end view-loop
; (Could instead have saved [temp_how_many] in align-combine)

; Get window size
UD 17, [windowSize]
  [params]
UD E


; Estimate maximum stack size (4 bytes per pixel)

[fullViewMem] = 4*[windowSize]*[windowSize]*[mostViewParts]
[fullViewMem]
[mem]         = [fullViewMem]/([reduction]**2)
[mem]

IF ( [saveAvgYN] .GE. 0.5) THEN
  [saveAvgYN] = 1
  
  [avgMem] = 4*[windowSize]*[windowSize]*[numViews]
  [avgMem]
  
  [mem] = [mem] + [fullViewMem] + [avgMem]
  ; (filtered stack + fullsize stack + average stack)
ENDIF

; Pad memory
[paddedMem] = int(1.0*[mem]/1e+6) + 1
[paddedMem]

; Submit job
SYS M
  qsub -v DATEXT='dat' -l nodes=1:ppn={%I0%[numProcs]} -l mem={%I0%[paddedMem]}mb -v
  numProcs={%I0%[numProcs]},saveAvg={%I0%[saveAvgYN]},shrink={%I0%[reduction]} separate-spider.pbs
.
EN 
