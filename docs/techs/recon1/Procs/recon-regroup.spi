  ; <html><head><title>Regroup existing groups into new groups</title></head><body><pre>
 ;
 ; SOURCE:  spider/docs/techs/recon1/Procs/recon-regroup 
 ;
 ; PURPOSE: Regroup existing groups into new groups for reconstruction
 ;          Creates new group selection file,  new particle selection file,
 ;          new group stacks, and new alignment parameter doc files
 ;
 ; USAGE:   clean ; ./spider spi/dat @recon-regroup


 ; ------------------------ Parameters ------------------------

 [newnumgrps]     = 4     ; Number of new groups e.g. number of nodes (0 == keep same grouping)

 [want-stacks-yn] = 0     ; Want to actually create new groups?       (1 == Yes, 0 == diagnostic)

 
 [want-global-yn] = 1     ; Want global lookup table?       (0 == no)

 [want-align-yn]  = 1     ; Want alignment files also?      (0 == no)

 [want-dala-yn]   = 1     ; Want aligned image  files also? (0 == no)

; -------------------------- Inputs --------------------------

 [av_dir]         = '../Averages'                   ; Input dir                  (one)

 [old_sel_grp]    = '[av_dir]/sel_group_best'       ; Old group selection list   (one)

 [old_sel_part]   = '[av_dir]/sel_part_best_***'    ; Old particle selection lists (one/group)
 ;  WIN  MIC_NUM     MIC_WIN  GLOBAL_NUM  GRP


 [ali_dir]        = '../Reconstruction'             ; Alignment dir               (one)

 [old_align_doc]  = '[ali_dir]/output/align_01_***' ; Old alignment doc files     (one/group)

 [old_unaligned]  = '[ali_dir]/data_***'            ; Old unaligned image stacks  (one-stack/group)

 [old_aligned]    = '[ali_dir]/output/dala_01_***'  ; Old aligned   images stacks (one-stack/group)

 ; ------------------------- Outputs -------------------------

 [win_dir]        = '../win_1'                      ; Dir. for reconstruction files

 [rec_dir]        = '../rec_1'                      ; Dir. for reconstruction files

 [new_sel_grp]    = '[win_dir]/sel_group'           ; New group selection file      (one)

 [new_sel_part]   = '[win_dir]sel_part_***'         ; New particle selection files  (one/group) 
 ; WIN MIC_NUM       MIC_WIN  GLOBAL_NUM  GRP

 [part2global]    = '[win_dir]/part2glonum_****'    ; Global particle lookup tables (one/new-group) (OPTIONAL)

 [new_align_doc]  = '[rec_dir]/align_01_***'        ; New aignment parameter files  (one/group) 

 [new_unaligned]  = '[rec_dir]/data_***'            ; New particle stacks           (one/group)

 [new_aligned]    = '[rec_dir]/dala_01_***'         ; New aligned images            (one-stack/new-group)


 ; --------------------- END BATCH HEADER ---------------------

 MD                                 ; Skip unnecessary output 
   VB OFF
 MD                                 ; Skip unnecessary output 
   TR OFF

 SYS                                ; Create output directory if needed
    mkdir -p [win_dir]


 UD N [old-num-grps]                ; Find number of old groups (usually micrographs)
   [old_sel_grp]                    ; Old group (micrograph) selection file (input)
 
 ; If regrouping not required, simply link xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
 IF ( [newnumgrps] == 0 ) THEN

   SYS
     echo " No regrouping, linking pre-existing group files   "  ;  date '+ TIME: %x  %X' 

;   DE
;     [new_sel_grp]

   ; Link group selection doc file
   SYS
     ln -sf  ../[old_sel_grp].$DATEXT  [new_sel_grp].$DATEXT

   ; Get number of old groups
   UD N [num-grps]
     [new_sel_grp]                  ; Group selection file  (input)

   ; Loop through old groups
   DO [old-grp] = 1,[old-num-grps]  ; Loop through old groups ----------------------
     [new-grp] = [old-grp]

     ; Link particle selection doc files
     SYS
       ln -sf ../[old_sel_part][old-grp].$DATEXT      [new_sel_part][new-grp].$DATEXT

     ; Link alignment doc files
     SYS
       ln -sf ../[old_align_doc][old-grp].$DATEXT     [new_align_doc][new-grp].$DATEXT

     IF ( [want-stacks-yn] .NE. 0 ) THEN
       ; Stacks requested, link stack files
       SYS
         ln -sf  ../[old_unaligned][old-grp].$DATEXT  [new_unaligned][new-grp].$DATEXT
     ENDIF

     IF ( [want-dala-yn] > 0 ) THEN
       ; Want aligned images (dala files), link aligned image files
       SYS
         ln -sf  ../[old_aligned][old-grp].$DATEXT    [new_aligned][new-grp].$DATEXT
     ENDIF

   ENDDO                            ; End group loop -----------------------------

   EN                               ; Finished (return)

 ENDIF



 ; Regrouping requested xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

 SYS
   echo -n " Regrouping files   "  ;  date '+ TIME: %x  %X' 


 [ntot] = 0                         ; Initialize overall cumulative total

 DO                                 ; Loop over all old groups ----------------------------------

   UD NEXT [key], [ingrp]           ; Get old group (micrograph) number
     [old_sel_grp]                  ; Old group (micrograph) selection file (input)
   IF ([key] <= 0) EXIT

   ; Accumulate total number of particles
   UD N [numparts]
     [old_sel_part][ingrp]          ; Old particle selection file   (input) 

   SYS
     echo " Old group: {%I3%[ingrp]} had:{%I6%[numparts]} particles."

   ; Count number of selected particles
   [ntot] = [ntot] + [numparts]        
 ENDDO

 UD NEXT E                          ; Finished with doc file
  [old_sel_grp]                     ; Finished with doc file      (input)

 ; Compute number of particles for each new group
 [newnpg] = [ntot]/[newnumgrps]     ; Number of particles in new groups
![newnpg] = INT([newnpg] + 0.5)     ; Number of particles in new groups
 [newnpg] = INT([newnpg])           ; Number of particles in new groups
 [numt]   = [newnpg] + 1
 SYS
   echo ; echo -n " Restacking: {%I5%[ntot]} particles"  
 SYS
   echo " From: {%I3%[old-num-grps]} groups into: {%I3%[newnumgrps]} groups, with {%I5%[newnpg]} particles each."
 SYS
   echo ' '

 IF ( [newnumgrps] == 0 ) THEN
   EN
 ENDIF
   
 DE                                        ; Remove any new group selection file 
   [new_sel_grp]                           ; New group selection file      (removed)
 SD /        GROUP      NUM_PARTICLES  
   [new_sel_grp]                           ; Group selection doc file      (output)
 DE                                         ; Remove any new group selection file 
   [new_sel_grp]                            ; New group selection file      (removed)
 SD /        GROUP       NUM_PARTS
   [new_sel_grp]                            ; Group selection doc file      (output)
 [one] = 1
 DE A
   [new_sel_part][one]
 DE A                 
   [new_aligned][one]  
 DE A                 
   [part2global][one]
 DE A                 
   [new_align_doc][one]
 DE A                 
   [new_unaligned][one]

 [outgrp]  = 0                             ; Initialize output group number
 [outpart] = [newnpg] + 1                  ; To force new output group
 [needold] = 1                             ; To force new input group
 [part_counter] = 0                        ; Initialize overall counter
 
 DO                                        ; Loop over all old particles ----------------
 
   IF ( [needold] >= 1 ) THEN 
     ; Start another input group (micrograph)

     IF ( [want-align-yn] > 0) THEN
       UD ICE                              ; Close doc file
         [old_align_doc][ingrp]            ; Old alignment doc file (closed)
     ENDIF
     UD NEXT E                             ; Close doc file
         [old_sel_part][ingrp]             ; Finished with this particle selection file (closed)

     UD NEXT [key], [ingrp]                ; Get next group (micrograph) number
       [old_sel_grp]                       ; Group (micrograph) selection file    (input)
     IF ( [key] <= 0 ) EXIT                ; Finished with all old groups

     [needold] = 0                         ; Flag to use old group
   ENDIF


   [calcgrpnum] = int([part_counter]*[newnumgrps]/[ntot])+1  ; The group number we *should* be on

;   IF ( [outpart] > [newnpg] ) THEN  ; (OUTPART is incremented after this step, so OUTPART+1 would get included)
   IF ( [calcgrpnum] > [outgrp] ) THEN 
      [part_counter]  ; DIAGNOSTIC 
      [calcgrpnum]
      [outgrp]

     ; Must start another output group

     IF ( [outgrp] > 0 ) THEN
       ; Not first output group, give details on last output group
       SYS
         echo " New group: {%I3%[outgrp]} has:{%I6%[outpart]} particles."

       SD E                                ; Close doc file
         [new_sel_part][outgrp]            ; Finished with this particle selection file (closed)
       SD E                                ; Close doc file
         [new_align_doc][outgrp]           ; Finished with alignment file (closed)
       SD E                                ; Close doc file
         [part2global][outgrp]             ; Global number doc file     (closed)

       SD [outgrp], [outgrp],[outpart]     ; Place group # in group selection file
         [new_sel_grp]                     ; Group selection file        (output)

       !sys
       !echo 'grp: {***[outgrp]}  outpart: {*****[outpart]}'

     ENDIF

     [outgrp]  = [outgrp] + 1              ; New output group #

     IF ( [outgrp] <= [newnumgrps] ) THEN

       ; Delete pre-existing files and label new doc files

       DE                                    ; Remove existing particle selection file 
         [new_sel_part][outgrp]              ; Particle selection  file    (removed)
       SD /          WIN        MIC_NUM       MIC_WIN    GLOBAL_NUM        GRP
         [new_sel_part][outgrp]              ; particle selection file (output)

       DE                                    ; Remove existing output stack
         [new_unaligned][outgrp]             ; Particle stack file    (removed)            

       IF ( [want-global-yn] > 0) THEN
         DE                                  ; Remove existing global lookup file 
           [part2global][outgrp]             ; Existing global number lookup file (removed)
         SD / KEY: WIN-NUM     REG: GLOBAL-NUM  MICROGRAPH   
           [part2global][outgrp]             ; Label for global lookup doc file (output)
       ENDIF

       IF ( [want-align-yn] > 0) THEN
         DE                                  ; Remove existing alignment file 
           [new_align_doc][outgrp]           ; Existing global alignment file (removed)
         SD /  KEY       PSI,    THE,    PHI,   REF#,    EXP#,  CUM.{ROT,   SX,    SY},  NPROJ,   DIFF,  CCROT,     ROT,     SX,     SY,   MIR-CC
           [new_align_doc][outgrp]           ; New global number doc file (output)
       ENDIF

     ENDIF

     [outpart] = 0                         ; Reset output particle # to zero
   ENDIF

   UD NEXT [key], [inpart],[mic],[micwin],[glonum],[tgrp]    ; Get next input particle number
     [old_sel_part][ingrp]                  ; Old particle selection file    (input)

   IF ( [key] <= 0 ) THEN
     [needold] = 1                         ; Flag for need to start next input group
     CYCLE                                 ; Finished with this input group
   ENDIF



   ; Have old and new particle numbers now xxxxxxxxxxxxxxxxx
   [outpart] = [outpart] + 1                  ; New output particle # 
   [part_counter] = [part_counter] + 1

   ; Restacking
   CP
     [old_unaligned][ingrp]@******[inpart]    ; Existing particle stack  (input)
     [new_unaligned][outgrp]@******[outpart]  ; New particle stack       (output)

   SD [outpart], [outpart],[mic],[micwin],[glonum],[outgrp]  ; Save new particle data
     [new_sel_part][outgrp]                   ; New particle selection file   (output)

   IF ( [want-global-yn] > 0) THEN
     ; Want global look-up table
     SD [outpart],[glonum],[mic]              ; Save lookup table
       [part2global][outgrp]                  ; New global llk-up doc file (output)
   ENDIF

   IF ( [want-dala-yn] > 0) THEN
     ; Want old aligned images (dala files)
     CP
       [old_aligned][ingrp]@******[inpart]    ; Existing aligned particle stack  (input)
       [new_aligned][outgrp]@******[outpart]  ; New alignedparticle stack       (output)
   ENDIF

   IF ( [want-align-yn] > 0 ) THEN

     ; Want new alignment doc file
     UD IC [inpart], [r1],[r2],[r3],[r4],[r5],[r6],[r7],[r8],[r9],[r10],[r11],[r12],[r13],[r14],[r15]
       [old_align_doc][ingrp]                  ; Old alignment doc file     (input)

     SD [outpart],[r1],[r2],[r3],[r4],[outpart],[r6],[r7],[r8],[r9],[r10],[r11],[r12],[r13],[r14],[r15]
       [new_align_doc][outgrp]                 ; New alignment doc file     (output)
   ENDIF
 ENDDO

 IF ( [outpart] > 0 ) THEN
   SD [outgrp], [outgrp],[outpart]     ; Place group # in group selection file
     [new_sel_grp]                     ; Group selection file        (output)

   SYS
     echo " New group: {%I3%[outgrp]} has:{%I6%[outpart]} particles."

   SD E                                    ; Close doc file
     [new_sel_part][outgrp]                ; Output particle selection file (closed)
 ENDIF

 UD ICE                                    ; Close doc file
   [old_align_doc][ingrp]                  ; Old alignment doc file    (closed)
  [dummy] = -[newnumgrps]
 SD /          PARTSBEFORE   PARTS_AFTER
   [new_sel_grp]
 SD [dummy], [ntot],[part_counter]
   [new_sel_grp]
;;UD NEXT E                                 ; Close doc file
 SD E                                      ; Close doc file
   [new_sel_grp]                           ; Group selection doc file  (closed)

 SYS
   echo ' '

 EN

 ; Modified 2013-12-02
 ;    2016-03-25 (agl) -- File locations
 ;    2013-12-02 (trs) -- Made numbering for each group consistent with RESTACK-N-CTF
 ;    2013-10-16 (agl) -- Modernized syntax, rewritten
 ;    2012-04-16 (trs) -- If grouping not required, creates softlinks to old files
 ;    2012-07-19 (trs) -- Creates output directory if needed
 ;    2012-05-01 (trs) -- Optionally writes global particle number to header position 42
 ;    2012-04-05 (trs) -- Doesn't use output from pnums.spi anymore
 ;    2012-04-05 (trs) -- Adapted from listallparticles.spi
 ;    2010-09-02 (trs & ay) -- Trap for >999 micrographs for 'SD [dummy]' line
 ;    2009-10-16 (trs) -- Removed option for unstacked images
 ;    2008-10-03 (trs) -- Added output file part2global
 ;    2008-10-03 (trs) -- Assigns global particle number
 ;    2007-12-17 (trs) -- Optional parameter for maximum number of particles per micrograph
 ;    2007-10-10 (trs) -- GEts number of micrographs from SEL_MICROGRAPH 
 ; </pre></body></html>
