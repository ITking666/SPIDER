 ; <html><head><title>Creates gnuplot file of refinement resolution curves</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/newprogs/plot-fsc-curves.spi 
 ;             Previously named: plotref.spi        Jan 2010  ArDean Leith
 ;             Previously named: plotrefres.spi     Oct 2013  ArDean Leith
 ;
 ; PURPOSE: Plots FSC resolution curves for each iteration of refinement 
 ;   
 ; I/O PARAMETERS AND FILES ARE SET HERE:
 ;
 ; <b> ------------ Parameters ----------------------------------------

 [cut] = 0.143;   0.5                         ; Resolution cutoff (0.5 or 0.143) 

 ; ------------ Input files ----------------------------------------

 [resolution] = 'final/resolutions'          ; File with number of iterations

 [fsc]        = 'final/fscdoc_m_{**[iter]}'  ; Iteration FSC curves

 [params]     = '../params'                  ; Parameter doc file  (optional)  

 ; --------------- Output files  -------------------------------------

 [gnuplot_iter] = 'fsc_iter.gnu'             ; Text file of gnuplot commands 

 ; -------------- END BATCH HEADER ---------------------------------- </b>

 IQ FI [p-exists]
   [params]                         ; Parameter doc. file

 IF ( [p-exists] > 0) THEN
   UD 5, [sp_pixsiz]                ; Get pixelsize (A)   
     [params]                       ; Parameter doc. file (input)

   [maxspfreq] = 0.5 /[sp_pixsiz]
   [t] = 0.25 / [maxspfreq]
 ENDIF
 

 UD N [niter]                       ; Find last iteration number
   [resolution]                     ; Resolution file   (input)

 SYS                                ; Remove gnuplot text file
   \rm -f [gnuplot_iter]            
 
 SYS 
   echo set title \"$DATEXT FSC  for Iterations \" >> [gnuplot_iter]

 SYS 
   echo set xlabel \"Frequency\" >> [gnuplot_iter]

 SYS 
   echo set style data lines >> [gnuplot_iter]

 ; Use a black line (color = -1) for the final curve
 SYS 
   echo set style line 32 lt -1 >> [gnuplot_iter]

 ; Put line at resolution cut-off 
 SYS 
   echo "f(x) = {%f4.3%[cut]}" >> [gnuplot_iter]

 SYS 
   echo plot \\ >> [gnuplot_iter]

 [iterend] = [niter] + 1

 DO [iter] = 2,[iterend]

   IQ FI [exists]
     [fsc]
   IF ( [exists] == 0 ) CYCLE

   IF ( [iter] == [iterend] ) THEN   ; Last curve gets a thick black line (ls 32)
      SYS
        echo \"[fsc].$DATEXT\" using 3:5 title \"[fsc]\" ls 32, \\ >> [gnuplot_iter]
   ELSE
      SYS
        echo \"[fsc].$DATEXT\" using 3:5 title \"[fsc]\" , \\ >> [gnuplot_iter]
   ENDIF
 ENDDO

 SYS
   echo "f(x) notitle" >> [gnuplot_iter]

 IF ( [p-exists] > 0 ) THEN
   SYS 
     echo " Exists: {**[p-exists]}  Pixsiz: {%f6.1%[sp_pixsiz]}  Maxspfreq: {%f6.2%[maxspfreq]}  t:{**[t]}"

   SYS
      echo "f(x) = (x{*{%f5.1%[t]}) notitle" 
 ENDIF

 SYS                                 
   echo  ' 'Displaying plot using: gnuplot -persist [gnuplot_iter]; echo   
 SYS                          ; Plot the file                       
   gnuplot -persist [gnuplot_iter]  

 EN
 
 ; Modified 2013-12-02
 ;    TO DO: change/add Angstroms to horizontal axis
 ;    2014-05-14 (agl) -- No long has group plots
 ;    2013-12-03 (trs) -- Updated deprecated Gnuplot syntax
 ;    2013-12-02 (trs) -- Reporting masked FSC 
 ;    2013-11-28 (trs) -- Plot didn't substitute group numbers

 ; </pre></body></html>


