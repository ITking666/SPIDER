 ([prj-radius],[iter],[grp])

 ; <html><head><title>Volume projection loop</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/gold/refine-prjloop.spi
 ;         New                              ArDean Leith  Nov 2002
  ; 
 ; PURPOSE: Projection loop.  Runs for each parallel group on each iteration.
 ;          Creates projections from reference volume 
 ;
 ; CALLED FROM: <a href="./pub_refine_start.spi">pub_refine_start</a> OR   
 ;              <a href="./refine.spi">refine.spi</a>  
 ;
 ; REGISTERS AND FILES ARE SET IN: <a href="refine settings.spi">refine settings.spi</a>
 ;
 ; INPUT REGISTERS:
 ;   [prj-radius]              Radius (pixels) of structure for projection
 ;   [iter]                    Alignment step iteration counter  
 ;   [grp]                     Current defocus group                      
 ;
 ;  '##' denotes iteration,  '%' denotes subset, and '***' denotes group
 ; INPUT FILES:
 ;   [sel_group]               input/sel_group          Group selection file         (one)
 ;   [iter_refangs]            work/ang_refs_##         Reference angles doc file    (one)      
 ;   [current_vol]             final/vol_##_s%          Current reference volume     (two) 
 ;
 ; OUTPUT FILES:
 ;   [ref_projs_s]           = work/refproj_##_s%'      Reference projections        (two/iter)
 ;   [ref_projs_s_grp]       = work/refproj_##_s%_***@' Group reference projections  (two/group/iter) (deleted)
 ;    
 ; ----------------- END BATCH HEADER -------------------------------------

 UD N [num-grps]            ; Find number of groups
   [sel_group]              ; Group selection file      (input)

 UD N [num-angs]            ; Get number of reference images used
   [iter_refangs]           ; Reference images angles doc. file (input)

 [num-in-grp] = INT ( [num-angs] / [num-grps])
 IF ( ([num-in-grp] *[num-grps]) < [num-angs]) [num-in-grp] = [num-in-grp] + 1
  
 [igo]  = ([grp] - 1) * [num-in-grp] + 1
 [iend] = [igo] + [num-in-grp] - 1
 IF ( [iend] > [num-angs] ) [iend] = [num-angs] 
 [ngot] = [iend] - [igo] + 1

 SYS
   echo  "  Starting: refine-prjloop  Iteration: {%I0%[iter]}  Group:{%I3%[grp]}  Projections: {%i4%[igo]} ... {%i4%[iend]} " 

 DE
   [iter_refangs_grp]      ; Reference projection angles doc file (removed)

 [out] = 0

 MD                           
   RESULTS OFF             ; Discard further results file info
 DO [i] = [igo],[iend]
   UD IC [i],[psi],[theta],[phi]
     [iter_refangs]        ; Reference projection angles doc file (input)
 
   [out] = [out] + 1
   SD [out],[psi],[theta],[phi],[i],[igo],[iend]
     [iter_refangs_grp]    ; Reference projection angles doc file (output)
 ENDDO
 MD                           
   RESULTS ON              ; Restart results file info

 UD ICE                    ; Close doc file
   [iter_refangs]          ; Reference projection angles doc file (closed)
 SD E                      ; Close doc file
   [iter_refangs_grp]      ; Reference projection angles doc file (closed)

 MY FL                     ; Flush results file

 ; Create stacks holding this iterations's set of reference projections from current volume subsets.
 
 [s] = 1                   ; Subset
 PJ 3F                     ; Projection operation
   [vol_s]                 ; Current reference volume             (input) 
   [prj-radius]            ; Radius of object
   1-[ngot]                ; Reference angles used    
   [iter_refangs_grp]      ; Reference angles doc file            (input)
   [ref_projs_s_grp]       ; Reference projection stack           (output) 


 [s] = 2                   ; Subset
 PJ 3F                     ; Projection operation
   [vol_s]                 ; Current reference volume             (input) 
   [prj-radius]            ; Radius of object
   1-[ngot]                ; Reference angles used    
   [iter_refangs_grp]      ; Reference angles doc file            (input)
   [ref_projs_s_grp]       ; Reference projection stack           (output) 

 RE

 ; </pre></body></html>

