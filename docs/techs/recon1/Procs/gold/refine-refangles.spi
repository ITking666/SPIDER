([iter],[smallang-yn],[ang-step-sm],[theta-sm],[ampenhance],[ang-step],[ang-limit],[num-ang])

 ; <html><head><title>Sets reference projection angles</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/refine_setangles.spi
 ;         Removed from main refine procedure       ArDean Leith  May 2014
 ;
 ; PURPOSE: Create document file containing angles for reference projections.
 ;          Also sets amplitude enhancement flag use (OPTIONAL)
 ;
 ; INPUT REGISTERS:     
 ;         [iter]         ; Current iteration
 ;         [smallang-yn]  ; Flag to use small angle refinement (1 == Yes)
 ;         [ang-step-sm]  ; Theta angular step in small angle refinement        
 ;         [theta-sm]     ; Theta range in small angle refinement
 ;
 ; OUTPUT REGISTERS:
 ;         [ampenhance]   ; Flag to use amplitude enhancemnt on this iteration
 ;         [ang-step]     ; Theta angular step              (varies with iteration)
 ;         [ang-limit]    ; Restriction on angular search   (varies with iteration)
 ;         [num-ang])     ; Number of reference projection angles
 ;
 ; -- END BATCH HEADER -- ----------------------------------------------------
 
 IF ( [smallang-yn] == 0 ) THEN  

   ; For usual angle refinement
   RR S [ang-step]       ; Get current angular step (varies with iteration)
     [ang-steps]         ; Angular steps for projection angle for all iterations
     [iter]              ; Current iteration

   VO EA [num-ang]       ; Sets [num-ang] to number of reference projections
     [ang-step]          ; Theta angular step          (varies with iteration)
     0, 90               ; Theta range, 90 is for use with 'Check Mirrored Positions'
     0, 359.9            ; Phi range
     [iter_refangs]      ; Reference projection angles doc file     (output)

   RR S [ang-limit]      ; Get restriction on angular search   (varies with iteration)
     [ang-limits]        ; Restriction on angular search for all iterations
     [iter]              ; Current iteration

 ELSE                    ; Small angle refinement

   ; For small angle refinement around a determined position
   VO EA [num-ang]       ; Sets [num-ang] to number of reference projections
     [ang-step-sm]       ; Theta angular step          (varies with iteration)    
     0, [theta-sm]       ; Theta range 
     0, 359.9            ; Phi range
     [iter_refangs]      ; Reference projection angles doc file     (output)
 ENDIF
 
 RR S [ampenhance]       ; Amplitude enhancement flag (varies with iteration)
   [amp-enhance-flags]   ; Global string variable
   [iter]                ; Current iteration

 RE

 ; </pre></body></html>
