 ; <html><head><title>Shows aligment radius on central slice of volume</title></head><body><pre>
 ;
 ; SOURCE:  refine-show-radii.spi
 ;                                          
 ; PURPOSE: Shows aligment radius on central slice of volume.
 ; 
 ; USAGE:   spider spi/dat @refine-show-radii 
 ;
 ; I/O PARAMETERS AND FILES ARE SET HERE:
 ;
 ; Input Variables   -------------------------------------
 ;
 ; [r2]            ; Aligment radius 
 ; [alignsh]       ; Alignment shift
 ; [prj-radius]    ; Projection radius
 ;
 ; ------------------ Input files -------------------------------------

 ; [vol_orig]    = Read input volume name from caller, e.g.: 'final/vol_02_all'  

 ; ---------------  Output files  -------------------------------------

 [centout]  = '[vol_orig]_cent_slice'   ; Annotated central slice from volume

 ; -------------- END BATCH HEADER ------------------------------</b>
 
 [s] = 1
 
 ; Input initial parameters & file names but not angular steps
 @refine-settings([pixsiz],[r2],[alignsh],[prj-radius],[iter1],[iter-end],[sphdecon],[smallang-yn],[qsub],[incore-yn],[gold-std],[bp-type])

 FR                : Read input volume name from caller, e.g.: 'final/vol_02_all'
   ?Input volume? [vol_orig]       

  ; Find location of central slice in volume
 FI CEN [xc],[yc],[zc]  
   [vol_orig]      ; Volume file    (input)

 ; Extract central slice from volume
 PS Z              ; Pick central slice
   [vol_orig]      ; Volume file   (input)
   [centout]       ; Central Image (output)
   [zc]            ; Slice

 !SYS
 !  echo "  Alignment radius: {%f6.1%[r2]} Shift: {%f6.1%[alignsh]}" ; echo

 [r2sh] = [r2] + [alignsh]   ; Add the alignment shift

 PT                ; Create Pattern or Mask File
   [centout]       ; Image file    (input/output)
   CLS             ; Outlined circle
   [xc],[yc]       ; Center
   [r2]            ; Radius
   Yes             ; Make another object?
   CLS             ; Outlined circle
   [xc],[yc]       ; Center
   [r2sh]          ; Radius
   Yes             ; Make another object?
   CLS             ; Outlined circle
   [xc],[yc]       ; Center
   [prj-radius]    ; Radius
   No              ; Make another object?

 ; Display filtered central image slice
 CP TO JPG         ; Copy to JPEG and display
   [centout]       ; Image file    (input)
   [centout]       ; JPEG Image    (invol)
 SYS
   display [centout].jpg  &

 SYS
   echo   

 RE
 ; </pre></body></html>


 EN D
 