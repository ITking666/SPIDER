 ; <html><head><title>CTF corrects selected images using CTF correction doc file</title></head><body><pre>
 ;
 ; SOURCE:  spider/docs/techs/recon1/Procs/apply-ctfcor-mic.spi
 ;
 ; PURPOSE: CTF corrects selected micrographs using CTF correction doc file
 ;          Alternative to correcting images using:  restack-n-ctf
 ;
 ; USAGE:   clean ; spider spi/dat @apply-ctfcor-mic

 ; ------------------ Parameters ------------------
 
 [progress-n]  = 1           ; Print progress message every Nth micrograph

 ; -------------------- Inputs --------------------

 [mic_dir]    = '../Micrographs'            ; Output directory           (one)

 [params]     = '../params'                 ; Parameter doc file         (one)

 [sel_mic]    = '../sel_micrograph'         ; Micrograph selection file  (one)

 [micgr]      = '[mic_dir]/raw****'         ; Raw micrograph images      (one / micrograph)

 [ctf_cor]    = 'power/flipctf_****'        ; CTF correction files       (one / micrograph)

 ; -------------------- Outputs --------------------

 [ctfd_micgr] = '[mic_dir]/flipd_mic_****' ; CTF-corrected micrographs (one / micrograph)

 ; --------------- END BATCH HEADER ---------------

 MD
   SET MP
   0
 MD
   VB OFF

 ; Get number of micrographs
 UD N [num-micgr]
   [sel_mic]

 DO                                 ; Loop over all micrographs ---------------

   UD NEXT [mic-key], [mic]         ; Get micrograph number
     [sel_mic]                      ;  Micrograph selection file (input)
     IF ( [mic-key] <= 0 ) EXIT

    IF (int([mic-key]/[progress-n]) == [mic-key]/[progress-n]) THEN
      SYS
        echo " Processing Micrograph: {%I4%[mic]}, {%I4%[mic-key]} out of: {%I4%[num-micgr]}"
    ENDIF

    ; Check if micrograph file exists
    IQ FI [exists-yn]
      [micgr][mic]                 ; Micrograph selection file (input)
    IF ([exists-yn] == 0) CYCLE    ; Skip non-existing micrographs

    ; Filter micrograph using CTF correction file
    TF COR
      [micgr][mic]                 ; Raw micrograph file  (input)
      [ctf_cor][mic]               ; CTF correction file       (input)
      [ctfd_micgr][mic]            ; CTR corrected micrograph (output)

 ENDDO                             ; End micrograph loop  -----------------

 EN

 ; </body></pre></html>
