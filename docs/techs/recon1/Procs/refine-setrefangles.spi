([iter],[smallang-yn],[ampenhance],[ang-step],[ang-limit],[num-ang])

 ; <html><head><title>Sets reference projection angles</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/refine_refangles.spi
 ;         From refine-inseries         ArDean Leith  May 2014
 ;
 ; INPUT REGISTERS:     
 ;       [iter]          ; Current iteration
 ;       [smallang-yn]   ; Flag to use small angle refinement (1 == Yes)
 ;       [ang-step-sm]   ; Theta angular step          
 ;       [theta-sm]      ; Theta range
 ;
 ; OUTPUT REGISTERS:
 ;       [ampenhance]    ; Flag to use amplitude enhancement on this iteration
 ;       [ang-step]      ; Theta angular step              (may vary with iteration)
 ;       [ang-limit]     ; Restriction on angular search   (varies with iteration)
 ;       [num-ang]       ; Number of reference projection angles
 ;
 ; -- END BATCH HEADER -- ...................................................
 
 DE                      ; Remove existing doc file
   [iter_refangs]        ; Ref. angles doc file             (input)

 IF ( [smallang-yn] == 0 ) THEN  

   ; For normal angle refinement
   RR S [ang-step]       ; Get current angular step (varies with iteration)
     [ang-steps]         ; Angular steps for projection angle for all iterations
     [iter]              ; Current iteration

   VO EA [num-ang]       ; Sets [num-ang] to number of reference projections
     [ang-step]          ; Theta angular step          (varies with iteration)
     0, 90               ; Theta range, 90 is for use with 'Check Mirrored Positions'
     0, 359.9            ; Phi range
     [iter_refangs]      ; Reference projection angles doc file     (output)

   RR S [ang-limit]      ; Get restriction on angular search   (varies with iteration)
     [ang-limits]        ; Restriction on angular search for all iterations
     [iter]              ; Current iteration

 ELSE                    ; Small angle refinement

   ; For Small angle refinement around a determined position
   RR S [ang-step]       ; Get angular step 
     [ang-steps-sm]      ; Angular steps for projection angle 
     1                   ; Selection

   RR S [theta-sm]       ; Get theta angle 
     [thetas-sm]         ; Theta  for projection angle 
     1                   ; Selection

   VO EA [num-ang]       ; Sets [num-ang] to number of reference projections
     [ang-step]          ; Theta angular step               
     0, [theta-sm]       ; Theta range 
     0, 359.9            ; Phi range
     [iter_refangs]      ; Reference projection angles doc file     (output)
 ENDIF
 

 RR S [ampenhance]       ; Amplitude enhancement flag (varies with iteration)
   [amp-enhance-flags]   ; Global string variable
   [iter]                ; Current iteration

 RE

 ; </pre></body></html>
