 ; <html><head><title>Multiplies reference volume by CTF & creates ref projections</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/make-ref-views.spi
 ;
 ; PURPOSE:  Creates reference projections from a reference volume.
 ;
 ; The following values are retrieved from the params file
 ;   [numgrp]   = Number of defocus groups
 ;   [pix_size] = Pixel size, Angstroms
 ;   [pixels]   = Array dimension, pixels
 ;   [ee]       = Electron energy
 ;   [spab]     = Spherical aberration
 ;   [ss]       = Source size
 ;   [defspr]   = Defocus spread
 ;   [acr]      = Amplitude contrast ratio
 ;   [geh]      = Gaussian envelope halfwidth
 ;
 ; USAGE:        clean ; ./spider spi/dat @make-ref-views

 ; <b> ------------ Parameters ---------------------------------------

 [dtheta] = 15     ; Delta theta: 15 gives 83 projections

 [radius] = -1     ; Radius: use -1 for default value

 ;    ------------ Input files ---------------------------------------

 [params]        = '../params'            ; Parameter doc file     (one)

 [refvol]        = '../reference_volume'  ; Reference volume       (one)

 [sel_group]     = 'sel_group'            ; Group selection file   (one)

 ; --------------- Output files  -------------------------------------

 [refangles]     = 'refangles'            ; Projection angles doc file  (one)

 [prj]           = 'prj_01'               ; Projection image stack file (one)

 [ref_view_list] = '../Averages/sel_proj' ; List of reference views     (one)

; -------------- END BATCH HEADER ---------------------------------</b>

 MD
   TR OFF                  ; Decrease results file output
 MD
   VB OFF                  ; Decrease results file output
 MD 
   SET MP 
   0                       ; Use all available processors  
 
 SYS
   mkdir -p ../Averages

 DE                        ; Delete 
   [refangles]             ; Doc file of angles       (removed)
 DE                        ; Delete 
   [ref_view_list]         ; Selection doc file       (removed)

 ; Get parameters
 IF ( [radius] < 0 ) THEN  ; Get actual size and compute radius
   UD 18, [sp_partsiz]
    [params] 
    [radius] = 0.69 * [sp_partsiz]
 ENDIF

 ; Create the reference angles document file
 VO EA [numang]            ; Create equally dispersed angles
   [dtheta]                ; Delta theta
   0,90                    ; Range of theta
   0,359.99                ; Range of phi
   [refangles]             ; Doc file of angles       (output)

 [numang] = [numang] - 1   ; Skip duplicate angle
 [numang]  

 ; Generate view-list
 DOC CREATE
   [ref_view_list]         ; View list doc file    (output)
   1                       ; Column to be filled
   1-[numang]              ; Numbers to put in column

 SYS
     echo  ' 'Creating: {%I0%[numang]} reference projections

 ; Create projections of the ref volume
 PJ 3Q                     ; Generate projections 
   [refvol]                ; Volume to be projected     (input)
   [radius]                ; Radius
   1-[numang]              ; List of file numbers
   [refangles]             ; Proj. angles doc file      (input)
   [prj]@****              ; Projection stack           (output)

 SYS
   echo ' '   

 EN 
 ; </pre></body></html>

