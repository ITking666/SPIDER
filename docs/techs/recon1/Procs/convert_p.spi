 ([dec]) 

 ; <html><head><title>Converts a scanned file to a SPIDER format</title></head><body><pre>
     
 ; SOURCE: spider/docs/techs/recon1/Procs/convert-p.spi

 ; PURPOSE: Converts a scanned file to a SPIDER format.
 ;
 ; [dec] = decimation factor for 'DC S' command
 ;  0 = Get value from param file (key=16)
 ;  1 = Full sized image
 ;  2 = 1/2 size
 ;  4 = 1/4 size

 FR
   ?Parameter file? [params]

 FR
   ?Input micrograph? [raw]

 FR
   ?SPIDER output file? [spider]

 ;  -----  Get zip & format flags from parameter file -----------------
 UD 1, [zip]
   [params]
 UD 2, [format]
   [params]

 ; ----------- Checks if files are gzip compressed -------------------

 IF ( [zip]*[format] > 0) THEN
    SYS                          ; Both tif & zip flags set
      gunzip [raw].gz
    SYS               
      echo " Unzipped: [raw].gz"

 ELSEIF ( [zip] > 0 ) THEN 
    SYS                          ; Zip, but not tif, flag set
      echo " Unzipping file "
    SYS
      gunzip [raw].$DATEXT.gz
    SYS               
      echo " Unzipped: [raw].$DATEXT.gz"

 ENDIF

 ; ----------- Conversion based on scanner type -------------------

 IF ( [format] == 0 ) THEN
   CP             ; Already a SPIDER file, put in output file
     [raw]        ; File   (input)
     [spider]     ; File   (output)

   SYS
     echo " Copied SPIDER file: [raw]   To: [spider]"

 ELSEIF ( [format] == 1 ) THEN
    UD 3,[nsam]   ; HiScan raw file, Get X,Y size parameters 
      [params]    ; Params file    (input)
    UD 4,[nrow]   ;         
      [params]

    CP FROM RAW
      16            ; Bits / pixel
      [raw]         ; File (input)
      [nsam],[nrow] ; Size
      342           ; Header bytes
      (1)
      N
      _1            ; File  (output)

    AR
      _1            ; File   (input)
      [spider]      ; File   (output)
      P1*(-1)+5

    SYS
      echo " Converted Hiscan: [raw]   To: [spider]"

 ELSEIF ( [format] == 3 ) THEN     
    SYS              ; ZI tif file.  Overview should always = 1
      zi2spi [raw] [spider].$DATEXT 1

    SYS
      echo " Converted ZI file: [raw]   To: [spider].$DATEXT"

 ELSEIF ( [format] == 4) THEN     
    CP FROM NIKON   ; Nikon Tif Scanner file
      [raw] 
      [spider]

    SYS
      echo " Converted Nikon TIF: [raw]   To: [spider]"

 ELSEIF ( [format] == 5) THEN     
;    CP FROM MRC
;      [raw]
;      [spider]
;      N,Y   ; No byte flip, flip top+bottom

    [temp_spi] = 'tmpspi-unflipped'
    
    SYS
      [xic] -i [raw] -o [temp_spi].$DATEXT
;;;      [xic] -i [raw] -o [spider].$DATEXT
    
    MR
      [temp_spi]
      [spider]
      Y   ; Flip around _y_-axis
    
    DE
      [temp_spi]
      
    SYS
      echo " Converted MRC: [raw]   To: [spider]"

 ELSE     
    SYS               ; 
      echo " Unsupported input format: {***[format]}"

 ENDIF

; ----------- Rezip if necessary -------------------

 IF ( [zip]*[format] > 0) THEN   
    SYS                          ; Both tif & zip flags set
      gzip [raw]

    SYS               
      echo " Re-zipped: [raw]"
 
 ELSEIF ([zip].EQ.1) THEN        ;
    SYS                          ; Recompress the file
      gzip [raw].$DATEXT

    SYS               
      echo " Re-zipped: [raw].$DATEXT"
 ENDIF

 ; ----------- Size reduction, if any -------------------


 IF ( [dec] < 1) THEN
    UD 16,[dec]        ; Get reduction factor from param file
      [params]           ; Param file   (input)
 ENDIF

 IF ( [dec] > 1) THEN   ; Reduction

   DC S                ; Reduce size
     [spider]
     _1                ; File  (input)
     [dec],[dec]       ; Reduction

   CP                  ; Copy
     _1                ; File  (input)
     [spider]          ; File  (output)

    SYS               
      echo " Decimated: [raw]   By: {**[dec]}"
 ENDIF

 RE
 ; </pre></body></html>

; Modified 2016-01-22
;    2016-01-22 (trs) -- flips XMIPP-converted MRC file
;    2016-01-20 (trs) -- using XMIPP to convert MRC to SPIDER
 